-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c, stmt) {
  case stmt of
    Skip  -> c
  | Assn  (s, expr)  -> 
      [c[0] <- [s, evalExpr (c[0], expr)], c[1]]
  | Read  (s)  ->
      var n;
	  n := readWorld(c[1]);
	  [c[0] <- [s, n[0]], n[1]]
  | Write (exp)  -> 
      [c[0], writeWorld(evalExpr (c[0], exp), c[1])]
  | Seq   (stmt1, stmt2)  -> 
      eval(eval(c, stmt1), stmt2) 

  | While (exp, st) ->
			if evalExpr (c[0], exp) != 0 then eval (eval (c, st), stmt) else c fi
  | If (exp, s1, s2) -> 
      if evalExpr (c[0], exp) != 0 then eval (c, s1) else eval (c, s2) fi 
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
