-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval ([st, world], stmt) {
  case stmt of
      Assn (lvalue, expr) -> [st <- [lvalue, evalExpr(st, expr)], world]
    | Seq (s1, s2) -> eval(eval([st, world], s1), s2)
    | Skip -> [st, world]
    | Read (rvalue) -> case readWorld(world) of
      [value, newWorld] -> [st <- [rvalue, value], newWorld]
    esac
    | Write (expr) -> [st, writeWorld(evalExpr(st, expr), world)]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
