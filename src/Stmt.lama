-- Statement evaluator.
import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
fun eval ([s, w], stmt) {
   case stmt of
      Assn (st, expr) -> [s <- [st, evalExpr(s, expr)], w]
   |  Seq (lst, rst) -> eval(eval([s, w], lst), rst)   
   |  Skip -> [s, w]
   |  Read (st) ->
      case readWorld(w) of 
         [value, world] -> [s <- [st, value], world]
      esac
   |  Write (expr) -> [s, writeWorld(evalExpr(s, expr), w)]
   esac
}
-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
   eval([emptyState, createWorld(input)], stmt).snd.getOutput
}