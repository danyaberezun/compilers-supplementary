-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval ([state, world], stmt) {
  case stmt of 
    Assn(name, exp) -> [state <- [name, evalExpr(state, exp)], world]
  | Seq(stmt1, stmt2) -> eval(eval([state, world], stmt1), stmt2)
  | Skip -> [state, world]
  | Write(exp) -> [state, writeWorld(evalExpr(state, exp), world)]
  | Read(name) -> case readWorld(world) of
      [value, new_world] -> [state <- [name, value], new_world]
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
