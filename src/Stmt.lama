-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[s, w], stmt) {
  case stmt of
    Assn (x, e) -> [s <- [x, evalExpr (s, e)], w]
  | Seq (s1, s2) -> eval (eval (c, s1), s2)
  | Skip -> c
  | Read (x) ->
    case readWorld (w) of
      [v, wn] -> [s <- [x, v], wn]
    esac
  | Write (e) -> [s, writeWorld (evalExpr (s, e), w)]
  | While (e, s1) ->
    while case c of [s, w] -> evalExpr (s, e) esac do
    	  c := eval (c, s1)
    od;
    c
  | DoWhile (s1, e) ->
    do
    	  c := eval (c, s1)
    while case c of [s, w] -> evalExpr (s, e) esac od;
    c
  | If (e, s1, s2) ->
    if evalExpr (s, e) then
       eval (c, s1)
    else
       eval (c, s2)
    fi
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}


