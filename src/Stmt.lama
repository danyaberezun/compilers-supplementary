-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[st, w], stmt) {
	case stmt of
	  Assn (name, expr) -> [st <- [name, evalExpr(st, expr)], w]
   | Seq (s1, s2) -> eval(eval(c, s1), s2)
   | Skip -> c
   | Read (name) -> let [n, newWorld] = readWorld(w) in [st <- [name, n], newWorld] 
   | Write (expr) -> [st, writeWorld(evalExpr(st, expr), w)]
   | If (expr, s1, s2) -> if evalExpr(st, expr) != 0 then eval(c, s1) else eval(c, s2) fi
   | While (expr, s) -> if evalExpr(st, expr) != 0 then eval(eval(c, s), stmt) else c fi
   | DoWhile (s, expr) -> eval(eval(c, s), While (expr, s))
   esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
