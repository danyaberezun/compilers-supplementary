-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c, stmt) {
  case c of [s, w] ->
    case stmt of
        Assn (strr, expr) -> [s <- [strr, evalExpr (s, expr)], w]
      | Seq (stmt1, stmt2) -> eval(eval(c, stmt1), stmt2)
      | Skip -> c
      | Read (strr) ->
        case readWorld (w) of
            [z, w1] -> [s <- [strr, z], w1]
          | _ -> failure ("no world")
        esac
      | Write (expr) -> [s, writeWorld(evalExpr(s, expr), w)]
    esac
    | _ -> failure ("no configuration")
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}