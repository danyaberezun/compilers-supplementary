-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[s, w], stmt) {
  case stmt of 
    Assn (x, e) -> [s <- [x, evalExpr(s, e)], w]
    | Seq (s1, s2) -> eval(eval(c, s1), s2)
    | Skip -> c 
    | Read (v) -> case readWorld(w) of 
                    [n, w] -> [s <- [v, n], w]
                  esac
    | Write (e) -> [s, writeWorld(evalExpr(s, e), w)]
    | While (e, s1) -> case evalExpr(s, e) of 
        0 -> c 
        | _ -> eval(eval(c, s1), While (e, s1))
      esac
    | If (e, s1, s2) -> case evalExpr(s, e) of 
        0 -> eval(c, s2)
        | _ -> eval(c, s1)
      esac
    |  DoWhile (s1, e) -> eval(eval(c, s1), While(e, s1))
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
