-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[st, w], stmt) {
  case stmt of
    Assn (s, e) -> [st <- [s, evalExpr (st, e)], w]
  | Seq (s1, s2) -> eval (eval ([st, w], s1), s2)
  | Skip -> [st, w]
  | Read (s) -> 
    case readWorld (w) of
      [n, w1] -> [st <- [s, n], w1]
    esac
  | Write (e) -> [st, writeWorld (evalExpr (st, e), w)]
  | If (e, s1, s2) -> if evalExpr (st, e) != 0 then eval (c, s1) else eval(c, s2) fi
  | While (e, s) -> if evalExpr (st, e) != 0 then eval (eval (c, s), While (e, s)) else c fi
  | DoWhile (s, e) ->
    case eval (c, s) of
      c'@[st', w'] -> if evalExpr (st', e) != 0 then eval (c', DoWhile (s, e)) else c' fi
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
