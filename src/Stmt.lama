-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[s, w], stmt) {
  case stmt of
      Assn (x, e) -> [s <- [x, evalExpr (s, e)], w]
    | Seq  (stmt1, stmt2) ->
        var cc = eval (c, stmt1);
        eval (cc, stmt2)
    | Skip -> c
    | Read (x) -> case readWorld (w) of [n, ww] -> [s <- [x, n], ww] esac
    | Write (e) ->
        var n = evalExpr (s, e);
        [s, writeWorld (n, w)]
    | If (e, stmt1, stmt2) ->
        var n = evalExpr (s, e);
        eval (c, if n then stmt1 else stmt2 fi)
    | While (e, stmt) ->
        while evalExpr (c [0], e) do
          c := eval (c, stmt)
        od;
        c
    | DoWhile (stmt, e) ->
        do
          c := eval (c, stmt)
        while evalExpr (c [0], e) od;
        c
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
