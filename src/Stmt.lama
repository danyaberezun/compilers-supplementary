-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval ([state, world], stmt) {
  case stmt of 
    Assn (v, e) -> 
        [state <- [v, evalExpr (state, e)], world]
  | Seq (s1, s2) -> eval(eval([state, world], s1), s2)
  | Skip -> [state, world] 
  | Read (v) -> 
      case readWorld(world) of [value, w] -> 
        [state <- [v, value], w]
      esac
  | Write (e) -> 
      [state, writeWorld(evalExpr(state, e), world)]
  | While (e, b) -> 
      case evalExpr(state, e) of 
        0 -> [state, world]
      | _ -> eval(eval([state, world], b), While(e, b)) 
      esac 
  | DoWhile (b, e) -> eval(eval([state, world], b), While(e, b))
  | If (e, s1, s2) -> 
    case evalExpr(state, e) of 
        0 -> eval([state, world], s2)
      | _ -> eval([state, world], s1)
    esac
  esac 
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
