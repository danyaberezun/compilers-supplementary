-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[state, world], stmt) {
   case stmt of 
      Skip -> c |
      Assn (v, e) -> [state <- [v, evalExpr(state, e)], world] |
      Read (x) -> case readWorld(world) of [value, new_world] -> [state <- [x, value], new_world] esac |
      Write (e) -> [state, writeWorld(evalExpr(state, e), world)] |
      Seq (s1, s2) -> eval(eval(c, s1), s2) |
      If (e, s1, s2) -> 
         if evalExpr(state, e) == 0
         then eval(c, s2)
         else eval(c, s1)
         fi |
      While (e, s) -> 
         if evalExpr(state, e) == 0
         then c
         else eval(eval(c, s), stmt)
         fi |
      DoWhile (s, e) -> eval(eval(c, s), While (e, s))
   esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
   eval([emptyState, createWorld(input)], stmt).snd.getOutput
}
