-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |


fun eval (c@[st, w], stmt) {
  case stmt of 
    Assn (x, e)                     -> [st <- [x, evalExpr (st, e)], w]
  | Skip                            ->  c
  | Seq (stmt1, stmt2)              -> 
      case eval (c, stmt1) of
        c2 -> eval (c2, stmt2)
      esac
  | Read (x)                        -> 
      case readWorld (w) of
        [z, w2] -> [st <- [x, z], w2]
      esac
  | Write (expr)                    -> [st, writeWorld ((evalExpr (st, expr)), w)] 
  | _                               -> failure ("Not valid statement")
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
