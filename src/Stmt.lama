-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |
fun eval (c@[st, w], stmt) {
    case stmt of
          Assn(s, e) -> [st <- [s, evalExpr(st, e)], w]
        | Seq(st1, st2) -> eval(eval(c, st1), st2)
        | Skip -> c
        | Read(s) -> let [inp, new_w] = readWorld(w) in [st <- [s, inp], new_w]
        | Write(e) -> [st, writeWorld(evalExpr(st, e), w)]
        | If(con, t, e) -> if evalExpr(st, con) then eval(c, t) else eval(c, e) fi
        | While(con, b) -> if evalExpr(st, con) then eval(c, Seq(b, stmt)) else c fi
        | DoWhile(b, con) -> eval(c, Seq(b, While(con, b)))
    esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
