-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[state, world], stmt) {
  case stmt of
    Skip        -> c
  | Seq (a, b)  -> eval (eval (c, a), b)
  | Assn (x, e) -> [state <- [x, evalExpr (state, e)], world]
  | Read (x)    -> case readWorld (world) of
                     [n, newWorld] -> [state <- [x, n], newWorld]
                   esac
  | Write (e)   -> [state, writeWorld(evalExpr (state, e), world)]
  | If (cond, thenStmt, elseStmt) -> eval (c, if evalExpr (state, cond) != 0 then thenStmt else elseStmt fi)
  | loop@(While (cond, act)) -> if evalExpr (state, cond) != 0 then eval (eval(c, act), loop) else c fi
  | loop@(DoWhile (act, cond)) -> case eval(c, act) of
                                    c@[state, _] -> if evalExpr (state, cond) != 0 then eval (c, loop) else c fi
                                  esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
