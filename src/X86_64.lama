-- X86 codegeneration interface
-- We use stack machine programs as intermediate representation
import SM;
import Collection;
import List;
import Buffer;
import Fun;
import Manifest;

fun min (x, y) {
  if x > y then y else x fi
}

fun max (x, y) {
  if x > y then x else y fi
}

-- Assembler language interface
-- The registers:
var regs = ["%rdi", "%rsi", "%rdx", "%rcx",
            "%r8" , "%r9" , "%r10", "%r11",
	    "%r12", "%r13", "%r14", "%r15",
	    "%rbx", "%rax", "%rbp", "%rsp"];

-- We can not freely operate with all register; only with 13 by now;
-- we also notify that the 6 first register are used to
-- pass arguments to a function
var nRegs    = regs.length - 4,
    nRegArgs = 6;

-- For convenience we define the following synonyms for the registers: 
var rdi = R (0),
    rsi = R (1),
    rdx = R (2),
    rcx = R (3),
    r8  = R (4),
    r9  = R (5),
    r10 = R (6),
    r11 = R (7),
    r12 = R (8),
    r13 = R (9),
    r14 = R (10),
    r15 = R (11),
    rbx = R (12),
    rax = R (13),
    rbp = R (14),
    rsp = R (15);

-- We need to know the word size to calculate offsets correctly
var wordSize = 8;

-- We need to distinguish the following operand types: 
--    R (int)        -- hard register                    
--    S (int)        -- a position on the hardware stack 
--    M (string)     -- a named memory location          
--    L (int/string) -- an immediate operand             
--    I (int, opnd)  -- an indirect operand with offset

-- Some x86 instruction (we do not need all of them):
--   Mov   (opnd, opnd)         -- copies a value from the first to the second operand  
--   Lea   (opnd, opnd)         -- loads an address of the first operand into the second
--   Binop (string, opnd, opnd) -- makes a binary operation; note, the first operand   
--                                 designates x86 operator, not the source language one
--   IDiv  (opnd)               -- x86 integer division, see instruction set reference 
--   Cltd                       -- see instruction set reference           
--   Set   (string, string)     -- sets a value from flags; the first operand is the   
--                                 suffix, which determines the value being set, the
--                                 the second --- (sub)register name
--   Jmp   (string)             -- unconditional jump to a label
--   CJmp  (string, string)     -- conditional jump to a label
--   Label (string)             -- a label
--   Push  (opnd)               -- pushes the operand on the hardware stack
--   Pop   (opnd)               -- pops from the hardware stack to the operand
--   Call  (string)             -- calls a function by its name 
--   Ret                        -- returns from a function
--   Meta  (string)             -- metainformation (declarations, etc.)
                                                               
-- Machine instruction printer
fun insnString (insn) {
  
  fun binopString (op) {
    case op of
      "+"   -> "addq"
    | "-"   -> "subq"
    | "*"   -> "imulq"
    | "&&"  -> "andq"
    | "!!"  -> "orq" 
    | "^"   -> "xorq"
    | "cmp" -> "cmpq"
    esac
  }

  fun opndString (opnd) {
    case opnd of
      R (i)      -> regs [i]
    | S (i)      -> if i >= 0
                    then sprintf ("-%d(%%rbp)", (i+1) * wordSize)
                    else sprintf ("%d(%%rbp)", wordSize * 2 + (-1-i) * wordSize)
                    fi

    | M (x)      -> sprintf ("%s(%rip)", x)
    | L (i@#val) -> sprintf ("$%d", i)
    | L (i@#str) -> sprintf ("%s", i)
    | I (0, x)   -> sprintf ("(%s)", opndString (x))
    | I (n, x)   -> sprintf ("%d(%s)", n, opndString (x))
    esac
  }
  
  case insn of
    Cltd               -> "\tcqo\n"
  | Set   (suf, s)     -> sprintf ("\tset%s\t%s\n", suf, s)
  | Sal   (s)          -> sprintf ("\tsalq\t%s\n", s)
  | IDiv  (s1)         -> sprintf ("\tidivq\t%s\n", opndString (s1))
  | Binop (op, s1, s2) -> sprintf ("\t%s\t%s,\t%s\n", binopString (op), opndString (s1), opndString (s2))
  | Mov   (s1, s2)     -> sprintf ("\tmovq\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Lea   (s1, s2)     -> sprintf ("\tleaq\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Push  (s)          -> sprintf ("\tpushq\t%s\n", opndString (s))
  | Pop   (s)          -> sprintf ("\tpopq\t%s\n", opndString (s))
  | Ret                -> "\tret\n"
  | Call  (p)          -> sprintf ("\tcall\t%s\n", p)
  | Label (l)          -> sprintf ("%s:\n", l)
  | Jmp   (l)          -> sprintf ("\tjmp\t%s\n", l)
  | CJmp  (c, l)       -> sprintf ("\tj%s\t%s\n", c, l)
  | Meta  (m)          -> m
  esac
}

-- Environment for symbolic interpreter
-- An environment holds
--    a symbolic stack
--    a maximal stack depth reached so far
--    a set of global variable names
--    a barrier condition (set up right after an unconditional jump is interpreter)
--    a stack map which maps labels to symbolic stacks

fun makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals) {
  -- Returns an internal name for a global variable
  fun globalName (name) {
    "global_" ++ name
  }

  -- Returns a string representation of the environment
  fun envString () {
    sprintf ("Stack     : %s\nStackSlots: %d\nGlobals   : %s\n", stack.string, stackSlots, elements (globals).string)
  }
  
  -- Allocates a new position on the symbolic stack;
  -- returns a pair: a location for allocated item and
  -- an updated environment
  fun alloc () {
    case 
      case stack of
        {}        -> [rdi, 0]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> if n < nRegs then [R (n+1), stackSlots] else [S (nLocals), nLocals + 1] fi
      | _         -> [S (nLocals), nLocals + 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals, barrier, stackMap, fLabel, nLocals)]esac
  }

  -- Pushes an item on the symbolic state; returns an updated envirtonment
  fun push (y) {
    makeEnv (y : stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)
  }

  -- Pops one item from the symbolic stack; returns a pair: a popped
  -- item and an updated environment
  fun pop () {
    case stack of
      x : stack -> [x, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Pops two items from the symbolic stack; returns a triple:
  -- popped items and an undated environment
  fun pop2 () {
    case stack of
      x : y : stack -> [x, y, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Peeks the top of the symbolic stack
  fun peek () {
    stack.fst
  }
  
  -- Adds a global variable; returns an updated environment
  fun addGlobal (name) {
    makeEnv (stack, stackSlots, addSet (globals, globalName (name)), barrier, stackMap, fLabel, nLocals)
  }

  -- References an allocation
  fun loc (alloc) {
    case alloc of
      Glb (x) -> M (globalName (x))
    | Arg (i) -> if i < nRegArgs then R (i) else S (-1-(i - nRegArgs)) fi
    | Loc (i) -> S (i)
    esac
  }
  
  -- Gets a list of global variables from the environment
  fun getGlobals () {
    globals.elements
  }

  -- Gets a maximal stack size from the environment
  fun getStackSize () {
    stackSlots
  }

  -- Gets barrier condition
  fun isBarrier () {
    barrier
  }

  -- Sets barrier condition
  fun setBarrier () {
    makeEnv (stack, stackSlots, globals, true, stackMap, fLabel, nLocals)
  }

  -- Associates the current symbolic state with a label
  fun setStack (l) {
    makeEnv (stack, stackSlots, globals, false, addMap (stackMap, l, stack), fLabel, nLocals)
  }

  -- Retrieves a stack for a label
  fun retrieveStack (l) {
    case findMap (stackMap, l) of
      None         -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    | Some (stack) -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    esac
  }

  -- Sets the label of current function
  fun enterFunction (fLabel, na, nl) {
    fun enter (env, n) {
      if n == 0
      then env
      else enter (env.allocate [1], n - 1)
      fi
    }
    
    enter (makeEnv ({}, 0, globals, false, emptyMap (compare), fLabel, nl), min (na, nRegArgs))
  }

  -- Gets the label of current function
  fun currentFunction () {
    fLabel
  }

  -- Gets the list of live registers past given symbolic stack depth
  fun liveRegisters (depth) {
    fun inner (d, acc, st) {
      case st of
        {}             -> acc
      | (r@R (_)) : tl -> inner (d+1, if d >= depth then r : acc else acc fi, tl)
      | _         : tl -> inner (d+1, acc, tl)
      esac
    }

    inner (0, {}, stack)
  }

   -- Gets a local static size for the current function
  fun staticSize () {
    nLocals + stackSlots
  }

  fun freeReg (R (i)) {
    stack.size <= i  
  }

  fun stackDepth () {
    stack.size
  }
  
  [envString,
   alloc,
   push,
   pop,
   pop2,
   addGlobal,
   loc,
   getGlobals,
   getStackSize,
   peek,
   isBarrier,
   setBarrier,
   setStack,
   retrieveStack,
   enterFunction,
   currentFunction,
   liveRegisters,
   staticSize,
   freeReg,
   stackDepth]
}

-- Exported accessors
fun envString (env) {
  env [0] ()
}

fun allocate (env) {
  env [1] ()
}

fun push (env, x) {
  env [2] (x)
}

fun pop (env) {
  env [3] ()
}

fun pop2 (env) {
  env [4] ()
}

fun addGlobal (env, name) {
  env [5] (name)
}

fun loc (env, name) {
  env [6] (name)
}

fun getGlobals (env) {
  env [7] ()
}

fun getStackSize (env) {
  env [8] ()
}

fun peek (env) {
  env [9] ()
}

fun isBarrier (env) {
  env [10] ()
}

fun setBarrier (env) {
  env [11] ()
}

fun setStack (env, l) {
  env [12] (l)
}

fun retrieveStack (env, l) {
  env [13] (l)
}

fun enterFunction (env, fLabel, nA, nL) {
  env [14] (fLabel, nA, nL)
}

fun currentFunction (env) {
  env [15] ()
}

fun liveRegisters (env, depth) {
  env [16] (depth)
}

fun staticSize (env) {
  env [17] ()
}

fun freeReg (env, r) {
  env [18] (r)
}

fun stackDepth (env) {
  env [19] ()
}

-- Creates an initial environment
fun initEnv () {
  makeEnv ({}, 0, emptySet (compare), false, emptyMap (compare), "", 1)
}

-- Codegeneration helper functions
-- Generates code section
fun codeSection (text) {
  singletonBuffer (Meta ("\t.text\n")) <+> text
}

-- Generates data section
fun dataSection (text) {
  singletonBuffer (Meta ("\t.data\n")) <+> text
}

-- Generates data definition
fun dataDef (name) {
  Meta (sprintf ("%s:\t.quad\t0\n", name))
}

-- Generates function prologue
fun prologue (fLabel) {
  singletonBuffer (Push  (rbp)) <+
  Mov   (rsp, rbp) <+
  Binop ("-", L (sprintf ("$%s_SIZE", fLabel)), rsp)  
}

fun epilogue (env) {
  var metaDef = Meta (sprintf ("\t.set\t%s_SIZE,\t%d\n", env.currentFunction, env.staticSize * wordSize));

  if compare (env.currentFunction, "main") == 0
  then [env, singletonBuffer (Mov (rbp, rsp)) <+ Pop (rbp) <+ Binop ("^", rax, rax) <+ Ret <+ metaDef]
  else case env.pop of
         [y, env] -> [env, singletonBuffer (Mov (rbp, rsp)) <+ Pop (rbp) <+ Mov (y, rax) <+ Ret <+ metaDef]
       esac
  fi
}

-- Checks if an operand resides on a stack
fun stackOpnd (opnd) {
  case opnd of
    S (_) -> true
  | _     -> false
  esac      
}

-- Checks if an operand resides in memory
fun memOpnd (opnd) {
  case opnd of
    S (_) -> true
  | M (_) -> true
  | _     -> false
  esac
}

-- Enframes a rdx-using code with save/restore
-- if RDX is occupied
fun withRDX (env, code) {
  if env.freeReg (rdx)
  then code  
  else singletonBuffer (Push (rdx)) <+> code <+ Pop (rdx)
  fi
}

-- Generates a move between locations, using
-- intermediate register if needed
fun move (from, to) {
  if memOpnd (from) && memOpnd (to)
  then singletonBuffer (Mov (from, rax)) <+ Mov (rax, to)
  else singletonBuffer (Mov (from, to))
  fi
}

-- Gets push/pops for live registers (as buffers in
-- a proper order)
fun savecode (env, nA) {
  foldr (fun ([pushs, pops], r) {
           [singletonBuffer (Push (r)) <+> pushs, pops <+ Pop (r)]
         },
	 [emptyBuffer (), emptyBuffer ()],
	 env.liveRegisters (nA)
  )
}

-- Generates a static call
-- Some guidelines for generating function calls:
--
-- 1. generate instructions to save live registers on the X86 stack (use
--    env.liveRegisters (number of arguments);
-- 2. generate instructions to move actual parameters from the symbolic
--    stack to the proper places:
--    a. first nRegArgs arguments go to the registers (reg[0]-reg[nRegArgs-1]).
--    b. the rest of arguments go to the regular X86 stack.
--    c. sometemes no work is needed (all arguments are already in their
--       places);
--    d. pay attention to the order of arguments placements 
-- 3. generate the call itself;
-- 4. discard those parameters that were passed through stack from the stack;
-- 5. restore saved live registers.         
fun call (env, fLabel, nA) {
  fun moveArgs(env, nA) {
    if nA == 0 then
      [env, emptyBuffer()]
    elif stackDepth(env) == nA then 
      case env.pop of
        [_, env] -> moveArgs(env, nA - 1)
      esac
    else
      case env.pop of
        [x, env] -> 
          case moveArgs(env, nA - 1) of
            [env, code] ->
              if nA <= nRegArgs then 
                [env, code <+ Mov(x, R(nA - 1))]
              else 
                [env, code <+ Push(x)]
              fi
          esac
      esac
    fi
  }

  let [pushs, pops] = savecode(env, nA) in (var ra = min(nRegArgs, nA);
  var sa = nA - ra;
  let [env, code] = moveArgs(env, nA) in
  let [x, env] = allocate(env) in [env, pushs <+> code
                                              <+  Call(fLabel)
                                              <+  Binop("+", L(sa * wordSize), rsp)
                                              <+> pops
                                              <+  Mov(rax, x)])
}

-- Gets a suffix for Set instruction from
-- source language comparison operator
fun suffix (op) {
  case op of
    "<"  -> "l"
  | "<=" -> "le"
  | "==" -> "e"
  | "!=" -> "ne"
  | ">=" -> "ge"
  | ">"  -> "g"
  esac
}

-- Compiles stack machine code into a list of x86 instructions. Takes an environment
-- and stack machine code, returns an updated environment and x86 code.
fun compile (env, code) {
  foldl (
    fun ([env, scode], i) {
      var code = scode <+ Meta ("# " ++ showSMInsn (i) ++ "\n");

      -- printf ("compiling %s\n", showSMInsn (i));
      
      case i of      
        READ ->
          let [env, callCode] = call (env, "Lread", 0) in
	  [env, code <+> callCode]
      | WRITE ->
          let [env, callCode] = call (env, "Lwrite", 1) in
	  [env, code <+> callCode]
	  
      (* Assignment        
         -- Some guidelines for generating functions:
         --
         -- 1. generate proper prologue for BEGIN instruction (use "prologue" helper); use
         --    env.enterFunction to create a proper environment;
         -- 2. generate epilogue for END instruction.
      *)
        
      | CONST (n) ->
          case env.allocate of
            [s, env] -> [env, code <+> move (L(n), s)]
          esac
      | LD (x) ->
          case env.allocate of
            [s, env] -> [env, code <+> move (env.loc (x), s)]
          esac
      | ST (x) ->
          case env.pop of
            [s, env] -> [push(env, loc (env, x)), code <+> move(s, loc(env, x))]
          esac
      | BINOP(op) -> 
          case env.pop2 of [y, x, env] ->
              case op of
                "+"  -> [push(env, x), code <+> if memOpnd(y) && memOpnd(x) then singletonBuffer(Mov(y, rax)) <+ Binop(op, rax, x) else singletonBuffer(Binop(op, y, x)) fi]
              | "-"  -> [push(env, x), code <+> if memOpnd(y) && memOpnd(x) then singletonBuffer(Mov(y, rax)) <+ Binop(op, rax, x) else singletonBuffer(Binop(op, y, x)) fi]
              | "*"  -> [push(env, x), code <+> if memOpnd(y) && memOpnd(x) then singletonBuffer(Mov(y, rax)) <+ Binop(op, rax, x) else singletonBuffer(Binop(op, y, x)) fi]
              | "/"  ->
                    case y of
                          R(2) -> [push(env, x), code <+> withRDX(env, move(rsi, rax) <+ Mov(rdx, rsi) <+ Cltd <+ IDiv(rsi) <+ Mov(rax, rsi))]
                        | _    -> [push(env, x), code <+> withRDX(env, move(x, rax) <+ Cltd <+ IDiv(y) <+ Mov(rax, x))]
                    esac
              | "%"  ->
                    case y of
                          R(2) -> [push(env, x), code <+> withRDX(env, move(rsi, rax) <+ Mov(rdx, rsi) <+ Cltd <+ IDiv(rsi) <+ Mov(rdx, rsi))]
                        | _    -> [push(env, x), code <+> withRDX(env, move(x, rax) <+ Cltd <+ IDiv(y) <+ Mov(rdx, x))]
                    esac
              | "!!" ->
                    case y of
                          R(2) -> [push(env, x), code 
                                        <+> withRDX(env, singletonBuffer(Binop("^", rax, rax))
                                        <+ Binop("cmp", L(0), x)
                                        <+ Set(suffix("!="), "%al")
                                        <+ Binop("^", rsi, rsi)
                                        <+ Binop("cmp", L(0), y)
                                        <+ Set(suffix("!="), "%sil")
                                        <+ Binop(op, rax, rsi)
                                        <+ Mov(rsi, x))]
                        | _    -> [push(env, x), code
                                        <+> withRDX(env, singletonBuffer(Binop("^", rax, rax))
                                        <+ Binop("cmp", L(0), x)
                                        <+ Set(suffix("!="), "%al")
                                        <+ Binop("^", rdx, rdx)
                                        <+ Binop("cmp", L(0), y)
                                        <+ Set(suffix("!="), "%dl")
                                        <+ Binop(op, rax, rdx)
                                        <+ Mov(rdx, x))]
                    esac
              | "&&" ->
                    case y of
                          R(2) -> [push(env, x), code 
                                        <+> withRDX(env, singletonBuffer(Binop("^", rax, rax))
                                        <+ Binop("cmp", L(0), x)
                                        <+ Set(suffix("!="), "%al")
                                        <+ Binop("^", rsi, rsi)
                                        <+ Binop("cmp", L(0), y)
                                        <+ Set(suffix("!="), "%sil")
                                        <+ Binop(op, rax, rsi)
                                        <+ Mov(rsi, x))]
                        | _    -> [push(env, x), code
                                        <+> withRDX(env, singletonBuffer(Binop("^", rax, rax))
                                        <+ Binop("cmp", L(0), x)
                                        <+ Set(suffix("!="), "%al")
                                        <+ Binop("^", rdx, rdx)
                                        <+ Binop("cmp", L(0), y)
                                        <+ Set(suffix("!="), "%dl")
                                        <+ Binop(op, rax, rdx)
                                        <+ Mov(rdx, x))]
                    esac
              | _    ->
                    case y of
                          R(2) -> [push(env, x), code <+ Binop("^", rbx, rbx) <+ Binop("cmp", y, x) <+ Set(suffix(op), "%bl") <+ Mov(rbx, x)]
                        | R(1) -> [push(env, x), code <+ Binop("^", rbx, rbx) <+ Binop("cmp", y, x) <+ Set(suffix(op), "%bl") <+ Mov(rbx, x)]
                        | _    -> [push(env, x), code <+ Binop("^", rax, rax) <+ Binop("cmp", y, x) <+ Set(suffix(op), "%al") <+ Mov(rax, x)]
                    esac
              esac
          esac
      | LABEL (l) ->
          if isBarrier(env) then
            [retrieveStack(env, l), code <+ Label(l)]
          else
            [env, code <+ Label (l)]
          fi
      | JMP (l) ->
          [setBarrier(setStack(env, l)), code <+ Jmp (l)]
      | CJMP (c, l) ->
          case env.pop of
            [x, env] -> [setStack(env, l), code <+ Mov(L(0), rax) <+ Binop("+", rax, x) <+ CJmp (c, l)]
          esac
      | LDA (x) ->
          case env.allocate of
            [s, env] -> [env, code <+> if memOpnd(s) then (singletonBuffer(Lea(loc(env, x), rax)) <+ Mov(rax, s))
                                        else singletonBuffer(Lea(loc(env, x), s)) fi]
          esac
      | STI ->
          case env.pop2 of
            [v, addr, env] ->                                       
              [push(env, v), code <+> withRDX(env, (if memOpnd(v) then
              if memOpnd(addr) then singletonBuffer(Mov(v, rax)) <+> singletonBuffer(Mov(addr, rdx)) <+ Mov(rax, I(0, rdx))
              else singletonBuffer(Mov(v, rax)) <+> singletonBuffer(Mov(rax, I(0, addr))) fi
              else if memOpnd(addr) then singletonBuffer(Mov(addr, rdx)) <+ Mov(v, I(0, rdx))
              else singletonBuffer(Mov(v, I(0, addr))) fi fi ))]
          esac
      | DROP ->
          case env.pop of 
            [_, env] -> [env, code]
          esac
      | CALL (f, n) ->
          case call(env, f, n) of
            [env, fcode] -> [env, code <+> fcode]
          esac
      | GLOBAL (x) ->
          [addGlobal(env, x), code]
      | END ->
          case env.epilogue of
            [env1, ecode] -> [env1, code <+> ecode]
          esac
      | BEGIN (fn, na, nl) ->
          [enterFunction (env, fn, na, nl), code <+> prologue(fn)]
      | _ -> failure ("codegeneration for instruction %s is not yet implemented\n", i.string)
      esac
    }, [env, emptyBuffer ()], code)
}

-- A top-level codegeneration function. Takes a driver's environment and a stack machine program,
-- compiles the program into machine code, and compiles the machine code into an executable
public fun compileX86 (args, code) {
  case compile (initEnv (), code) of
    [env, code] ->
       var asmFile = args.getBaseName ++ ".s",
           runtime = case getEnv ("LAMA_RUNTIME") of
                       #val  -> "../runtime64/"
                     | path  -> path
                     esac ++ "/runtime.o";
       
       fwrite (asmFile,
               map (insnString, 
                    getBuffer $
                      singletonBuffer (Meta ("\t.global\tmain\n")) <+>
                      dataSection (listBuffer $ map (dataDef, getGlobals (env))) <+>
		      codeSection (code)
                    ).stringcat);
                    
       system ({"gcc -g -o ", args.getBaseName, " ", runtime, " ", asmFile}.stringcat)
  esac
}
