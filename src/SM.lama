-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, conf@[st, s, w], insns) {
  case insns of 
    {} -> conf
    | ins:tail -> 
      case ins of 
        READ -> case readWorld(w) of 
                [n, w] -> eval(env, [n:st, s, w], tail)
              esac
        | WRITE -> case st of 
                    n:st -> eval(env, [st, s, writeWorld(n, w)], tail)
                  esac
        | BINOP (op) -> case st of 
                      x:y:st -> eval(env, [evalBinop(op, y, x) : st, s, w], tail)
                      esac
        | LD (x) -> eval(env, [s(x):st, s, w], tail)
        | ST (x) -> case st of 
                      n:st -> eval(env, [st, s <- [x, n], w], tail)
                    esac
        | CONST (n) -> eval(env, [n:st, s, w], tail)
        | LABEL (_) -> eval(env, conf, tail)
        | JMP (l) -> eval(env, conf, env.fromLabel(l))
        | CJMP (cond, l) -> case cond of 
            "z" -> case st of 
              n:st -> eval(env, [st, s, w], if n == 0 then env.fromLabel(l) else tail fi)
            esac
            | "nz" -> case st of 
              n:st -> eval(env, [st, s, w], if n != 0 then env.fromLabel(l) else tail fi)
            esac
          esac
      esac
    esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (x) -> singletonBuffer(LD (x))
    | Const (n) -> singletonBuffer(CONST (n))
    | Binop (op, l, r) -> compileExpr(l) <+> compileExpr(r) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (exp, s1, s2) -> case genLabel(env) of 
        [label1, env] -> case compile(lab, env, s1) of 
          [s1WasLabel, env, s1compiled] -> case compile(lab, env, s2) of 
            [s2WasLabel, env, s2compiled] -> [true, env, compileExpr(exp) <+ CJMP("z", label1) 
                                          <+> s1compiled <+ JMP(lab) <+> label(label1, true) <+> s2compiled]
              esac
          esac
      esac
    | While (exp, s1) -> case genLabel(env) of 
        [label1, env] -> case compile(lab, env, s1) of 
            [s1WasLabel, env, s1compiled] -> [s1WasLabel, env, label(label1, true) <+> compileExpr(exp) <+ CJMP("z", lab)
                                          <+> s1compiled <+ JMP(label1) <+> label(lab, true)]
          esac
      esac
    | DoWhile (s1, exp) -> case genLabel(env) of 
        [label1, env] -> case compile(lab, env, s1) of 
            [s1WasLabel, env, s1compiled] -> [s1WasLabel, env, label(label1, true) <+> s1compiled <+> compileExpr(exp) <+ CJMP("nz", label1)]
          esac
      esac
    | _              -> failure ("compileSM not implemented\n")
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
