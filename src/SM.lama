-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

fun evalInsn (c@[stack, state, world], insn) {
    case insn of 
      ST (x) -> case stack of (z : s) -> [s, state <- [x, z], world] esac 
    | LD (x) -> [state(x) : stack, state, world]
    | CONST (x) -> [x : stack, state, world]
    | BINOP (op) -> case stack of (y : (x : s)) -> [evalBinop(op, x, y) : s, state, world] esac
    | READ -> case readWorld(world) of [res, newWorld] -> [res : stack, state, newWorld] esac
    | WRITE -> case stack of (z : s) -> [s, state, writeWorld(z, world)] esac
  esac
}
  
-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
    case insns of 
        {} -> c
      | i : rest -> eval (evalInsn(c, i), rest)
    esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list (of, possibly, lists)
-- of stack machine instructions
fun compileExpr (expr) {
    case expr of
      Var (s) -> { LD(s) }
    | Const (i) -> { CONST(i) }
    | Binop (op, first, second) -> compileExpr(first) +++ compileExpr(second) +++ { BINOP(op) }
    esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
    case stmt of
      Assn (a, exp) -> compileExpr (exp) +++ ( ST (a) : {})
    | Seq (fst, snd) -> compileSM (fst) +++ compileSM (snd)
    | Skip -> {}
    | Read (x) -> READ : ST (x) : {}
    | Write (exp) -> compileExpr (exp) +++ (WRITE : {})
    esac
}
