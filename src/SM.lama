-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[stack, cstack, st, w], insns) {
    case insns of
      {} -> c
    | BINOP (s):ins ->
        case stack of
          y:x:is       -> eval ([evalOp (s, x, y):is, cstack, st, w], ins)
        esac
    | LD  (x):ins      -> eval ([lookup (st, x):stack, cstack, st, w], ins)
    | LDA (x):ins      -> eval ([Ref (x):stack, cstack, st, w], ins)
    | ST  (x):ins   ->
        case stack of
          i:is         -> st.assign (x, i);
                          eval (c, ins)
        esac
    | STI:ins       ->
        case stack of
          i:j:is       -> st.assign (j, i);
                          eval ([i:is, cstack, st, w], ins)
        esac
    | SEXP  (s, n):ins ->
        case take (stack, n) of
          [stack, v] -> eval ([Sexp (s, listArray (v)):stack, cstack, st, w], ins)
        esac
    | CONST (n):ins    -> eval ([n:stack, cstack, st, w], ins)
    | LABEL (s):ins    -> eval (c, ins)
    | JMP   (l):ins    -> eval (c, env.fromLabel (l))
    | CJMP  (c, l):ins ->
        case stack of
          i:is        -> if (compare(c, "z") == 0) == (i == 0) then
                         eval ([is, cstack, st, w], env.fromLabel (l))
                         else
                         eval ([is, cstack, st, w], ins)
                         fi
        esac
    | CALL   (f, n)   :ins -> eval ([stack, [st, ins]:cstack, st, w], env.fromLabel (f))
    | BEGIN  (f, a, l):ins ->
        case stack.take (a) of
          [stack, args] ->
            case makeState (a, l) of
              st -> foldl (fun (x, arg) { st.assign (Arg (x), arg); x + 1 }, 0, args);
                    eval ([stack, cstack, st, w], ins)
            esac
        esac
    | GLOBAL (x):ins       -> st.assign (Glb (x), 0);
                              eval (c, ins)
    | END:ins              -> 
        case cstack of
          [st, cins]:cstack -> eval ([stack, cstack, st, w], cins)
        | _  -> c
        esac
    | DUP:ins       ->
        case stack of
          x:stack     -> eval ([x:x:stack, cstack, st, w], ins)
        esac
    | DROP:ins      ->
        case stack of
          _:stack     -> eval ([stack, cstack, st, w], ins)
        esac
    | STRING (s):ins  -> eval ([s:stack, cstack, st, w], ins)
    | ARRAY  (n):ins  ->
        case take (stack, n) of
          [stack, vs] -> eval ([listArray (vs):stack, cstack, st, w], ins)
        esac
    | STA:ins         ->
        case stack of
          v:Ref (x):stack -> assign (st, x, v);
                             eval   ([v:stack, cstack, st, w], ins)
        | v:i:arr:stack -> 
            case arr of
              Sexp (_, arr) -> arr[i] := v
            | _             -> arr[i] := v
            esac; 
            eval ([v:stack, cstack, st, w], ins)
        esac
    | ELEM:ins        ->
        case stack of
          i:arr:stack -> 
          var v = case arr of
                    Sexp (_, arr) -> arr[i]
                  | _             -> arr[i]
                  esac; 
            eval ([v:stack, cstack, st, w], ins)
        esac
    | BUILTIN (f, n):ins ->
        case take (stack, n) of
          [stack, args] ->
            case evalBuiltin (f, args, w) of
              [v, w] -> eval ([v:stack, cstack, st, w], ins)
            esac
        esac
    esac
  }  
  
  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | Arg (i)       -> Arg (i)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }
  
  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.


-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip       -> [false, env, emptyBuffer ()]
    | Var    (x) -> [false, env, singletonBuffer (LD  (lookupVar (env, x)))]
    | Ref    (x) -> [false, env, singletonBuffer (LDA (lookupVar (env, x)))]
    | Const  (n) -> [false, env, singletonBuffer (CONST (n))]
    | Ignore (e) ->
        case genLabel (env) of
          [lab, env] ->
            case compile (lab, env, e) of
              [used, env, code] -> [false, env, code <+> label (lab, used) <+ DROP]
            esac
        esac
    | Binop (op, e1, e2) ->
        case genLabels (env, 2) of
          [lab1, lab2, env] ->
            case compile (lab1, env, e1) of
              [used1, env, code1] ->
                case compile (lab2, env, e2) of
                  [used2, env, code2] -> [false, env, code1 <+> label (lab1, used1)
                                                            <+> code2
                                                            <+> label (lab2, used2)
                                                            <+  BINOP (op)
                                         ]
                esac
            esac
        esac
    | Set  (s, e) -> compile (lab, env, Assn (Ref (s), e))
    | Assn (x, e) ->
        case genLabels (env, 2) of
          [labX, labE, env] ->
            case compile (labX, env, x) of
              [usedX, env, codeX] ->
                case compile (labE, env, e) of
                  [usedE, env, codeE] -> [false, env, codeX <+> label (labX, usedX)
                                                            <+> codeE
                                                            <+> label (labE, usedE)
                                                            <+  STA
                                         ]
                esac
            esac
        esac
    | Seq (s1, s2) ->
        case genLabel (env) of
          [labS2, env] ->
            case compile (labS2, env, s1) of
              [usedS2, env, codeS1] ->
                case compile (lab, env, s2) of
                  [used, env, codeS2] -> [used, env, codeS1 <+> label (labS2, usedS2) <+> codeS2]
                esac
            esac
        esac
    | If (e, s1, s2) ->
        case genLabels (env, 2) of
          [labE, l, env] -> 
            case compile (labE, env, e) of
              [used, env, codeE] ->
                case compile (lab, env, s1) of
                  [_, env, codeT] ->
                    case compile (lab, env, s2) of
                      [_, env, codeF] -> [true, env, emptyBuffer () <+> codeE
                                                                    <+> label (labE, used)
                                                                    <+  CJMP ("z", l)
                                                                    <+> codeT
                                                                    <+  JMP (lab)
                                                                    <+> label (l, true)
                                                                    <+> codeF]
                    esac
                esac
            esac
        esac
    | While (e, s) ->
        case genLabels (env, 3) of
          [l, labS, labE, env] ->
            case compile (l, env, s) of
              [_, env, codeS] ->
                case compile (labE, env, e) of
                  [used, env, codeE] -> [false, env, emptyBuffer () <+  JMP   (l)
                                                                    <+> label (labS, true)
                                                                    <+> codeS
                                                                    <+> label (l, true)
                                                                    <+> codeE
                                                                    <+> label (labE, used)
                                                                    <+  CJMP  ("nz", labS)
                                        ]
                esac
            esac
        esac
    | DoWhile (b, e) ->
        case genLabels (env, 3) of
          [l, labS, labE, env] ->
            case compile (labS, env, b) of
              [usedS, env, codeS] ->
                case compile (labE, env, e) of
                  [usedE, env, codeE] -> [false, env, emptyBuffer () <+> label (l, true)
                                                                     <+> codeS
                                                                     <+> label (labS, usedS)
                                                                     <+> codeE
                                                                     <+> label (labE, usedE)
                                                                     <+  CJMP  ("nz", l)
                                         ]
                esac
            esac
        esac
    | Scope (defs, e) ->
        case compileDefs (beginScope (env), defs) of
          [env, gs, fs] ->
            case map (fun (g) { GLOBAL (g) }, gs) of
              codeG ->
                case foldl (fun (env, Fun (l, a, b)) { rememberFun (env, l, a, b) }, env, fs) of
                  env ->
                    case compile (lab, env, e) of
                      [used, env, codeE] -> [used, endScope (env), listBuffer (codeG) <+> codeE]
                    esac
                esac
            esac
        esac
    | Call (f, args) ->
        case compileList (env, args) of
          [env, code] ->
            case f of
              "read"   -> [false, env, code <+ BUILTIN (f, 0)]
            | "write"  -> [false, env, code <+ BUILTIN (f, 1)]
            | "length" -> [false, env, code <+ BUILTIN (f, 1)]
            | _        -> case lookupFun (env, f) of
                            Fun (l, a) -> [false, env, code <+ CALL (l, a)]
                          esac
            esac
        esac
    | Array   (es)     ->
        case compileList (env, es) of
          [env, code] -> [false, env, code <+ ARRAY (es.size)]
        esac
    | String  (s)      -> [false, env, singletonBuffer (STRING (s))]
    | Elem    (e1, e2) ->
        case compileList (env, { e1, e2 }) of
          [env, code] -> [false, env, code <+ ELEM]
        esac
    | ElemRef (e1, e2) ->
        case compileList (env, { e1, e2 }) of
          [env, code] -> [false, env, code]
        esac
    | Sexp    (s, es)  ->
        case compileList (env, es) of
          [env, code] -> [false, env, code <+ SEXP (s, es.size)]
        esac 
    esac
  }

  fun compileDefs (env, defs) {
    foldl (
      fun ([env, globals, fs], def) {
        case def of
          Var (x) ->
            case addVars (env, x) of
              env -> if isGlobal (env) then
                     [env, x +++ globals, fs]
                     else
                     [env, globals, fs]
                     fi
            esac
        | Fun (l, a, b) ->
            case genFunLabel (env, l) of
              [lab, env] ->
                case addFun (env, l, lab, size (a)) of
                  env -> [env, globals, Fun (lab, a, b) : fs]
                esac
            esac
        esac
      },
      [env, {}, {}], defs
    )
  }

  fun compileList (env, args) {
    foldl (
      fun ([env, code], arg) {
        case genLabel (env) of
          [lab, env] ->
            case compile (lab, env, arg) of
              [used, env, codeA] -> [env, code <+> codeA <+> label (lab, used)]
            esac
        esac
      },
      [env, emptyBuffer ()], args
    )
  }

  fun compileFuns (env, funs) {
    case foldl (
      fun ([env, code], Fun (l, a, b, st)) {
        case genLabel (beginFun (env, st).addArgs (a)) of
          [lab, env] ->
            case compile (lab, env, b) of
              [used, env, codeS] -> [env, code <+  LABEL (l)
                                               <+  BEGIN (l, size (a), getLocals (env))
                                               <+> codeS
                                               <+> label (lab, used)
                                               <+  END
                                    ]
            esac
        esac
      }, [env, emptyBuffer ()], funs) of 
      [env, code] ->
        case getFuns (env) of
          [f:fs, env]  -> case compileFuns (env, f:fs) of
                            [env, codeFuns] -> [env, code <+> codeFuns]
                          esac
        | _            ->   [env, code]
        esac
    esac
  }

  case genLabel (beginScope (initCompEnv ()).addFun ("read"  , "$read"  , 0)
                                            .addFun ("write" , "$write" , 1)
                                            .addFun ("length", "$length", 1)) of
    [endLab, env] ->
      case compile (endLab, env, stmt) of
        [endLabUsed, env, code] -> getBuffer $
              singletonBuffer (LABEL ("main")) <+ BEGIN ("main", 0, getLocals (env)) 
                <+> code <+> label (endLab, endLabUsed) <+ END 
                <+> (case getFuns (env) of [funs, env] -> compileFuns (env, funs) [1] esac) 
      esac
  esac
}
