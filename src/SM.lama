-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  fun getArr (e) {
     case e of 
        Sexp (_, a) -> a 
      | _ -> e 
     esac
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[stack, cst, st, w], insns) {
     case insns of 
        s:t -> case s of
           READ              -> let [value, w2] = readWorld(w) in eval ([value:stack, cst, st, w2], t)
         | WRITE             -> let value:stack2 = stack in eval([stack2, cst, st, writeWorld(value, w)], t)
         | BINOP  (op)       -> let value1:value2:stack = stack in eval([evalOp (op, value2, value1):stack, cst, st, w], t)
         | LD     (x)        -> eval ([lookup (st, x):stack, cst, st, w], t)
         | ST     (x)        -> case stack of value:_ -> assign (st, x, value); eval([stack, cst, st, w], t) esac
         | CONST  (n)        -> eval ([n:stack, cst, st, w], t)
         | LABEL  (x)        -> eval (c, t)
         | JMP    (x)        -> eval (c, fromLabel (env, x))
         | CJMP   (e, x)     -> let value:stack2 = stack in
            let go = 
               case e of
                  "z"  -> if value == 0 then true else false fi
                | "nz" -> if value != 0 then true else false fi
               esac 
            in if go then eval ([stack2, cst, st, w], fromLabel (env, x)) else eval ([stack2, cst, st, w], t) fi
         | LDA    (x)        -> eval ([Ref (x):stack, cst, st, w], t)
         | STI               -> let value:name:stack2 = stack in assign (st, name, value); eval ([value:stack2, cst, st, w], t)
         | DROP              -> let _:stack2 = stack in eval ([stack2, cst, st, w], t)
         | DUP               -> let value:_ = stack in eval ([value:stack, cst, st, w], t)
         | GLOBAL (x)        -> eval (c, t)
         | BEGIN  (_, a, nL) -> let [stack, nP] = take (stack, a) in eval ([stack, cst, [listArray (nP), initArray (nL, fun (_) {0})], w], t)
         | END               ->
            case cst of
               {} -> eval (c, {})
             | [tail, st2]:cst2 -> eval ([stack, cst2, st2, w], tail)
            esac
         | CALL   (f, _)     -> eval ([stack, [t, st]:cst, st, w], fromLabel (env, f))
         | STRING (x)        -> eval ([x:stack, cst, st, w], t)
         | ARRAY  (n)        -> let [_:stack, elems] = take (stack, n) in eval ([listArray (elems):stack, cst, st, w], t)
         | SEXP   (e, n)     -> let [_:stack, elems] = take (stack, n) in eval ([Sexp (e, listArray (elems)):stack, cst, st, w], t)
         | STA               -> 
            case stack of
               value:Ref(x):Ref(_):stack    -> assign(st, x, value); eval ([value:stack, cst, st, w], t)
             | value:ind:Sexp(_, arr):stack -> arr[ind] := value;    eval ([value:stack, cst, st, w], t)
             | value:ind:arr:stack          -> arr[ind] := value;    eval ([value:stack, cst, st, w], t)
            esac
         | ELEM              -> let ind:arr:stack = stack in eval ([getArr (arr)[ind]:stack, cst, st, w], t)
         | BUILTIN (f, n)    -> let [stack, args] = take (stack, n) in
            let [value, w] = evalBuiltin (f, args, w) in eval ([value:stack, cst, st, w], t)
        esac
     | {}  -> c
     esac
  }

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }
  
  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 1, enterScope (emptyState ()), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }
  
  fun compileMany (lab, env, exprs) {
    case exprs of
      {}     -> [false, env, emptyBuffer ()]
    | {e}    -> compile (lab, env, e)
    | e : es ->
        case genLabel (env) of
          [eLab, env] ->
            case compile (eLab, env, e) of
              [eUsed, env, eCode] ->
                 case compileMany (lab, env, es) of
                   [labUsed, env, esCode] -> [labUsed, env, eCode <+> label (eLab, eUsed) <+> esCode]
                 esac
            esac
        esac
    esac
  }


  fun compileArgs (env, args) {
     case args of
        arg:tail -> let [la, env] = genLabel (env) in
           let [laUsed, env, acode] = compile (la, env, arg) in
              let [env, argscode] = compileArgs (env, tail) in
                 [env, acode <+> label (la, laUsed) <+> argscode]
      | {}       -> [env, emptyBuffer ()]
     esac
  }

  fun addDefs (env, defs, code, funs) {
     case defs of
        def:t ->
           case def of 
              Fun (f, a, b) -> let [lf, env] = genFunLabel (env, f) in
                 addDefs (addFun (env, f, lf, size (a)), t, code, Fun (lf, a, b) : funs)
            | Var (ns) -> 
                 addDefs (addVars (env, ns), t, if isGlobal (env) then listBuffer (map (fun (n) { GLOBAL (n) }, ns)) else code fi, funs)
           esac
      | {} -> [env, code, funs]
      esac
  }

  fun rememberFuns (env, funs) {
     case funs of
        Fun (f, a, b):t -> rememberFuns (rememberFun (env, f, a, b), t)
      | {} -> env
     esac
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip                 -> [false, env, emptyBuffer ()]
    | Var     (x)          -> [false, env, singletonBuffer (LD (lookupVal (env, x)))]
    | Ref     (x)          -> [false, env, singletonBuffer (LDA (lookupVar (env, x))) <+ DUP]
    | Const   (n)          -> [false, env, singletonBuffer (CONST (n))]
    | Seq     (s1, s2)     -> let [l2, env] = genLabel (env) in
       let [l2Used, env, code1] = compile (l2, env, s1) in
          let [lUsed, env, code2] = compile (lab, env, s2) in
             [lUsed, env, code1 <+> label (l2, l2Used) <+> code2]
    | If      (c, s1, s2)  -> let [lc, env] = genLabel (env) in
       let [lcUsed, env, codec] = compile (lc, env, c) in
          let [lelse, env] = genLabel (env) in
             let [_, env, code1] = compile (lab, env, s1) in
                let [_, env, code2] = compile (lab, env, s2) in
                   [true, env, codec <+> label (lc, lcUsed) <+ CJMP ("z", lelse) <+> code1 <+ JMP (lab) <+ LABEL (lelse) <+> code2]
    | While   (c, s)        -> let [lc, env] = genLabel (env) in
       let [ls, env] = genLabel (env) in
          let [le, env] = genLabel (env) in
             let [leUsed, env, codec] = compile (le, env, c) in
                let [lcUsed, env, codes] = compile (lc, env, s) in
                   [false, env, singletonBuffer(JMP(lc)) <+ LABEL (ls) <+> codes <+ LABEL (lc) <+> codec <+> label (le, leUsed)<+ CJMP ("nz", ls)]
    | DoWhile (s, c)        -> let [lc, env] = genLabel (env) in
       let [ls, env] = genLabel (env) in
          let [le, env] = genLabel (env) in
             let [leUsed, env, codec] = compile (le, env, c) in
                let [lcUsed, env, codes] = compile (lc, env, s) in
                   [false, env, singletonBuffer(LABEL (ls)) <+> codes <+> label (lc, lcUsed) <+> codec <+> label (le, leUsed) <+ CJMP ("nz", ls)]
    | Binop   (op, e1, e2)  -> let [le1, env] = genLabel (env) in
       let [le2, env] = genLabel (env) in
          let [le1Used, env, code1] = compile (le1, env, e1) in
             let [le2Used, env, code2] = compile (le2, env, e2) in
                [false, env, code1 <+> label (le1, le1Used) <+> code2 <+> label (le2, le2Used) <+ BINOP (op)]
    | Ignore  (e)           -> let [le, env] = genLabel (env) in
       let [leUsed, env, code] = compile (le, env, e) in
          [false, env, code <+ DROP]
    | Call    (f, a)        -> let Fun (lf, p) = lookupFun (env, f) in
       let [env, acode] = compileArgs (env, a) in 
          case lf of
             Builtin -> [false, env, acode <+ BUILTIN (f, size (a))]
           | _       -> [false, env, acode <+ CALL (lf, size (a))]
          esac
    | Scope   (defs, e)     -> let [env, code, funs] = addDefs (beginScope (env), defs, {}, {}) in
       let env = rememberFuns (env, funs) in 
          let [labUsed, env, ecode] = compile (lab, env, e) in
             [labUsed, endScope (env), code <+> ecode]
    | String  (x)           -> [false, env, singletonBuffer (STRING (x))]
    | Elem    (a, ind)      -> let [la, env] = genLabel (env) in
       let [li, env] = genLabel (env) in
          let [laUsed, env, acode] = compile (la, env, a) in
             let [liUsed, env, icode] = compile (li, env, ind) in
                [false, env, acode <+> label (la, laUsed) <+> icode <+> label (li, liUsed) <+ ELEM]
    | ElemRef    (a, ind)   -> let [la, env] = genLabel (env) in
       let [li, env] = genLabel (env) in
          let [laUsed, env, acode] = compile (la, env, a) in
             let [liUsed, env, icode] = compile (li, env, ind) in
                [false, env, acode <+> label (la, laUsed) <+> icode <+> label (li, liUsed)]
    | Builtin    (x, es)    -> let [l, env] = genLabel (env) in
       let [lUsed, env, code] = compileMany (l, env, es) in
          [false, env, code <+> label (l, lUsed) <+ BUILTIN (x, size (es))]
    | Array      (es)       -> case es of
                                  {}  -> [false, env, singletonBuffer (CONST (0)) <+ ARRAY (0)]
                                | _   -> let [l, env] = genLabel (env) in 
                                            let [lUsed, env, code] = compileMany (l, env, es) in
                                               [false, env, (singletonBuffer (CONST (size (es))) <+> code <+> label (l, lUsed)) <+ ARRAY (size (es))]
                               esac
    | Sexp       (name, es) -> case es of
                                  {}  -> [false, env, singletonBuffer (CONST (0)) <+ SEXP (name, 0)]
                                | _ -> let [l, env] = genLabel (env) in
                                          let [lUsed, env, code] = compileMany (l, env, es) in
                                             [false, env, (singletonBuffer (CONST (size (es))) <+> code <+> label (l, lUsed)) <+ SEXP (name, size (es))]
                               esac
    | Assn  (Ref(x), e)     -> let [l, env] = genLabel(env) in
                                  let [lUsed, env, code] = compile (l, env, e) in
                                     [false, env, code <+> label(l, lUsed) <+ ST (lookupVar(env, x))]
    | Assn  (e1, e2)         -> let [l, env] = genLabel(env) in
                                  let [lUsed, env, code] = compileMany (l, env, {e1, e2}) in
                                     [false, env, code <+> label(l, lUsed) <+ STA]
    esac
  }  

  fun compileFunctions (env, main) {
    fun iterate (env, funList) {
      case funList of
        {}     -> case getFuns (env) of
                    [{}, env] -> emptyBuffer ()
                  | [fs, env] -> iterate (env, fs)
                  esac
      | Fun (f, args, body, state) : fs ->
          case genLabel (addArgs (beginFun (env, state), args)) of
            [endLab, env] ->
               case compile (endLab, env, body) of
                 [endUsed, env, code] -> 
                    singletonBuffer (LABEL (f)) <+
                    BEGIN (f, size (args), getLocals (env)) <+>
                    code <+>
                    label (endLab, endUsed) <+
                    END <+>
                    iterate (env, fs)
               esac
          esac          
      esac
    }
    
    main <+> iterate (env, {})
  }
  
  case genLabel(initCompEnv ()) of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, env, code] ->
            getBuffer $
            compileFunctions (env, 
                              singletonBuffer (LABEL ("main")) <+
                              BEGIN ("main", 0, getLocals (env)) <+>
                              code <+>
                              label (endLab, endLabUsed) <+
                              END 
                             )            
       esac
  esac
}
