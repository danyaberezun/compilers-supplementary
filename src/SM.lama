-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, world], insns) {
  case insns of
    READ:rest -> case readWorld(world) of [v, world] -> eval (env, [v:stack, state, world], rest) esac
    | WRITE:rest -> case stack of
        st:stackR -> eval (env, [stackR, state, writeWorld(st, world)], rest)
        esac
    | BINOP(s):rest -> case stack of
        fst:snd:stackR -> eval(env, [binop(s) (snd, fst) : stackR, state, world], rest)
        esac
    | LD(x):rest -> eval (env, [state(x):stack, state, world], rest)
    | ST(x):rest -> case stack of
        fst:stackR -> eval (env, [stackR, state <- [x, fst], world], rest)
        esac
    | CONST(n):rest -> eval(env, [n:stack, state, world], rest)
    | LABEL(s):rest -> eval(env, [stack, state, world], rest)
    | JMP(label):rest -> eval(env, [stack, state, world], fromLabel(env, label))
    | CJMP(c, label):rest -> case stack of
        fl:stackR -> case c of
            "z" -> case fl of
                0 -> eval(env, [stackR, state, world], fromLabel(env, label))
                |_ -> eval(env, [stackR, state, world], rest)
            esac
            | "nz" -> case fl of
                0 -> eval(env, [stackR, state, world], rest)
                |_ -> eval(env, [stackR, state, world], fromLabel(env, label))
            esac
        esac
     esac
    | {} -> [stack, state, world]
  esac
}
-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Var (s) -> {LD (s)}
      | Const (i) -> {CONST(i)}
      | Binop (s, e1, e2) -> compileExpr(e1) +++ compileExpr(e2) +++ {BINOP(s)}
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.

public fun compileSM (stmt) {
  fun compile (env, stmt) {
      case stmt of
          Assn(s, e) -> [env, compileExpr(e) +++ {ST(s)}]
          | Skip -> [env, {}]
          | Seq (s1, s2) -> case compile(env, s1) of
               [env1, st1] -> case compile(env1, s2) of
                    [env2, st2] -> [env2, st1 +++ st2]
                    esac
               esac
          | Read (s) -> [env, {READ, ST(s)}]
          | Write (e) -> [env, compileExpr(e) +++ {WRITE}]
          | If (e, s1, s2) -> case genLabels (env, 2) of
              [lfls, lend, env1] -> case compile(env1, s1) of
                  [env2, stru] -> case compile(env2, s2) of
                      [env3, sfls] -> [env3, compileExpr(e) +++ {CJMP ("z", lfls)} +++ stru +++ {JMP(lend), LABEL(lfls)} +++ sfls +++ {LABEL(lend)}]
                      esac
                  esac
              esac
          | While (e, s) -> case genLabels (env, 2) of
                [le, ls, env1] -> case compile(env1, s) of
                    [env2, cs] -> [env2, {JMP(le), LABEL(ls)} +++ cs +++ {LABEL(le)} +++ compileExpr(e) +++ {CJMP("nz", ls)}]
                    esac
                esac
          | DoWhile (s, e) -> case genLabels (env, 2) of
                 [le, ls, env1] -> case compile(env1, s) of
                     [env2, cs] -> [env2, {LABEL(ls)} +++ cs +++ {LABEL(le)} +++ compileExpr(e) +++ {CJMP("nz", ls)}]
                     esac
                 esac
      esac
  }
  compile(initCompEnv (), stmt)[1]
}
