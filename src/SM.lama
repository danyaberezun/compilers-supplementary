-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    fun varity (f) {
      if f then "var" else "val" fi
    }
    
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns | None -> failure(string(l)) esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  fun eval1 (c@[st, cst, s, w], x) {
    case x of
       BINOP (op) ->
        case st of
          (r:l:st) -> [{evalOp(op, l, r)} +++ st, cst, s, w]
        esac
    | LD    (x) -> [{lookup(s, x)} +++ st, cst, s, w]
    | LDA   (x) -> [{x} +++ st, cst, s, w]
    | STI       ->
        case st of (v:e:st) ->
          assign (s, v, e); [st, cst, s, w]
        esac
    | ST    (x) -> assign (s, x, hd (st)); [tl (st), cst, s, w]
    | DROP      -> [tl (st), cst, s, w]
    | DUP       -> [{hd (st)} +++ st, cst, s, w]
    | CONST (n) -> [{n} +++ st, cst, s, w]
    | STRING (n) -> [{n} +++ st, cst, s, w]
    | ARRAY (n)  -> case take(st, n) of [t, h] -> [listArray(h):t, cst, s, w] esac
    | SEXP (name, n)  -> case take(st, n) of [t, h] -> [listArray(h):t, cst, s, w] esac
    | ELEM       ->
        case st of
          e:a:st -> [a[e]:st, cst, s, w]
        esac
    | STA       ->
        case st of
          e:a:t:st -> a[e] := t; [st, cst, s, w]
        esac
    | LABEL (x) -> c
    | BEGIN (name, nargs, locs) ->
        case take (st, nargs) of
          [st, args] ->
            var s = makeState (nargs, locs);
            foldl(fun (ind, va) { assign(s, Arg (ind), va); ind + 1 }, 0, args);
            [st, cst, s, w]
        esac
    | GLOBAL (_) -> c
    esac
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[st, cst, s, w], insns) {
    case insns of
      {} -> c
    | x:xs  ->
      case x of
        JMP (lab) ->
          var xs = fromLabel (env, lab);
          eval(c, xs)
      | CJMP (cond, lab) ->
          c := eval1(c, BINOP (cond));
          case c[0] of
            (v:vs) ->
              var cNext = [vs, cst, s, w];
              if v
              then
                  var xs = fromLabel (env, lab);
                  eval(cNext, xs)
              else eval(cNext, xs)
              fi
          esac
      | CALL (f, a) ->
          case f of
            "$read" -> if a == 0
              then
                case readWorld(w) of
                  [va, w] -> eval([{va} +++ st, cst, s, w], xs)
                esac
              else failure(a) fi
          | "$write" -> if a == 1
            then eval ([st, cst, s, writeWorld(hd (st), w)], xs)
            else failure(a) fi
          | "$length" -> if a == 1
            then eval ([length(hd(st)):tl(st), cst, s, w], xs)
            else failure(a) fi
          | _ -> eval ([st, [s, xs] : cst, s, w], fromLabel (env, f))
          esac
      | END ->
        case cst of
          -- leaveFunction (s0, getGlobal (s))
          [s0, insns]:cst -> eval([st, cst, s0, w], insns)
        | {} -> c
        esac
      | _ -> eval(eval1(c, x), xs)
      esac
    esac
  }

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }
  
  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip              -> [false, env, emptyBuffer ()]
    | Var    (x)        -> [false, env, singletonBuffer (LD (lookupVal(env, x)))]
    | Ref    (x)        -> [false, env, singletonBuffer (LDA (lookupVal(env, x)))]
    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]
    | Ignore (e) ->
      case compile(lab, env, e, true) of [r, env, e] -> [r, env, e <+ DROP] esac
    | Binop (s, l, r) ->
      case compile (lab, env, l) of [lab1, env, l] ->
        case compile (lab, env, r) of [lab2, env, r] ->
          [lab1 !! lab2, env, l <+> r <+ BINOP (s)]
        esac
      esac
    | Assn  (x, e)   ->
      case compile(lab, env, e, true) of [r1, env, e] ->
        case compile(lab, env, x, true) of [r2, env, xcode] ->
          [r1 !! r2, env, e <+ DUP <+> xcode <+ case x of ElemRef (_, _) -> STA | _ -> STI esac]
        esac
      esac
    | Seq   (s1, s2) ->
        case genLabel (env) of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | While (e, s)   ->
        case genLabel (env) of
          [begLab, env] ->
            case genLabel (env) of
              [endLab, env] ->
                case compile(lab, env, e) of [_, env, e] ->
                  var res0 = label (begLab, true) <+> e <+ CONST (0) <+ CJMP ("==", endLab);
                  case compile (begLab, env, s) of
                    [_, env, scode] -> [false, env, res0 <+> scode <+ JMP (begLab) <+> label (endLab, true)]
                  esac
                esac
            esac
        esac
    | If (e, t, f)   ->
        case genLabel (env) of
          [elsLab, env] ->
            case genLabel (env) of
              [endLab, env] ->
                case compile (endLab, env, t) of
                  [_, env, tcode] ->
                    case compile (endLab, env, f) of [_, env, f] ->
                      case compile (endLab, env, e) of [_, env, e] ->
                        [false, env, e <+ CONST (0) <+ CJMP ("==", elsLab)
                            <+> tcode <+ JMP (endLab) <+> label (elsLab, true) <+> f <+> label (endLab, true)]
                      esac
                    esac
                esac
            esac
        esac
    | DoWhile (s, e)   ->
        case genLabel (env) of
          [begLab, env] ->
            case genLabel (env) of
              [endLab, env] ->
                case compile (begLab, env, s) of
                  [_, env, scode] ->
                    case compile (endLab, env, e) of [_, env, e] ->
                      [false, env, label (begLab, true) <+> scode <+> e <+ CONST (0) <+ CJMP ("!=", begLab) <+> label (endLab, true)]
                    esac
                esac
            esac
        esac
    | Scope (v, s) ->
        var cont = 1;
        var gCode = emptyBuffer ();
        env := foldl(fun (env, v) {
          case v of
            Var (va) -> addVars (env, va)
          | Fun (name, args, body) -> addFun(env, name, name, args)
          esac
        }, beginScope (env), v);
        env := foldl(fun (env, v) {
          case v of
            Var (va) -> env
          | Fun (name, args, body) ->  rememberFun (env, name, args, body)
          esac
        }, env, v);
        if isGlobal (env)
        then
          gCode := foldl (fun (code, v) {
            case v of
              Var (va) -> foldl (fun (code, va) { code <+ GLOBAL (va) }, code, va)
            | _ -> code
            esac
          }, emptyBuffer (), v)
        fi;
        case compile(lab, env, s) of [used, env, code] ->
          [used, endScope (env), gCode <+> code]
        esac
    | Call (f, args) ->
      case genLabel (env) of [endLab, env] ->
          fun folder ([env, used0, code0], arg) {
            case compile (endLab, env, arg) of
              [used1, env, code1] -> [env, used0 !! used1, code0 <+> code1]
            esac
          }
          case foldl (folder, [env, false, emptyBuffer ()], args) of
            [env, used, code] ->
              case lookupFun (env, f) of
                  Fun (f, n) -> [false, env, code <+ CALL (f, size (n))]
              esac
          esac
      esac
    | String (x) -> [false, env, singletonBuffer(STRING (x))]
    | Array (x) ->
      case foldl(fun([env, c], p) { case compile(lab, env, p) of [_, env, b] -> [env, c <+> b] esac }, [env, emptyBuffer ()], x) of
        [env, code] -> [false, env, code <+ ARRAY (size (x))]
      esac
    | Sexp (name, x) ->
      case foldl(fun([env, c], p) { case compile(lab, env, p) of [_, env, b] -> [env, c <+> b] esac }, [env, emptyBuffer ()], x) of
        [env, code] -> [false, env, code <+ SEXP (name, size (x))]
      esac
    | Elem (a, e) ->
        case compile (lab, env, Seq (a, e)) of
          [used, env, code] -> [used, env, code <+ ELEM]
        esac
    | ElemRef (a, e) ->
        case compile (lab, env, Seq (a, e)) of
          [used, env, code] -> [used, env, code]
        esac
    esac
  }

  fun compileFun (env, endLab, Fun (name, args, body, st)) {
    if st != 0
    then
      env := addArgs (beginFun (env, st), args)
    fi;
    case compile (endLab, env, body) of [labUsed, env, code] ->
      [env, labUsed, singletonBuffer (LABEL (name))
        <+ BEGIN (name, size (args), getLocals (env))
        <+> code
        <+ END]
    esac
  }

  case initCompEnv ().beginScope.addFun ("read"  , "$read"  , {})
                                .addFun ("write" , "$write" , {"a"})
                                .addFun ("length", "$length", {"a"}).genLabel of
    [endLab, env] ->
      fun iterate([env, used0, code0], fns) {
        case fns of
          {} -> [env, used0, code0]
        | fn:fns ->
            case compileFun (env, endLab, fn) of [env, used1, code1] ->
              case getFuns (env) of [rest, env] ->
                iterate([env, used0 !! used1, code0 <+> code1], fns +++ rest)
              esac
            esac
        esac
      }
      var myMain = {Fun ("main", 0, stmt, 0)};
      case iterate ([env, false, emptyBuffer ()], myMain) of
        [env, endLabUsed, code] ->
          getBuffer $ code <+> label (endLab, endLabUsed)
      esac
  esac
}
