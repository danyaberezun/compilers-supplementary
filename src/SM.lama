-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> x
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | SEXP   (t, n)    -> sprintf ("SEXP %s %d", t, n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }
  
  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (c@[stack, cst, state, w], insns) {
    case insns of
      {} -> c
    | JMP(l) : _ -> eval(c, fromLabel(env, l))
    | CJMP(t, l) : tail ->
      case stack of x : stack_ ->
        case t of
          "z" -> 
            eval([stack_, cst, state, w], 
                if x == 0 then fromLabel(env, l) else tail fi)
        | "nz" -> 
            eval([stack_, cst, state, w], 
                if x != 0 then fromLabel(env, l) else tail fi)
        esac
      esac
    | READ : tail -> 
          case readWorld(w) of
            [z, w1] -> eval([z : stack, cst, state, w1], tail)
        esac
    | WRITE : tail -> 
          case stack of
            z : stack -> eval([stack, cst, state, writeWorld(z, w)], tail)
          esac
    | BINOP (op):t ->
        case stack of r:l:tail -> 
          eval([evalOp(op, l, r):tail, cst, state, w], t) 
        esac
    | LD(x) : tail -> eval([lookup(state, x) : stack, cst, state, w], tail)
    | ST(x) : tail ->
      let z:stack = stack in
      assign(state, x, z);
      eval([z:stack, cst, state, w], tail)
    | CONST(z) : tail -> eval([z : stack, cst, state, w], tail)
    | LDA(x) : tail -> eval([x : stack, cst, state, w], tail)

    | STI : tail ->
      let (v@#val):ref:stack = stack in
      assign(state, ref, v);
      eval([v:stack, cst, state, w], tail)
    | DROP : tail ->
      case stack of 
        x:stack -> eval([stack, cst, state, w], tail)
      esac
    | LABEL(_) : tail -> eval(c, tail)
    | END : _ ->
      case cst of
        {} -> c
      | [state, cont] : cst -> eval([stack, cst, state, w], cont)
      esac
    | BEGIN(fname, args, locals) : tail ->
      let [stack, a] = take(stack, args) in
      let [_, l] = makeState(args, locals) in
      eval([stack, cst, [listArray(a), l], w], tail)
    | CALL(fname, args) : tail ->
      let cst = [state, tail] : cst in
      eval([stack, cst, state, w], fromLabel(env, fname))
    | GLOBAL(_) : tail -> eval(c, tail)


    | STRING(x) : tail -> eval ([x:stack, cst, state, w], tail)
    | ARRAY(n) : tail ->
      case take(stack, n) of
        [_:stack_rest, elems] -> 
          eval([listArray(elems):stack_rest, cst, state, w], tail)
      esac
    | SEXP(e, n) : tail ->
      case take(stack, n) of
        [_:stack_rest, elems] ->
          eval([Sexp(e, listArray(elems)):stack_rest, cst, state, w], tail)
      esac
    | STA : tail ->
        case stack of
          v:i:a:rest ->
            (case a of Sexp(_,x)->x | x->x esac)[i] := v;
            eval([v:rest, cst, state, w], tail)
        | _ -> error("sta err")
        esac
    | ELEM : tail ->
        case stack of
          i:a:rest ->
            let x = case a of Sexp(_,y)->y | y->y esac in
            eval([x[i]:rest, cst, state, w], tail)
        | _ -> error("elem err")
        esac
    | BUILTIN(f, n) : tail ->
      case take(stack, n) of
        [stack_rest, args] ->
          case evalBuiltin(f, args, w) of
            [value, w_new] ->
              eval([value:stack_rest, cst, state, w_new], tail)
          esac
      esac
    esac
  }
  
  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a local variable for the current function
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups a name of a variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Fun (_, _) -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    | x          -> x
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   beginFun,
   getFuns,
   lookupVar,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 1, enterScope(emptyState ()), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun beginFun (env, state) {
  env [6] (state)
}

fun getFuns (env) {
  env [7] ()
}

fun lookupVar (env, name) {
  env [8] (name)
}

fun lookupFun (env, name) {
  env [9] (name)
}

fun getLocals (env) {
  env [10] ()
}

fun genFunLabel (env, name) {
  env [11] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [12] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [13] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

(* Assignment

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).
*)
public fun compileSM (stmt) {
  fun label(lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL(lab))
    else emptyBuffer()
    fi
  }
  
  fun compileMany (lab, env, exprs) {
    case exprs of
      {e}    -> compile (lab, env, e)
    | e : es ->
        case genLabel(env) of
          [eLab, env] ->
            case compile (eLab, env, e) of
              [eUsed, env, eCode] ->
                 case compileMany (lab, env, es) of
                   [labUsed, env, esCode] -> [labUsed, env, eCode <+> label (eLab, eUsed) <+> esCode]
                 esac
            esac
        esac
    esac
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip -> [false, env, emptyBuffer ()]
    | Var(x) -> [false, env, singletonBuffer(LD(lookupVar(env, x)))]
    | Ref(x) -> [false, env, singletonBuffer(LDA(lookupVar(env, x)))]
    | Const(x) -> [false, env, singletonBuffer(CONST(x))]
    | Seq (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If(cond, s1, s2) ->
        let [postCond, env] = env.genLabel in
        let [elseLab, env] = env.genLabel in
        let [postCondUsed, env, cond] = compile(postCond, env, cond) in
        let [_, env, tb] = compile(lab, env, s1) in
        let [_, env, eb] = compile(lab, env, s2) in
        [true, env, cond <+> label(postCond, postCondUsed) <+ CJMP("z", elseLab) <+> tb <+ JMP(lab) <+ LABEL(elseLab) <+> eb]
    | DoWhile(s, e) ->
        let [blockLab, env] = env.genLabel in
        let [condLab, env] = env.genLabel in
        let [clu, env, bc] = compile(condLab, env, s) in
        let [fc, env] = env.genLabel in
        let [fcu, env, cond] = compile(fc, env, e) in
        [false, env, singletonBuffer(LABEL(blockLab)) <+> bc <+> label(condLab, clu) <+> cond <+> label(fc, fcu) <+ CJMP("nz", blockLab)]   
    | While(e, s) ->
        compile(lab, env, If(e, DoWhile(s, e), Skip))
    | Binop (op, e1, e2) ->
        let [e1lab, env] = genLabel(env) in
        let [e1labUsed, env, e1] = compile(e1lab, env, e1) in
        let [e2lab, env] = genLabel(env) in
        let [e2labUsed, env, e2] = compile(e2lab, env, e2) in
        [false, env, e1 <+> label(e1lab, e1labUsed) <+> e2 <+> label(e2lab, e2labUsed) <+ BINOP(op)]
    | Ignore(e) ->
        let [condLab, env] = genLabel(env) in
        let [eLabUsed, env, e] = compile(condLab, env, e) in [false, env, e <+ DROP]
    
    | Assn(Ref(x), e) ->
        let [start_lab, env1] = genLabel(env) in
        let [end_lab, env2, code] = compile(start_lab, env1, e) in
        [false, env2, code <+> label(start_lab, end_lab) <+ ST(lookupVar(env2, x))]
    | Assn(ElemRef(a, i), e) ->
        let [start_lab, env1] = genLabel(env) in
        let [end_lab, env2, code] = compileMany(start_lab, env1, {ElemRef(a, i), e}) in
        [false, env2, code <+> label(start_lab, end_lab) <+ STA]
    | Assn(e1, e2) ->
        let [lab1, lab2, env1] = genLabels(env, 2) in
        let [lab3, env2, code1] = compile(lab1, env1, e1) in
        let [lab4, env3, code2] = compile(lab2, env2, e2) in
        [false, env3, 
        code1 <+> label(lab1, lab3) 
        <+> code2 <+> label(lab2, lab4) 
        <+ STI]

    | Scope(dfns, expr) ->
      env := beginScope(env);
      
      env := foldl(fun(env, dfn) {
        case dfn of
          Fun(name, args, _) -> 
            let [lab, env] = genFunLabel(env, name) in 
            addFun(env, name, lab, size(args))
        | Var(xs) -> addVars(env, xs)
        esac
      }, env, dfns);
      
      env := foldl(fun(env, dfn) {
        case dfn of
          Fun(name, args, body) ->
            let [lab, _] = genFunLabel(env, name) in
            rememberFun(env, lab, args, body)
        | _ -> env
        esac
      }, env, dfns);
      
      let global_code = if isGlobal(env) then
        foldl(fun(code, dfn) {
          case dfn of
            Var(xs) -> foldl(fun(c, x) { c <+ GLOBAL(x) }, code, xs)
          | _ -> code
          esac
        }, emptyBuffer(), dfns)
      else emptyBuffer() fi in
      
      let [lab2, env, expr_code] = compile(lab, env, expr) in
      [lab2, endScope(env), global_code <+> expr_code]                      
    | Call(name, args) ->
      case args of
        {} ->
          let Fun(lab, n) = lookupFun(env, name) in
          if size(args) == n then
            [false, env, singletonBuffer(CALL(lab, n))]
          else
            failure("size(args) != n\n")
          fi
      | _ ->
          let [l1, e1] = genLabel(env) in
          let [l2, e2, arg_code] = compileMany(l1, e1, args) in
          let Fun(lab, n) = lookupFun(e2, name) in
          if size(args) == n then
            [false, e2, (arg_code <+> label(l1, l2)) <+ CALL(lab, n)]
          else
            failure("size(args) != n\n")
          fi
      esac

    | String (s) -> [false, env, singletonBuffer(STRING(s))]
    | Array(exps) ->
      case exps of
        {} -> [false, env, singletonBuffer(CONST(0)) <+ ARRAY(0)]
      | _ ->
          let sz = size(exps) in
          let [l1, e1] = genLabel(env) in
          let [l2, e2, code] = compileMany(l1, e1, exps) in
          [false, e2, (singletonBuffer(CONST(sz)) <+> code <+> label(l1, l2)) <+ ARRAY(sz)]
      esac
    | Sexp(name, exps) ->
      case exps of
        {} -> [false, env, singletonBuffer(CONST(0)) <+ SEXP(name, 0)]
      | _ ->
          let sz = size(exps) in
          let [l1, e1] = genLabel(env) in
          let [l2, e2, code] = compileMany(l1, e1, exps) in
          [false, e2, (singletonBuffer(CONST(sz)) <+> code <+> label(l1, l2)) <+ SEXP(name, sz)]
      esac
    | Elem(arr, idx) ->
      let [l1, e1] = genLabel(env) in
      let [l2, e2, code] = compileMany(l1, e1, {arr, idx}) in
      [false, e2, (code <+> label(l1, l2)) <+ ELEM]
    | ElemRef (e_array, e_index) ->  let [lab1, env1] = genLabel(env) in (
                                      let [lab2, env2, cd] = compileMany(lab1, env1, {e_array, e_index}) in (
                                        [false, env2, (cd <+> label(lab1, lab2))]
                                      )
                                    )
    | ElemRef(arr, idx) ->
      let [l1, e1] = genLabel(env) in
      let [l2, e2, code] = compileMany(l1, e1, {arr, idx}) in
      [false, e2, code <+> label(l1, l2)]
    | Builtin(name, args) ->
      case args of
        {} -> [false, env, singletonBuffer(BUILTIN(name, 0))]
      | _ ->
          let [l1, e1] = genLabel(env) in
          let [l2, e2, code] = compileMany(l1, e1, args) in
          [false, e2, (code <+> label(l1, l2)) <+ BUILTIN(name, size(args))]
      esac
    esac
  }

fun compileFunctions (env, main) {
    fun iterate (env, funList) {
      case funList of
        {}     -> case getFuns (env) of
                    [{}, env] -> emptyBuffer ()
                  | [fs, env] -> iterate (env, fs)
                  esac
      | Fun (f, args, body, state) : fs ->
          case genLabel (addArgs (beginFun (env, state), args)) of
            [endLab, env] ->
               case compile (endLab, env, body) of
                 [endUsed, env, code] -> 
                    singletonBuffer (LABEL (f)) <+
                    BEGIN (f, args.size, getLocals (env)) <+>
                    code <+>
                    label (endLab, endUsed) <+
                    END <+>
                    iterate (env, fs)
               esac
          esac          
      esac
    }
    
    main <+> iterate (env, {})
  }
    
  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, env, code] ->
            getBuffer $
            compileFunctions (env, 
                              singletonBuffer (LABEL ("main")) <+
                              BEGIN ("main", 0, getLocals (env)) <+>
                              code <+>
                              label (endLab, endLabUsed) <+
                              END 
                             )            
       esac
  esac
}