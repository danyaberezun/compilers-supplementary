-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> x
    esac
  }

  case i of
    READ             -> "READ"
  | WRITE            -> "WRITE"
  | BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});

  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, v) {
    case loc of
      Arg (i) -> args[i] := v
    | Loc (i) -> locs[i] := v
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then v else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  (* Assignment *)
  fun eval (c@[st, cst, s, w], insns) {
    fun evalStep (env, c, instruction, tail) {
      fun readImpl ([stack, cst, state, w]) {
        case readWorld (w) of 
          [value, w] -> [[value : stack, cst, state, w], KeepLinear]
        esac
      }

      fun writeImpl ([head : tail, cst, state, w]) {
        [[tail, cst, state, writeWorld (head, w)], KeepLinear]
      }

      fun binopImpl ([rhs : lhs : tail, cst, state, w], op) {
        [[evalOp (op, lhs, rhs) : tail, cst, state, w], KeepLinear]
      }

      fun storeImpl ([head : tail, cst, state, w], name) {
        assign (state, name, head);
        [[tail, cst, state, w], KeepLinear]
      }

      fun stiImpl ([value : (RefVal (name)) : tail, cst, state, w]) {
        state.assign (name, value);
        [[value : tail, cst, state, w], KeepLinear]
      }

      fun constImpl ([stack, cst, state, w], value) {
        [[value : stack, cst, state, w], KeepLinear]
      }

      fun loadImpl (c @ [stack, cst, state, w], name) {
        [[state.lookup (name) : stack, cst, state, w], KeepLinear]
      }

      fun ldaImpl (c @ [stack, cst, state, w], name) {
        [[RefVal (name) : stack, cst, state, w], KeepLinear]
      }

      fun dropImpl (c @ [head : tail, cst, state, world]) {
        [[tail, cst, state, world], KeepLinear]
      }

      fun labelImpl (c, lab) {
        [c, KeepLinear]
      }

      fun jmpImpl (c, lab) {
        case env.fromLabel (lab) of
          code -> [c, ShouldJump (code)]
        esac
      }

      fun cjmpImpl (c @ [top : tail, cst, state, w], kind, label) {
        case kind of
          "z"  -> 
            case top of
              0 -> jmpImpl ([tail, cst, state, w], label)
            | _ -> [[tail, cst, state, w], KeepLinear]
            esac
        | "nz" -> 
            case top of
              0 -> [[tail, cst, state, w], KeepLinear]
            | _ -> jmpImpl ([tail, cst, state, w], label)
            esac
        esac
      }

      fun dupImpl (c @ [top : tail, cst, state, w]) {
        [[top : top : tail, cst, state, w], KeepLinear]
      }

      fun callImpl (c @ [stack, cst, state, w], fn, tail) {
        [[stack, [state, tail] : cst, state, w], ShouldJump (env.fromLabel (fn))]
      }

      fun beginImpl (c @ [stack, cst, state, w], n, l) {
        fun addLocal (n, l) {
          fun go (stt, n, args) {
            case args of
              head : tail -> assign (stt, Arg (n), head); go (stt, n + 1, tail)
            | _           -> n
            esac
          }

          case stack.take (n) of 
            [tail, args] -> case makeState (n, l) of
                              stt -> go (stt, 0, args); [tail, stt]
                            esac
          esac
        }

        case stack.addLocal (n, l) of
          [stack, state] -> [[stack, cst, state, w], KeepLinear]
        esac
      }

      fun globalImpl (c @ [stack, cst, state, w], name) {
        state.assign (Glb (name), 0);
        [[stack, cst, state, w], KeepLinear]
      }

      fun endImpl (c @ [stack, cst, state, w]) {
        case cst of
          [state, insns] : rest -> [[stack, rest, state, w], ShouldJump (insns)] 
        | _                     -> [c, KeepLinear]
        esac
      }

      case instruction of 
        READ               -> readImpl (c)
      | WRITE              -> writeImpl (c)
      | BINOP (op)         -> binopImpl (c, op)
      | LD    (name)       -> loadImpl (c, name)
      | LDA   (name)       -> ldaImpl (c, name)
      | ST    (name)       -> storeImpl (c, name)
      | STI                -> stiImpl (c)
      | CONST (value)      -> constImpl (c, value)
      | LABEL (lab)        -> labelImpl (c, lab)
      | JMP (lab)          -> jmpImpl (c, lab)
      | CJMP (kind, lab)   -> cjmpImpl (c, kind, lab)
      | CALL (fn, _)       -> callImpl (c, fn, tail)
      | BEGIN (_, a, l)    -> beginImpl (c, a, l)
      | GLOBAL (name)      -> globalImpl (c, name)
      | END                -> endImpl (c)
      | DUP                -> dupImpl (c)
      | DROP               -> dropImpl (c)
      esac
    }

    case insns of 
      instruction : tail -> case evalStep (env, c, instruction, tail) of 
                              [c, KeepLinear]        -> eval (env, c, tail)
                            | [c, ShouldJump (code)] -> eval (env, c, code)
                            esac
    | _                  -> c
    esac
  }
  (* End *)

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a local variable for the current function
  fun addVar (name) {
    if scopeDepth == 1
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }

  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 1, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups a name of a variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Fun (_, _) -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    | x          -> x
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if scopeDepth == 1
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 1
  }

  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   beginFun,
   getFuns,
   lookupVar,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun beginFun (env, state) {
  env [6] (state)
}

fun getFuns (env) {
  env [7] ()
}

fun lookupVar (env, name) {
  env [8] (name)
}

fun lookupFun (env, name) {
  env [9] (name)
}

fun getLocals (env) {
  env [10] ()
}

fun genFunLabel (env, name) {
  env [11] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [12] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [13] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

(* Assignment *)

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for code generation:
--    a. generate the code for the topmost expression (the program itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun addAll (env, defList) {
    fun addEach (env, code, defList) {
      case defList of
        head : tail -> case head of
                         Fun (fn, args, _) -> case env.genFunLabel (fn) of  
                                                [fnLab, env] -> addEach (env.addFun (fn, fnLab, size (args)), code, tail)
                                              esac
                       | Var (nList)       -> case env.addVars (nList) of
                                                env -> 
                                                  case env.isGlobal of
                                                    0 -> env.addEach (code, tail)
                                                  | _ -> env.addEach (foldl (fun (acc, n) {acc <+ GLOBAL (n)}, code, nList), tail)
                                                  esac 
                                              esac
                       esac
      | _           -> [code, env]
      esac
    }
    
    case env.beginScope of
      env -> env.addEach (emptyBuffer(), defList)
    esac
  }

  fun addFn (env, defList) {
    case defList of
      head : tail -> case head of
                       Fun (fn, args, e) -> case env.lookupFun (fn) of
                                              Fun (flab, _) -> addFn (env.rememberFun (flab, args, e), tail)
                                            esac
                     | _                 -> env.addFn (tail)
                     esac
    | _           -> env
    esac
  }



  fun compile (lab, env, stmt) {
    case stmt of
      Skip                 -> [false, env, emptyBuffer ()]
    | Var    (x)           -> [false, env, singletonBuffer (LD (env.lookupVar (x)))]
    | Ref    (x)           -> [false, env, singletonBuffer (LDA (env.lookupVar (x)))]
    | Const  (n)           -> [false, env, singletonBuffer (CONST (n))]
    | Read   (x)           -> [false, env, singletonBuffer (READ) <+ ST (env.lookupVar (x))]
    | Ignore (expr)        -> case compile (lab, env, expr) of 
                                [used, env, code] -> [used, env, code <+ DROP]
                              esac 
    | Binop (op, lhs, rhs) -> case env.genLabels (2) of 
                                [after1, after2, env] -> 
                                  case compile (after1, env, lhs) of
                                    [used1, env, code1] -> 
                                      case compile (after2, env, rhs) of
                                        [used2, env, code2] -> 
                                          [false, env, code1 <+> label (after1, used1) <+> code2 <+> label (after2, used2) <+ BINOP (op)]
                                      esac
                                  esac
                              esac
    | Write (expr)         -> case env.genLabel of
                                [labexpr, env] -> case compile (labexpr, env, expr) of
                                                    [used, env, code] -> [false, env, code <+> label (labexpr, used) <+ WRITE]
                                                  esac
                              esac
    | Assn (lhs, rhs)      -> case env.genLabels (2) of
                                [lab2, lab1, env] ->
                                  case compile(lab2, env, rhs) of 
                                    [used2, env, code2] ->
                                      case compile(lab1, env, lhs) of 
                                        [used1, env, code1] -> 
                                          [false, env, code1 <+> label (lab1, used1) <+> code2 <+> label (lab2, used2) <+ STI]
                                      esac
                                  esac
                              esac
    | Seq (lhs, rhs)       -> case env.genLabel of
                                [lab2, env] -> case compile (lab2, env, lhs) of
                                                 [used2, env, code1] -> case compile (lab, env, rhs) of
                                                                          [used, env, code2] ->
                                                                            [used, env, code1 <+> label (lab2, used2) <+> code2]
                                                                        esac
                                               esac
                              esac
    | While (cond, body) ->
        case env.genLabels (3) of 
          [toCondLab, toBodyLab, eLab, env] -> 
            case compile (toCondLab, env, body) of 
              [_, env, bodyCode] -> 
                case compile (eLab, env, cond) of
                  [eUsed, env, condCode] -> 
                    [false, env, singletonBuffer (JMP (toCondLab)) <+ LABEL (toBodyLab) <+> bodyCode <+ LABEL (toCondLab) <+> condCode <+> label (eLab, eUsed) <+ CJMP ("nz", toBodyLab)]
                esac 
            esac
        esac
    | DoWhile (expr, cond) ->
        case env.genLabels (3) of 
          [condLab, loopLab, lb, env] -> 
            case compile (condLab, env, expr) of 
              [_, env, loopcode] -> 
                case compile (lb, env, cond) of
                  [used, env, condcode] -> 
                    [false, env, singletonBuffer (LABEL (loopLab)) <+> loopcode <+ LABEL (condLab) <+> condcode <+> label (lb, used) <+ CJMP ("nz", loopLab)]
                esac
            esac
        esac
    | If (cond, expr1, expr2) ->
        case env.genLabels (3) of 
          [lab1, lab2, lb, env] ->
            case compile (lab1, env, expr1) of 
              [used1, env, code1] ->
                case compile (lab, env, expr2) of 
                  [_, env, code2] -> 
                    case compile(lb, env, cond) of
                      [used, env, condcode] -> 
                        [true, env, condcode <+> label (lb, used) <+ CJMP ("z", lab2) <+> code1 <+> label(lab1, used1) <+ JMP (lab) <+ LABEL (lab2) <+> code2]
                    esac
                esac
            esac
        esac
    | Call (fn, bd)           -> case env.genLabel of
                                   [fnLab, env] -> 
                                    case env.lookupFun (fn) of
                                      Fun (f, arg) -> 
                                        case compile (fnLab, env, foldr (fun (body, last) { Seq (last, body) }, Skip, bd)) of
                                          [fnUsed, env, code] -> [false, env, code <+> label (fnLab, fnUsed) <+ CALL (f, arg)]
                                        esac
                                    esac
                                 esac
    | Scope (defList, body)   -> case env.addAll (defList) of
                                   [defCode, env] ->
                                     case env.addFn (defList) of
                                       env -> case env.genLabel of
                                                [endLabel, env] -> 
                                                  case compile (endLabel, env, body) of
                                                    [endUsed, env, bodyCode] -> 
                                                      [false, env.leaveScope, defCode <+> bodyCode <+> label (endLabel, endUsed)]
                                                  esac
                                              esac
                                     esac
                                 esac 
    esac
  }

  fun compileMultipass (lab, env, stmt) {
    fun compileFns (env) {
      fun compileFnsRec (env, code, fList) {
        case fList of
          head : tail -> case head of
                           Fun (fn, args, e, s) ->
                             case addArgs (env.beginFun (s), args) of
                              env -> case env.genLabel of [lab, env] ->
                                case compile (lab, env, e) of 
                                  [used, env, eCode] -> 
                                    compileFnsRec (env.endScope, singletonBuffer (LABEL (fn)) <+ BEGIN (fn, size (args), env.getLocals) <+> eCode <+> label (lab, used) <+ END <+> code, tail)
                                esac
                              esac
                             esac
                         esac
        | _           -> [code, env]
        esac
      }

      case env.getFuns of
        [{}, env]    -> [emptyBuffer (), env]
      | [fList, env] -> case env.compileFnsRec (emptyBuffer (), fList) of
                          [code, env] -> case env.compileFns of
                                           [fnCode, env] -> [code <+> fnCode, env]
                                         esac
                        esac
      esac
    }

    case stmt of
      Scope (_, _) -> case compile (lab, env, stmt) of
                        [_, env, code] -> 
                          case env.compileFns of
                            [fnCode, env] -> 
                              [false, env, singletonBuffer (BEGIN ("main", 0, env.getLocals)) <+> code <+ END <+> fnCode]
                          esac
                      esac
    | _            -> case compile (lab, env, Scope ({}, stmt)) of
                        [_, env, code] -> 
                          case env.compileFns of
                            [fnCode, env] -> 
                              [false, env, singletonBuffer (BEGIN ("main", 0, env.getLocals)) <+> code <+ END <+> fnCode]
                          esac
                      esac
    esac
  }

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compileMultipass (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $ code <+> label (endLab, endLabUsed)
       esac
  esac
}

