-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval ([stack, state, world], insns) {
  case insns of
    instr : rest ->
      case instr of
        READ       ->
          case readWorld (world) of
            [v, newWorld] -> eval ([v : stack, state, newWorld], rest)
          esac
        | WRITE      ->
          case stack of
            v : restStack -> eval ([restStack, state, writeWorld (v, world)], rest)
          esac
        | BINOP (op) ->
          case stack of
            a : b : restStack -> eval ([evalBinOp (op, b, a) : restStack, state, world], rest)
          esac
        | LD (name)  -> eval ([state (name) : stack, state, world], rest)
        | ST (name)  ->
          case stack of
            v : restStack -> eval ([restStack, state <- [name, v], world], rest)
          esac
        | CONST (v)  -> eval ([v : stack, state, world], rest)
      esac
    | {} -> [stack, state, world]
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list 
-- of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (v) -> { LD (v) }
    | Const (c) -> { CONST (c) }
    | Binop (op, a, b) -> compileExpr (a) +++ compileExpr(b) +++ { BINOP(op) }
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  case stmt of
    Assn (name, expr) -> compileExpr (expr) +++ { ST (name) }
    | Seq  (a, b) -> compileSM (a) +++ compileSM (b)
    | Skip -> []
    | Read (name) -> {READ, ST (name) }
    | Write (expr) -> compileExpr (expr) +++ { WRITE }
  esac
}
