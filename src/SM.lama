-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> sprintf ("%s", x)
    esac
  }

  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | SEXP   (s, n)    -> sprintf ("SEXP ""%s"", %d", s, n)
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});

  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun createLocal (st, a, l) {
    case take (st, a) of
      [st, as] -> [st, [listArray (as), initArray (l, fun (_) { 0 })]]
    esac
  }

  fun eval (c@[st, cst, s, w], insns) {
    case insns of
      {} -> c
    | BINOP (op):insns -> case st of
        b:a:st -> eval ([evalOp (op, a, b):st, cst, s, w], insns)
      esac
    | LD (x):insns -> eval ([lookup (s, x):st, cst, s, w], insns)
    | LDA (x):insns -> eval ([x:st, cst, s, w], insns)
    | ST (x):insns -> case st of z:_ -> assign (s, x, z); eval (c, insns) esac
    | STI:insns -> case st of z:x:st -> assign (s, x, z); eval ([z:st, cst, s, w], insns) esac
    | SEXP (x, n):insns -> case take (st, n) of
        [st, xs] -> eval ([Sexp (x, listArray (xs)):st, cst, s, w], insns)
      esac
    | CONST (n):insns -> eval ([n:st, cst, s, w], insns)
    | LABEL (l):insns -> eval (c, insns)
    | JMP (l):_ -> eval (c, fromLabel (env, l))
    | CJMP (c, l):insns -> case st of
      v:st ->
        case (case [c, v] of
          ["z",  0] -> true
        | ["z",  _] -> false
        | ["nz", 0] -> false
        | ["nz", _] -> true
        | _ -> failure ("unknown CJMP type ""%s""\n", c)
        esac) of
          jmp -> eval ([st, cst, s, w], if jmp then fromLabel (env, l) else insns fi)
        esac
      esac
    | CALL (f, n):insns -> eval ([st, [s, insns]:cst, s, w], fromLabel (env, f))
    | BEGIN (f, a, l):insns -> case createLocal (st, a, l) of
        [st, s] -> eval ([st, cst, s, w], insns)
      esac
    | GLOBAL (x):insns -> assign (s, Glb (x), 0); eval (c, insns)
    | END:insns -> case cst of
        {} -> c
      | [s, q]:cst -> eval ([st, cst, s, w], q)
      esac
    | DROP:insns -> case st of _:st -> eval ([st, cst, s, w], insns) esac
    | STRING (x):insns -> eval ([clone (x):st, cst, s, w], insns)
    | ARRAY (n):insns -> case take (st, n) of
        [st, xs] -> eval ([listArray (xs):st, cst, s, w], insns)
      esac
    | STA:insns -> case st of
        x:i:Sexp (_, xs):st -> xs [i] := x; eval ([x:st, cst, s, w], insns)
      | x:i:xs:st -> xs [i] := x; eval ([x:st, cst, s, w], insns)
      esac
    | ELEM:insns -> case st of
        i:Sexp (_, xs):st -> eval ([xs [i]:st, cst, s, w], insns)
      | i:xs:st -> eval ([xs [i]:st, cst, s, w], insns)
      esac
    | BUILTIN (f, n):insns -> case take (st, n) of
        [st, as] -> case evalBuiltin (f, as, w) of
          [x, w] -> eval ([x:st, cst, s, w], insns)
        esac
      esac
    | ins:_ -> failure ("SM interpreter is not implemented: %s\n", string (ins))
    esac
  }


  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a variable in the scope
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, true)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, true)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a value in the scope
  fun addVal (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name, false)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals, false)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }

  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nArgs, nLocals, {})]
  }

  -- Lookups variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Loc (i, true) -> Loc (i)
    | Glb (x, true) -> Glb (x)
    | _             -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    esac
  }

  -- Lookups value
  fun lookupVal (name) {
    case lookup (state, name) of
      Loc (i, _) -> Loc (i)
    | Glb (x, _) -> Glb (x)
    | Arg (i)    -> Arg (i)
    | _          -> error (sprintf ("the name ""%s"" does not designate a value", name), getLoc (name))
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }

  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   addVal,
   beginFun,
   getFuns,
   lookupVar,
   lookupVal,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun addVal (env, name) {
  env [6] (name)
}

fun beginFun (env, state) {
  env [7] (state)
}

fun getFuns (env) {
  env [8] ()
}

fun lookupVar (env, name) {
  env [9] (name)
}

fun lookupVal (env, name) {
  env [10] (name)
}

fun lookupFun (env, name) {
  env [11] (name)
}

fun getLocals (env) {
  env [12] ()
}

fun genFunLabel (env, name) {
  env [13] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [14] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [15] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of local variables
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals values
fun addVals (env, names) {
  foldl (fun (env, name) {addVal (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.


-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (expr) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun isBuiltin (fLabel) {
    matchSubString (fLabel, "$", 0)
  }

  fun compile (lab, env, expr) {
    case expr of
      Assn (Ref (x), e) -> case genLabel (env) of
        [stLab, env] -> case compile (stLab, env, e) of
          [stLabUsed, env, eCode] -> [false, env,
            eCode
            <+> label (stLab, stLabUsed)
            <+ ST (lookupVar (env, x))
          ]
        esac
      esac
    | Assn (ElemRef (xs, i), e) -> case genLabel (env) of
        [iLab, env] -> case genLabel (env) of
          [eLab, env] -> case genLabel (env) of
            [staLab, env] -> case compile (iLab, env, xs) of
              [iLabUsed, env, xsCode] -> case compile (eLab, env, i) of
                [eLabUsed, env, iCode] -> case compile (staLab, env, e) of
                  [staLabUsed, env, eCode] -> [false, env, xsCode
                    <+> label (iLab, iLabUsed)
                    <+> iCode
                    <+> label (eLab, eLabUsed)
                    <+> eCode
                    <+> label (staLab, staLabUsed)
                    <+ STA]
                esac
              esac
            esac
          esac
        esac
      esac
    | Assn (x, e) -> case genLabel (env) of
        [eLab, env] -> case genLabel (env) of
          [stiLab, env] -> case compile (eLab, env, x) of
            [eLabUsed, env, xCode] -> case compile (stiLab, env, e) of
              [stiLabUsed, env, eCode] -> [false, env,
                xCode
                <+> label (eLab, eLabUsed)
                <+> eCode
                <+> label (stiLab, stiLabUsed)
                <+ STI
              ]
            esac
          esac
        esac
      esac
    | Set (x, e) -> case genLabel (env) of
        [lab, env] -> case compile (lab, env, e) of
          [labUsed, env, code] -> [false, env, code
            <+> label (lab, labUsed)
            <+ ST (lookupVar (env, x))]
        esac
      esac
    | Seq (s1, s2) -> case genLabel (env) of
        [s2Lab, env] -> case compile (s2Lab, env, s1) of
          [s2LabUsed, env, s1Code] -> case compile (lab, env, s2) of
            [labUsed, env, s2Code] -> [labUsed, env,
              s1Code
              <+> label (s2Lab, s2LabUsed)
              <+> s2Code
            ]
          esac
        esac
      esac
    | Skip -> [false, env, emptyBuffer ()]
    | If (c, t, f) -> case genLabel (env) of
        [jmpLab, env] -> case genLabel (env) of
          [eLab, env] -> case compile (jmpLab, env, c) of
            [jmpLabUsed, env, cCode] -> case compile (lab, env, t) of
              [labUsed1, env, tCode] -> case compile (lab, env, f) of
                [labUsed2, env, fCode] -> [true, env,
                  cCode
                  <+> label (jmpLab, jmpLabUsed)
                  <+ CJMP ("z", eLab)
                  <+> tCode
                  <+ JMP (lab)
                  <+ LABEL (eLab)
                  <+> fCode
                ]
              esac
            esac
          esac
        esac
      esac
    | While (c, b) -> case genLabel (env) of
        [cLab, env] -> case genLabel (env) of
          [jmpLab, env] -> case genLabel (env) of
            [bLab, env] -> case compile (jmpLab, env, c) of
              [jmpLabUsed, env, cCode] -> case compile (cLab, env, b) of
                [cLabUsed, env, bCode] -> [false, env,
                  emptyBuffer ()
                  <+ JMP (cLab)
                  <+ LABEL (bLab)
                  <+> bCode
                  <+ LABEL (cLab)
                  <+> cCode
                  <+> label (jmpLab, jmpLabUsed)
                  <+ CJMP ("nz", bLab)
                ]
              esac
            esac
          esac
        esac
      esac
    | DoWhile (b, c) -> case genLabel (env) of
        [cLab, env] -> case genLabel (env) of
          [jmpLab, env] -> case genLabel (env) of
            [bLab, env] -> case compile (jmpLab, env, c) of
              [jmpLabUsed, env, cCode] -> case compile (cLab, env, b) of
                [cLabUsed, env, bCode] -> [false, env,
                  emptyBuffer ()
                  <+ LABEL (bLab)
                  <+> bCode
                  <+> label (cLab, cLabUsed)
                  <+> cCode
                  <+> label (jmpLab, jmpLabUsed)
                  <+ CJMP ("nz", bLab)
                ]
              esac
            esac
          esac
        esac
      esac
    | Var (x) -> [false, env, singletonBuffer (LD (lookupVal (env, x)))]
    | Ref (x) -> [false, env, singletonBuffer (LDA (lookupVal (env, x)))]
    | Const (n) -> [false, env, singletonBuffer (CONST (n))]
    | Binop (op, lhs, rhs) -> case genLabel (env) of
        [rhsLab, env] -> case genLabel (env) of
          [opLab, env] -> case compile (rhsLab, env, lhs) of
            [rhsLabUsed, env, lhsCode] -> case compile (opLab, env, rhs) of
              [opLabUsed, env, rhsCode] -> [false, env,
                lhsCode
                <+> label (rhsLab, rhsLabUsed)
                <+> rhsCode
                <+> label (opLab, opLabUsed)
                <+ BINOP (op)
              ]
            esac
          esac
        esac
      esac
    | Scope (ds, e) ->
      case foldl (fun ([env, code, funs], d) {
        case d of
          Var (x) -> case addVar (env, x) of
            env -> if isGlobal (env)
                   then [env, code <+ GLOBAL (x), funs]
                   else [env, code, funs]
                   fi
          esac
        | Fun (f, xs, e) -> case genFunLabel (env, f) of
          [fLabel, env] -> [addFun (env, f, fLabel, size (xs)), code, Fun (fLabel, xs, e) : funs]
        esac
        esac
      }, [beginScope (env), emptyBuffer (), {}], ds) of
        [env, code, funs] -> case foldl (fun (env, Fun (fLabel, xs, e)) {
          rememberFun (env, fLabel, xs, e)
        }, env, funs) of
          env -> case compile (lab, env, e) of
            [labUsed, env, eCode] -> [labUsed, endScope (env), code <+> eCode]
          esac
        esac
      esac
    | Call (f, es) -> case lookupFun (env, f) of
        Fun (fLabel, nArgs) -> if size (es) == nArgs
          then case foldl (fun ([env, code], e) {
              case genLabel (env) of
                [lab, env] -> case compile (lab, env, e) of
                  [labUsed, env, eCode] -> [env, code <+> eCode <+> label (lab, labUsed)]
                esac
              esac
            }, [env, emptyBuffer ()], es) of
              [env, code] -> if isBuiltin (fLabel)
                then case substring (fLabel, 1, length (fLabel) - 1) of
                    fLabel -> [false, env, code <+ BUILTIN (fLabel, size (es))]
                  esac
                else [false, env, code <+ CALL (fLabel, size (es))]
                fi
            esac
          else error
            ( sprintf
              ( "number of call args (%d) doesn't correspond to number of function params (%d)"
              , size (es)
              , nArgs
              )
            , getLoc (f)
            )
          fi
      esac
    | Ignore (e) -> case genLabel (env) of
        [dropLab, env] -> case compile (dropLab, env, e) of
          [dropLabUsed, env, eCode] -> [false, env,
            eCode
            <+> label (dropLab, dropLabUsed)
            <+ DROP
          ]
        esac
      esac
    | String (s) -> [false, env, singletonBuffer (STRING (s))]
    | Array (xs) -> case foldl (fun ([env, code], e) {
        case genLabel (env) of
          [lab, env] -> case compile (lab, env, e) of
            [labUsed, env, eCode] -> [env, code <+> eCode <+> label (lab, labUsed)]
          esac
        esac
      }, [env, emptyBuffer ()], xs) of
        [env, code] -> [false, env, code <+ ARRAY (size (xs))]
      esac
    | Sexp (s, xs) -> case foldl (fun ([env, code], e) {
        case genLabel (env) of
          [lab, env] -> case compile (lab, env, e) of
            [labUsed, env, eCode] -> [env, code <+> eCode <+> label (lab, labUsed)]
          esac
        esac
      }, [env, emptyBuffer ()], xs) of
        [env, code] -> [false, env, code <+ SEXP (s, size (xs))]
      esac
    | Elem (xs, i) -> case genLabel (env) of
        [iLab, env] -> case genLabel (env) of
          [eLab, env] -> case compile (iLab, env, xs) of
            [iLabUsed, env, xsCode] -> case compile (eLab, env, i) of
              [eLabUsed, env, iCode] -> [false, env, xsCode
                <+> label (iLab, iLabUsed)
                <+> iCode
                <+> label (eLab, eLabUsed)
                <+ ELEM]
            esac
          esac
        esac
      esac
    | _ -> failure ("compileSM not implemented: %s\n", string (expr))
    esac
  }

  fun generateFuns (env, code) {
    case getFuns (env) of
      [{}, _] -> code
    | [fs, env] -> case foldl (fun ([env, code], Fun (fLabel, args, body, state)) {
        case addArgs (beginFun (env, state), args) of
          env -> case genLabel (env) of
            [lab, env] -> case compile (lab, env, body) of
              [labUsed, env, bodyCode] -> [env, code
                <+ LABEL (fLabel)
                <+ BEGIN (fLabel, size (args), getLocals (env))
                <+> bodyCode
                <+> label (lab, labUsed)
                <+ END]
            esac
          esac
        esac
      }, [env, code], fs) of
        [env, code] -> generateFuns (env, code)
      esac
    esac
  }

  case initCompEnv ().beginScope.addFun ("read"  , "$read"  , 0)
                                .addFun ("write" , "$write" , 1)
                                .addFun ("length", "$length", 1).genLabel of
    [endLab, env] ->
      case compile (endLab, env, expr) of
        [endLabUsed, env, code] -> getBuffer $ generateFuns (env, emptyBuffer ()
           <+ BEGIN ("main", 0, getLocals (env)) <+> code <+> label (endLab, endLabUsed) <+ END)
      esac
  esac
}
