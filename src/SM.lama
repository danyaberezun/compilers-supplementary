-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c@[stack, state, world], insns) {
   case insns of
      {} -> c
   |  head_insns:tail_insns ->
      case head_insns of
         READ ->
            let [value, new_world] = readWorld(world) in
            eval([value:stack, state, new_world], tail_insns)
      |  WRITE ->
            let value:stack_tail = stack in
            eval([stack_tail, state, writeWorld(value, world)], tail_insns)
      |  BINOP(operation) ->
            let y:x:stack_tail = stack in
            eval([evalBinop(operation, x, y) : stack_tail, state, world], tail_insns)
      |  LD(x) -> eval([state(x) : stack, state, world], tail_insns)
      |  ST(name) ->
            let value:stack_tail = stack in
            eval([stack_tail, state <- [name, value], world], tail_insns)
      |  CONST(const) -> eval([const : stack, state, world], tail_insns)
      esac
   esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list
-- of stack machine instructions
fun compileExpr (expr) {
   case expr of
      Var(name) -> {LD(name)}
   |  Const(const) -> {CONST(const)}
   |  Binop(operation, first, second) ->
         compileExpr(first) +++ compileExpr(second) +++ {BINOP(operation)}
   esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
   case stmt of
      Assn(name, expr) -> compileExpr(expr) +++ {ST(name)}
   |  Seq(first_stmt, second_stmt) -> compileSM(first_stmt) +++ compileSM(second_stmt)
   |  Skip -> {}
   |  Read(var_name) -> {READ, ST(var_name)}
   |  Write(expr) -> compileExpr(expr) +++ {WRITE}
   esac
}
