-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> x
    esac
  }

  case i of
    READ             -> "READ"
  | WRITE            -> "WRITE"
  | BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];

  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});

  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, v) {
    case loc of
      Arg (i) -> args[i] := v
    | Loc (i) -> locs[i] := v
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then v else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  (* Assignment *)
  fun eval (c@[stack, cst, state, world], insns) {
    case insns of
      {} -> c
      | inst : insns -> 
        case inst of 
          READ -> case readWorld(world) of
            [rd, w2] ->  eval([rd : stack, cst, state, w2], insns)
          esac
        | WRITE -> case stack of 
            (x : y) -> eval([y, cst, state, writeWorld(x, world)], insns)
          esac
        | BINOP (op) -> case stack of
            (x : (y : z)) -> eval([evalOp(op, y, x) : z, cst, state, world], insns)
          esac
        | LD (x) -> eval([lookup(state, x) : stack, cst, state, world], insns)
        | ST (x) -> assign(state, x, hd(stack)); eval(c, insns) 
        | CONST (n) -> eval([n : stack, cst, state, world], insns)
        | JMP (l) -> eval(c, fromLabel(env, l))
        | LABEL (l) -> eval(c, insns)
        | CJMP (c, l) -> case stack of
            (x : y) -> case c of
                "Z" -> eval([y, cst, state, world], if x == 0 then fromLabel(env, l) else insns fi)
              | _ -> eval([y, cst, state, world], if x != 0 then fromLabel(env, l) else insns fi)
            esac
          esac
        | LDA (x) -> eval([x : stack, cst, state, world], insns)
        | DROP -> eval([tl(stack), cst, state, world], insns)
        | STI -> case stack of 
            x:y:z -> assign(state, y, x); eval([x:z, cst, state, world], insns)
          esac
        | CALL (f, n) -> eval([stack, [state, insns]:cst, state, world], fromLabel(env, f))
        | BEGIN(f, a, l) -> case take(stack, a) of 
          [stack, args] -> var x =  makeState(a, l); foldl(fun(i, arg) {
             assign(x, Arg(i), arg); i + 1 }, 0, args);
             eval([stack, cst, x, world], insns)
          esac
        | END -> case cst of 
            {} -> c
            | [state, insns]:cst -> eval([stack, cst, state, world], insns)
            esac
        | GLOBAL(x) -> assign(state, Glb(x), 0); eval(c, insns)
        | DUP -> case stack of
            (x : y) -> eval([x:x:y, cst, state, world], insns)
          esac
        esac 
    esac
  }
  (* End *)

  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a local variable for the current function
  fun addVar (name) {
    if scopeDepth == 1
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }

  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 1, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups a name of a variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Fun (_, _) -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    | x          -> x
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if scopeDepth == 1
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 1
  }

  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   beginFun,
   getFuns,
   lookupVar,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun beginFun (env, state) {
  env [6] (state)
}

fun getFuns (env) {
  env [7] ()
}

fun lookupVar (env, name) {
  env [8] (name)
}

fun lookupFun (env, name) {
  env [9] (name)
}

fun getLocals (env) {
  env [10] ()
}

fun genFunLabel (env, name) {
  env [11] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [12] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [13] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

(* Assignment *)

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for code generation:
--    a. generate the code for the topmost expression (the program itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  fun addDefs(env, defs) {
    fun addDef([env, f, gl_def], def) {
      case def of 
        Var(x) -> [addVars(env, x), f, if env.isGlobal
          then foldl(fun(gl_def, def) {gl_def <+ GLOBAL(def)}, gl_def, x)
          else gl_def
          fi]
        | Fun(name, arg, e) -> case genFunLabel(env, name) of
          [lab1, env] -> [addFun(env, name, lab1, arg.size), [lab1, arg, e] : f, gl_def]
          esac
      esac
    }

    foldl(addDef, [beginScope(env), {}, emptyBuffer()], defs)
  }

  fun compile (lab, env, stmt) {
    case stmt of
      Skip              -> [false, env, emptyBuffer ()]
    | Var    (x)        -> [false, env, singletonBuffer (LD (lookupVar(env, x)))]
    | Ref    (x)        -> [false, env, singletonBuffer (LDA (lookupVar(env, x)))]
    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (lookupVar(env, x)) <+ DROP]
    | Write (e)      ->
        case genLabel(env) of 
          [lab, env] -> 
            case compile(lab, env, e) of
              [labUsed, env, code] -> [false, env, code <+> label(lab, labUsed) <+ WRITE]
            esac
        esac
    | Assn  (x, e)   -> 
        case genLabels(env, 2) of 
          [lab1, lab2, env] ->
            case compile(lab1, env, x) of
              [lab1Used, env, left] -> 
                case compile(lab2, env, e) of
                  [lab2Used, env, right] -> [false, env, left <+> label(lab1, lab1Used) <+> right <+> label(lab2, lab2Used) <+ STI]
                esac
            esac
        esac
    | Seq   (s1, s2) ->
        case genLabel(env) of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (e, s1, s2) -> 
        case genLabels(env, 2) of
          [eLab, s2Lab, env] ->
            case compile(eLab, env, e) of
              [eLabUsed, env, eCode] ->
                case compile (lab, env, s1) of
                  [_, env, s1Code] ->
                      case compile (lab, env, s2) of
                        [_, env, s2Code] -> [true, env, eCode <+> label(eLab, eLabUsed) <+ CJMP("Z", s2Lab) <+> s1Code <+ JMP(lab) 
                          <+> label(s2Lab, true) <+> s2Code <+ JMP(lab)]
                      esac
                esac
              esac  
        esac
    | While (e, s) -> 
        case genLabels(env, 2) of
          [eLab, start, env] ->
            case compile(eLab, env, e) of
              [eLabUsed, env, eCode] ->
                case compile (start, env, s) of
                  [_, env, sCode] -> [true, env, label(start, true) <+> eCode <+> label(eLab, eLabUsed) <+ CJMP("Z", lab) <+> sCode  
                  <+ JMP(start)]
                esac
            esac
        esac

    | DoWhile (s, e) -> 
        case genLabels(env, 3) of
          [eLab, lab1, start, env] ->
            case compile(eLab, env, e) of
              [eLabUsed, env, eCode] ->
              case compile (lab1, env, s) of
                [labUsed, env, sCode] -> [labUsed, env, label(start, true) <+> sCode <+> eCode <+> label(eLab, eLabUsed) <+ CJMP("NZ", start)]
              esac
            esac
        esac
    | Binop (op, e1, e2) ->
      case genLabels(env, 2) of
          [e1Lab, e2Lab, env] ->
            case compile(e1Lab, env, e1) of
              [e1LabUsed, env, e1Code] ->
              case compile (e2Lab, env, e2) of
                [e2LabUsed, env, e2Code] -> [false, env, e1Code <+> label(e1Lab, e1LabUsed) <+> e2Code <+> label(e2Lab, e2LabUsed) <+ BINOP(op)]
              esac
            esac
        esac
    | Ignore (e) -> 
      case genLabel(env) of
        [lab1, env] ->
          case compile(lab1, env, e) of
            [labUsed, env, code] -> [false, env, code <+> label(lab1, labUsed) <+ DROP]
          esac
      esac
    | Scope (defs, e) -> case addDefs(env, defs) of
          [env, f, gl] -> case foldl(fun(env, f) {case f of 
                                          [lab, arg, e] -> rememberFun(env, lab, arg, e)
                                          esac
                                          }, env, f) of
                            env -> case compile(lab, env, e) of
                                [labUsed, env, code] -> [labUsed, leaveScope(env), gl <+> code]
                              esac
                          esac                        
        esac
      
    | Call(f, arg) -> case lookupFun(env, f) of
        Fun (fLabel, args) ->  case genLabel(env) of
          [aLabel, env] -> case compile(aLabel, env, foldl(fun (expr, arg1) {Seq(expr, arg1) }, Skip, arg)) of
              [aLabelUsed, env, aCode] -> [false, env, aCode <+> label(aLabel, aLabelUsed) <+ CALL(fLabel, args)]
          esac
        esac
      esac
    | _                 ->  failure ("compileSM not implemented\n")
    esac
  }


  fun compileFun([env, c], f@Fun(lab, arg, e, st)) {
    case genLabel(addArgs(beginFun(env, st), arg)) of
      [lab1, env] -> case compile(lab1, env, e) of 
        [lab1Used, env, code] -> [env, c <+ LABEL(lab) <+ BEGIN(lab, arg.size, getLocals(env)) <+> code <+> label(lab1, lab1Used) <+ END ]
      esac
    esac
  }

  fun compileFuns(env) {
    case getFuns(env) of
      [{}, env] -> emptyBuffer()
    | [f, env] -> case foldl (compileFun, [env, emptyBuffer()], f) of 
        [env, code] -> code <+> compileFuns(env)
      esac
    esac 
  }

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, env, code] -> getBuffer $ (
              singletonBuffer(LABEL("main")) 
              <+ BEGIN ("main", 0, getLocals(env))
              <+> code 
              <+> label (endLab, endLabUsed)
              <+ END <+> compileFuns(env))
       esac
  esac
}

