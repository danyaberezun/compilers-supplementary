-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, st, w], insns) {
    case insns of
          BINOP(op):insns_rest -> let y:x:stack_rest = stack in eval(env, [evalBinop(op, x, y):stack_rest, st, w], insns_rest) 
        | CONST(x):insns_rest -> eval(env, [x:stack, st, w], insns_rest) 
        | READ:insns_rest -> let [inp, new_w] = readWorld(w) in eval(env, [inp:stack, st, new_w], insns_rest)
        | WRITE:insns_rest -> let out:stack_rest = stack in eval(env, [stack_rest, st, writeWorld(out, w)], insns_rest)
        | LD(x):insns_rest -> eval(env, [st(x):stack, st, w], insns_rest)
        | ST(x):insns_rest -> let y:stack_rest = stack in eval(env, [stack_rest, st <- [x, y], w], insns_rest)
        | LABEL(_):insns_rest -> eval(env, c, insns_rest)
        | JMP(l):_ -> eval(env, c, fromLabel(env, l))
        | CJMP(c, l):insns_rest -> let x:stack_rest = stack in
            if case c of 
                  "z" -> x == 0 
                | "nz" -> x != 0
            esac then
                eval(env, [stack_rest, st, w], fromLabel(env, l))
            else
                eval(env, [stack_rest, st, w], insns_rest)
            fi
        | _ -> c
    esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
    case expr of
          Var(x) -> emptyBuffer() <+ LD(x)
        | Const(x) -> emptyBuffer() <+ CONST(x)
        | Binop(op, x, y) -> compileExpr(x) <+> compileExpr(y) <+ BINOP(op)
    esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If(con, t, e) ->
        let [el, env] = env.genLabel in
        let [_, env, tb] = compile(lab, env, t) in
        let [_, env, eb] = compile(lab, env, e) in
        [true, env, compileExpr(con) <+ CJMP("z", el) <+> tb <+ JMP(lab) <+ LABEL(el) <+> eb
        ]
    | DoWhile(b, con) ->
        let [ll, env] = env.genLabel in
        let [cl, env] = env.genLabel in
        let [clu, env, bc] = compile(cl, env, b) in
        [false, env, label(ll, true) <+> bc <+> label(cl, clu) <+> compileExpr(con) <+ CJMP("nz", ll)]
    | While(con, b) -> compile (lab, env, If (con, DoWhile (b, con), Skip))
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
