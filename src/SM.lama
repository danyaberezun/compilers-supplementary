-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, world], insns) {
   case insns of
        {} -> c
      | head:tail ->
          case head of
            READ -> let [value, nw] = readWorld(world)
                    in eval(env, [value:stack, state, nw], tail)
            | WRITE -> let value:stack_tail = stack
                       in eval(env, [stack_tail, state, writeWorld(value, world)], tail)
            | BINOP (op) -> let y:x:stack_tail = stack  -- FIXED ORDER: y first, then x
                            in eval(env, [evalBinop(op, x, y):stack_tail, state, world], tail)
            | LD (x) -> eval(env, [state(x):stack, state, world], tail)
            | ST (name) -> let value:stack_tail = stack
                           in eval(env, [stack_tail, state <- [name, value], world], tail)
            | CONST (int) -> eval(env, [int:stack, state, world], tail)
            | LABEL (x) -> eval(env, c, tail)
            | JMP   (x) -> eval(env, c, fromLabel (env, x))
            | CJMP  (e, x) -> let value:rest = stack in
                let go = 
                  case e of
                    "z"  -> if value == 0 then true else false fi
                  | "nz" -> if value != 0 then true else false fi
                  esac 
                in if go then eval (env, [rest, state, world], fromLabel (env, x)) else eval (env, [rest, state, world], tail) fi
          esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Var(name)              -> singletonBuffer (LD (name) )
    | Const(value)           -> singletonBuffer (CONST (value) )
    | Binop(op, x, y) -> compileExpr (x) <+> compileExpr (y) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read (x)       -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn (x, e)    -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq (stmt1, stmt2) -> case env.genLabel of
                            [nextLabel, newEnv] ->
                              case compile (nextLabel, newEnv, stmt1) of
                                [nextLabelUsed, updatedEnv, stmt1Code] ->
                                    case compile (lab, updatedEnv, stmt2) of
                                      [labUsed, finalEnv, stmt2Code] ->
                                        [labUsed, finalEnv, stmt1Code <+> label (nextLabel, nextLabelUsed) <+> stmt2Code]
                                    esac
                              esac
                            esac
    | If (cond, th, el) ->  case env.genLabel of
                              [elseLabel, newEnv] ->
                                case compile(lab, newEnv, th) of
                                  [_, thenEnv, thenCode] ->
                                    case compile(lab, thenEnv, el) of
                                      [_, finalEnv, elseCode] -> 
                                        [true, finalEnv, compileExpr(cond) <+
                                        CJMP("z", elseLabel) <+>
                                        thenCode <+
                                        JMP(lab) <+
                                        LABEL(elseLabel) <+>
                                        elseCode]
                                    esac
                                esac
                            esac
    | DoWhile (body, cond) -> case env.genLabels(2) of
                                [bodyLabel, condLabel, newEnv] ->
                                  case compile(condLabel, newEnv, body) of
                                    [condLabelUsed, finalEnv, bodyCode] ->
                                      [false, finalEnv, singletonBuffer(LABEL(bodyLabel)) <+>
                                        bodyCode <+>
                                        label(condLabel, condLabelUsed) <+>
                                        compileExpr(cond) <+
                                        CJMP("nz", bodyLabel)]
                                  esac
                              esac
    | While (cond, body) -> compile (lab, env, If (cond, DoWhile (body, cond), Skip))
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
