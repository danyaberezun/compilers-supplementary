-- Stack machine.

import Ref;
import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Parser;
import Lexer;
import Expr;
import Builtins;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  -- Shows a location (a reference to function argument, local or global variable)
  fun showLoc (d) {
    case d of
      Arg (i) -> sprintf ("arg[%d]", i)
    | Loc (i) -> sprintf ("loc[%d]", i)
    | Glb (x) -> x
    esac
  }
  
  case i of
    BINOP  (s)       -> sprintf ("BINOP %s", s)
  | LD     (x)       -> sprintf ("LD %s", showLoc (x))
  | LDA    (x)       -> sprintf ("LDA %s", showLoc (x))
  | ST     (x)       -> sprintf ("ST %s", showLoc (x))
  | STI              -> "STI"
  | CONST  (n)       -> sprintf ("CONST %d", n)
  | LABEL  (s)       -> sprintf ("LABEL %s", s)
  | JMP    (l)       -> sprintf ("JMP %s", l)
  | CJMP   (c, l)    -> sprintf ("CJMP %s, %s", c, l)
  | CALL   (f, n)    -> sprintf ("CALL %s, %d", f, n)
  | BEGIN  (f, a, l) -> sprintf ("BEGIN %s, %d, %d", f, a, l)
  | GLOBAL (x)       -> sprintf ("GLOBAL %s", x)
  | END              -> sprintf ("END")
  | DUP              -> "DUP"
  | DROP             -> "DROP"
  | STRING (s)       -> sprintf ("STRING ""%s""", s)
  | ARRAY  (n)       -> sprintf ("ARRAY %d", n)
  | SEXP   (t, n)    -> sprintf ("SEXP %s %d", t, n)
  | STA              -> "STA"
  | ELEM             -> "ELEM"
  | BUILTIN (f, n)   -> sprintf ("BUILTIN %s, %d", f, n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, a world and a program,
-- returns a final output
fun eval (env, w, insns) {
  -- Global state maps names of global variables to values
  var globalState = ref (fun (x) {error (sprintf ("name ""%s"" is undefined", x), getLoc (x))});
  
  -- Make a fresh local state: a pair of arrays for arguments and local variables;
  -- takes the numbers of arguments and local variables respectively
  fun makeState (a, l) {
    [initArray (a, fun (_) {0}), initArray (l, fun (_) {0})]
  }

  -- Lookups a location in local/global states
  fun lookup ([args, locs], loc) {
    case loc of
      Arg (i) -> args[i]
    | Loc (i) -> locs[i]
    | Glb (x) -> deref (globalState) (x)
    esac 
  }

  -- Assigns a value to a location
  fun assign ([args, locs], loc, vl) {
    case loc of
      Arg (i) -> args[i] := vl
    | Loc (i) -> locs[i] := vl
    | Glb (x) -> var g = deref (globalState);
                 globalState ::= fun (y) {if compare (x, y) == 0 then vl else g (y) fi}
    esac
  }

  -- Takes n positions from the list, retursn a pair: the remaining list and the taken
  -- sublist
  fun take (list, n) {
    fun inner (n, acc, list) {
      if n == 0
      then [list, acc]
      else inner (n-1, list.hd : acc, list.tl)
      fi
    }

    inner (n, {}, list)
  }

  -- Core interpreter: takes a configuration and a program, returns a configuration
  fun eval (config@[stack, callStack, state, world], insns) {
    case insns of
      {} -> state
    | insn : insns ->
        case insn of
          BINOP (op) ->
            let right:left:stack = stack in
            eval ([evalOp (op, left, right):stack, callStack, state, world], insns)
        | LD (loc) -> eval ([lookup (state, loc):stack, callStack, state, world], insns)
        | LDA (loc) -> eval ([Ref (loc):stack, callStack, state, world], insns)
        | ST (loc) ->
            let value:stack = stack in
            assign (state, loc, value);
            eval ([value:stack, callStack, state, world], insns)
        | STI ->
            let value:Ref (loc):stack = stack in
            assign (state, loc, value);
            eval ([value:stack, callStack, state, world], insns)
        | CONST (n) -> eval ([n:stack, callStack, state, world], insns)
        | LABEL (l) -> eval (config, insns)
        | JMP (l) -> eval (config, fromLabel (env, l))
        | CJMP (cond, l) ->
            case stack of
              n:stack ->
                if
                  case cond of
                    "nz" -> n != 0
                  | "z" -> n == 0
                  esac
                then
                  eval ([stack, callStack, state, world], fromLabel (env, l))
                else
                  eval ([stack, callStack, state, world], insns)
                fi
            esac
        | DUP ->
            case stack of
              n:stack -> eval ([n:n:stack, callStack, state, world], insns)
            esac
        | DROP ->
            case stack of
              n:stack -> eval ([stack, callStack, state, world], insns)
            esac
        | BEGIN (name, numArgs, numLocals) ->
            let [stack, args] = take (stack, numArgs) in
            let [_, locals] = makeState (numArgs, numLocals) in
            let state = [listArray (args), locals] in
            eval ([stack, callStack, state, world], insns)
        | END ->
            case callStack of
              {} -> config
            | [state, insns]:callStack -> eval ([stack, callStack, state, world], insns)
            esac
        | CALL (l, numArgs) ->
            eval ([stack, [state, insns]:callStack, state, world], fromLabel (env, l))
        | BUILTIN (x, numArgs) ->
            let [stack, args] = take (stack, numArgs) in
            let [value, world] = evalBuiltin (x, args, world) in
            eval ([value:stack, callStack, state, world], insns)
        | GLOBAL (x) ->
            assign (state, Glb (x), 0); eval (config, insns)
        | STRING (v) -> eval ([v:stack, callStack, state, world], insns)
        | ARRAY  (n) -> 
            let [stack, elems] = take (stack, n) in 
            eval ([listArray (elems):stack, callStack, state, world], insns)
        | SEXP (t, n) -> 
            let [stack, elems] = take (stack, n) in 
            eval ([Sexp (t, listArray (elems)):stack, callStack, state, world], insns)
        | STA -> 
            let value:index:arr:stack = stack in
            let arr =
              case arr of
                Sexp (_, arr) -> arr
              | arr -> arr
              esac
            in
            arr[index] := value;
            eval ([value:stack, callStack, state, world], insns)
        | ELEM -> 
            let index:arr:stack = stack in
            let arr =
              case arr of
                Sexp (_, arr) -> arr
              | arr -> arr
              esac
            in
            eval ([arr[index]:stack, callStack, state, world], insns)
        | _ -> failure ("SM eval for instruction %s is not yet implemented\n", insn.string)
        esac
    esac
  }
  
  eval ([{}, {}, makeState (0, 0), w], insns) [3].getOutput
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), createWorld (input), insns)
}

-- Compilation environment: generates labels
fun makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, functions) {
  -- Generates a new label
  fun genLabel () {
    [sprintf ("L%d", nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
  }

  -- Adds a new function 
  fun rememberFun (fLabel, args, body) {
    makeCompEnv (nLabels,
                 scopeDepth,
                 state,
                 nLocals,
                 nArgs,
                 Fun (fLabel, args, body, state) : functions)
  }

  -- Enters a local scope
  fun beginScope () {
    makeCompEnv (nLabels, scopeDepth+1, enterScope (state), nLocals, nArgs, functions)
  }

  -- Leaves a local scope
  fun endScope () {
    makeCompEnv (nLabels, scopeDepth-1, leaveScope (state), nLocals, nArgs, functions)
  }

  -- Adds an argument for the current function
  fun addArg (name) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Arg (nArgs)), nLocals, nArgs+1, functions)
  }

  -- Adds a local variable for the current function
  fun addVar (name) {
    if isGlobal ()
    then makeCompEnv (nLabels, scopeDepth, addName (state, name, Glb (name)), nLocals, nArgs, functions)
    else makeCompEnv (nLabels, scopeDepth, addName (state, name, Loc (nLocals)), nLocals+1, nArgs, functions)
    fi
  }

  -- Adds a binding for a function
  fun addFun (name, fLabel, nargs) {
    makeCompEnv (nLabels, scopeDepth, addName (state, name, Fun (fLabel, nargs)), nLocals, nArgs, functions)
  }
  
  -- Enters a function
  fun beginFun (state) {
    makeCompEnv (nLabels, 2, enterFunction (state), 0, 0, functions)
  }

  -- Get functions and empty functions list
  fun getFuns () {
    [functions, makeCompEnv (nLabels, scopeDepth, state, nLocals, nArgs, {})]
  }

  -- Lookups a name of a variable
  fun lookupVar (name) {
    case lookup (state, name) of
      Fun (_, _) -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
    | x          -> x
    esac
  }

  -- Lookups a name of a function
  fun lookupFun (name) {
    case lookup (state, name) of
      x@Fun (_, _) -> x
    | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
    esac
  }

  -- Gets the number of local variables
  fun getLocals () {
    nLocals
  }

  -- Generates an intrinsic function name
  fun genFunLabel (name) {
     if isGlobal ()
     then [sprintf ("L%s", name),             makeCompEnv (nLabels  , scopeDepth, state, nLocals, nArgs, functions)]
     else [sprintf ("L%s_%d", name, nLabels), makeCompEnv (nLabels+1, scopeDepth, state, nLocals, nArgs, functions)]
     fi 
  }

  -- Checks if the current scope is a global
  fun isGlobal () {
    scopeDepth == 2
  }
  
  [genLabel,
   rememberFun,
   beginScope,
   endScope,
   addArg,
   addVar,
   beginFun,
   getFuns,
   lookupVar,
   lookupFun,
   getLocals,
   genFunLabel,
   addFun,
   isGlobal
  ]
}

-- Initialize a new compilation environment
fun initCompEnv () {
  makeCompEnv (0, 0, emptyState (), 0, 0, {})
}

-- Accessor functions
fun genLabel (env) {
  env [0] ()
}

fun rememberFun (env, name, args, body) {
  env [1] (name, args, body)
}

fun beginScope (env) {
  env [2] ()
}

fun endScope (env) {
  env [3] ()
}

fun addArg (env, name) {
  env [4] (name)
}

fun addVar (env, name) {
  env [5] (name)
}

fun beginFun (env, state) {
  env [6] (state)
}

fun getFuns (env) {
  env [7] ()
}

fun lookupVar (env, name) {
  env [8] (name)
}

fun lookupFun (env, name) {
  env [9] (name)
}

fun getLocals (env) {
  env [10] ()
}

fun genFunLabel (env, name) {
  env [11] (name)
}

fun addFun (env, name, fLabel, nargs) {
  env [12] (name, fLabel, nargs)
}

fun isGlobal (env) {
  env [13] ()
}

-- Helper function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case genLabel (env) of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Helper function: adds a bunch of arguments
fun addArgs (env, names) {
  foldl (fun (env, name) {addArg (env, name)}, env, names)
}

-- Helper function: adds a bunch of locals
fun addVars (env, names) {
  foldl (fun (env, name) {addVar (env, name)}, env, names)
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine
-- instructions.

(* Assignment

-- Some guidelines for compiling functions and scopes.
--
-- 1. use env.beginScope/env.endScope properly;
-- 2. in each scope traverse the definitions twice:
--    a. first, add all bindings into symbolic state,
--       using end.addVar/env.addFun;
--    b. do not forget to generate GLOBAL instructions for
--       top-level variable declarations;
--    c. on the secord traversal add functions themselves for
--       future code generation (use env.rememberFun).
-- 3. the overall flow for codegeneration:
--    a. generate the code for thetopmost expression (the progrman itself);
--    b. take list of nested functions definitions via env.getFuns and
--       generate the code for each function;
--    c. repeat b. until no function definitions appear.
-- 4. when compiling functions, do not forget to create proper
--    environment (use env.beginFun) and register arguments
--    (use env.addArg).
*)
public fun compileSM (expr) {
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }
  
  fun compileMany (lab, env, exprs) {
    case exprs of
      {e}    -> compile (lab, env, e)
    | e : es ->
        case env.genLabel of
          [eLab, env] ->
            case compile (eLab, env, e) of
              [eUsed, env, eCode] ->
                 case compileMany (lab, env, es) of
                   [labUsed, env, esCode] -> [labUsed, env, eCode <+> label (eLab, eUsed) <+> esCode]
                 esac
            esac
        esac
    esac
  }

  fun compile (lab, env, expr) {
    -- printf ("expr to SM: %s\n", expr.string);
    case expr of
      Skip              -> [false, env, emptyBuffer ()]
    | Var    (name)        -> [false, env, singletonBuffer (LD (lookupVar (env, name)))]
    | Ref    (name)        -> [false, env, singletonBuffer (LDA (lookupVar (env, name)))]
    | Const  (n)        -> [false, env, singletonBuffer (CONST (n))]
    | Ignore (e)        ->
        let [eLab, env] = genLabel (env) in
        let [eLabUsed, env, eCode] = compile (eLab, env, e) in
        [false, env,
          eCode <+> label (eLab, eLabUsed) <+ DROP]
    | Binop (op, e1, e2) ->
        let [lab1, lab2, env] = env.genLabels (2) in
        let [lab1Used, env, e1Code] = compile (lab1, env, e1) in
        let [lab2Used, env, e2Code] = compile (lab2, env, e2) in
        [false, env,
          e1Code <+> label (lab1, lab1Used) <+>
          e2Code <+> label (lab2, lab2Used) <+
          BINOP (op)]
    | Assn (e1@ElemRef (ea, ei), e2) -> -- TODO: generalize
        let [eLab, env] = genLabel (env) in
        let [labUsed, newEnv, code] = compileMany (eLab, env, {e1, e2}) in
        [false, newEnv, (code <+> label (eLab, labUsed)) <+ STA]
    | Assn (e1, e2) ->
        let [lab1, lab2, env] = genLabels (env, 2) in
        let [lab1Used, env, e1Code] = compile (lab1, env, e1) in
        let [lab2Used, env, e2Code] = compile (lab2, env, e2) in
        [false, env,
          e1Code <+> label (lab1, lab1Used) <+>
          e2Code <+> label (lab2, lab2Used) <+
          STI]
    | Seq (s1, s2) ->
        let [s2Lab, env] = genLabel (env) in
        let [s2LabUsed, env, s1Code] = compile (s2Lab, env, s1) in
        let [labUsed, env, s2Code] = compile (lab, env, s2) in
        [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
    | If (s0, s1, s2) ->
        let [s0Lab, s2Lab, env] = genLabels (env, 2) in
        let [s0LabUsed, env, s0Code] = compile (lab, env, s0) in
        let [labUsed1, env, s1Code] = compile (lab, env, s1) in
        let [labUsed2, env, s2Code] = compile (lab, env, s2) in
        [true, env,
          s0Code <+>
            label (s0Lab, s0LabUsed) <+
            CJMP ("z", s2Lab) <+>
            s1Code <+
            JMP (lab) <+
            LABEL (s2Lab) <+>
            s2Code]
    | While (e, s) ->
        let [lab1, lab2, lab3, env] = genLabels (env, 3) in
        let [lab2used, env, sCode] = compile (lab2, env, s) in
        let [lab3used, env, eCode] = compile (lab3, env, e) in
        [false, env,
          singletonBuffer (JMP (lab2)) <+
            LABEL (lab1) <+>
            sCode <+
            LABEL (lab2) <+>
            eCode <+>
            label (lab3, lab3used) <+
            CJMP ("nz", lab1)]
    | DoWhile (s, e)   ->
        let [lab1, lab2, lab3, env] = genLabels (env, 3) in
        let [lab2used, env, sCode] = compile (lab2, env, s) in
        let [lab3used, env, eCode] = compile (lab3, env, e) in
        [false, env,
          singletonBuffer (LABEL (lab1)) <+>
            sCode <+>
            label (lab2, lab2used) <+>
            eCode <+>
            label (lab3, lab3used) <+
            CJMP ("nz", lab1)]
    | Scope (defs, e) ->
        let env = beginScope (env) in
        let env = foldl (fun (env, def) {
          case def of
            Var (names) -> addVars (env, names)
          | Fun (name, args, body) ->
            let [l, env] = genFunLabel (env, name) in
            addFun (env, name, l, size (args))
          esac
        }, env, defs) in
        let env = foldl (fun (env, def) {
          case def of
            Var (_) -> env
          | Fun (name, args, body) ->
            let [l, env] = genFunLabel (env, name) in
            rememberFun (env, l, args, body)
          esac
        }, env, defs) in
        let globalDefs =
          if isGlobal (env) then
            foldl (fun (code, def) {
              case def of
                Var (names) ->
                  foldl (fun (code, name) {
                    code <+ GLOBAL (name)
                  }, code, names)
                | Fun (_, _, _) -> code
              esac
            }, emptyBuffer(), defs)
          else emptyBuffer()
          fi in
        let [labUsed, env, code] = compile (lab, env, e) in
        let env = endScope (env) in
        [labUsed, env, globalDefs <+> code]
    | Call (name, exprs) ->
        let Fun (l, numArgs) = lookupFun (env, name) in
        case exprs of
          {} -> [false, env, singletonBuffer (CALL (l, numArgs))]
        | _ -> 
            let [argsLab, env] = genLabel (env) in
            let [argsLabUsed, env, argsCode] = compileMany (argsLab, env, exprs) in
            [false, env, argsCode <+> label (argsLab, argsLabUsed) <+ CALL (l, numArgs)]
        esac
    | Builtin (name, exprs) ->
        let numArgs = size (exprs) in
        -- printf ("builtin na: %d\n", numArgs);
        case exprs of
          {} -> [false, env, singletonBuffer (BUILTIN (name, 0))]
        | _ -> 
            let [argsLab, env] = genLabel (env) in
            let [argsLabUsed, env, argsCode] = compileMany (argsLab, env, exprs) in
            [false, env, argsCode <+> label (argsLab, argsLabUsed) <+ BUILTIN (name, numArgs)]
        esac
    | String (v) -> [false, env, singletonBuffer (STRING (v))]
    | Array (exprs) -> 
        let [argsLab, env] = genLabel (env) in
        let [argsLabUsed, env, argsCode] = compileMany (argsLab, env, exprs) in
        [false, env, argsCode <+> label (argsLab, argsLabUsed) <+ ARRAY (size (exprs))]
    | Sexp (tag, exprs) -> 
        case exprs of
          {} -> [false, env, singletonBuffer (SEXP (tag, 0))]
        | _ -> 
            let [argsLab, env] = genLabel (env) in
            let [argsLabUsed, env, argsCode] = compileMany (argsLab, env, exprs) in
            [false, env, argsCode <+> label (argsLab, argsLabUsed) <+ SEXP (tag, size (exprs))]
        esac
    | Elem (ea, ei) ->
        let [eaLab, env] = genLabel (env) in
        let [eaLabUsed, env, eaCode] = compile (eaLab, env, ea) in
        let [eiLab, env] = genLabel (env) in
        let [eiLabUsed, env, eiCode] = compile (eiLab, env, ei) in
        [false, env, eaCode <+> label (eaLab, eaLabUsed) <+> eiCode <+> label (eiLab, eiLabUsed) <+ ELEM]
    | ElemRef (ea, ei) ->
        let [eaLab, env] = genLabel (env) in
        let [eaLabUsed, env, eaCode] = compile (eaLab, env, ea) in
        let [labUsed, env, eiCode] = compile (lab, env, ei) in
        [labUsed, env, eaCode <+> label (eaLab, eaLabUsed) <+> eiCode]
    | _ -> failure ("compileSM not implemented for %s\n", expr.string)
    esac
  }

  fun compileFunctions (env, main) {
    fun iterate (env, funList) {
      case funList of
        {}     -> case getFuns (env) of
                    [{}, env] -> emptyBuffer ()
                  | [fs, env] -> iterate (env, fs)
                  esac
      | Fun (f, args, body, state) : fs ->
          -- printf ("compiling fun %s (%s) { %s }\n", f, args.string, body.string);
          case genLabel (addArgs (beginFun (env, state), args)) of
            [endLab, env] ->
               case compile (endLab, env, body) of
                 [endUsed, env, code] -> 
                    singletonBuffer (LABEL (f)) <+
                    BEGIN (f, args.size, getLocals (env)) <+>
                    code <+>
                    label (endLab, endUsed) <+
                    END <+>
                    iterate (env, fs)
               esac
          esac          
      esac
    }
    
    main <+> iterate (env, {})
  }
    
  let env = initCompEnv () in
  let env = beginScope (env) in
  let [endLab, env] = genLabel (env) in
  let [endLabUsed, env, code] = compile (endLab, env, expr) in
    getBuffer $
    compileFunctions (env, 
                      singletonBuffer (LABEL ("main")) <+
                      BEGIN ("main", 0, getLocals (env)) <+>
                      code <+>
                      label (endLab, endLabUsed) <+
                      END 
                     )            
}
