-- Parser
import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
public fun error(msg, loc) {
  failure("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue(atr, v, loc) {
  case atr of
      Ref  -> error("reference expected", loc)
    | Void -> Ignore(v)
    | _    -> v
  esac
}

fun assertVoid(atr, v, loc) {
  case atr of
      Void -> v
    | Val  -> error("value expected", loc)
    | Weak -> Seq(v, Const(0))
    | _    -> error("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr(l, p, r) {
  syntax(-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop(op) {
  [
    syntax(pos -s[op]),
    fun(l, loc, r) {
      fun(a) {
        assertValue(a, Binop(op, l(Val), r(Val)), loc)
      }
    }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0(item) {
  list0By(item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list(item) {
  listBy(item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope(defs, expr) {
  fun expandVarDefs(defs, expr) {
    foldr(
      fun ([defs, expr], def) {
        case def of
          [ident, None] -> [ident:defs, expr]
          | [ident, Some (value)] -> [ident:defs, Seq(Ignore(Set(ident, value)), expr)]
        esac
      },
      [{}, expr],
      defs
    )
  }

  fun expandValDefs(defs, expr) {
      foldr(
        fun([defs, expr], [ident, value]) {
          [ident:defs, Seq(Ignore(Set(ident, value)), expr)]
        },
        [{}, expr],
        defs
      )
  }

  case
    foldr(
      fun([defs, expr], def) {
        case def of
          f@Fun (_, _, _) -> [f:defs, expr]
          | Val (ds) ->
            case expandValDefs(ds, expr) of
              [ds, expr] -> [Val(ds):defs, expr]
            esac
          | Var (ds) ->
            case expandVarDefs(ds, expr) of
              [ds, expr] -> [Var(ds):defs, expr]
            esac
        esac
      },
      [{}, expr],
      defs
    ) of
    [defs, expr] -> Scope(defs, expr)
  esac
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope(expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope(defs, exprConstructor(sexpr))
    | _ -> exprConstructor(expr)
  esac
}

var primary = memo $ eta syntax(
  loc = pos x = inbr[s("["), list0(syntax(e = exp { e(Val) })), s("]")]
    { fun(a) { assertValue(a, Array(x), loc) } }
  | loc = pos x = strlit { fun(a) { assertValue(a, String(x), loc) } }
  | loc = pos x = chrlit { fun(a) { assertValue(a, Const(x), loc) } }
  | loc = pos x = decimal { fun(a) { assertValue(a, Const(stringInt(x)), loc) } }
  | x = lident args = inbr[s("("), list0(syntax(e = exp { e(Val) })), s(")")]? {
    fun(a) {
      case args of
        None -> case a of
          Ref    -> Ref(x)
          | Void -> Ignore(Var(x))
          | _    -> Var(x)
        esac
        | Some(args) -> assertValue(a, Call(x, args))
      esac
    }
  }
  | loc = pos x = uident args = inbr[s("("), list0(syntax(e = exp { e(Val) })), s(")")]? {
    fun(a) {
      assertValue(
        a,
        Sexp(
          x,
          case args of
            None -> {}
            | Some (args) -> args
          esac
        ),
        loc
      )
    }
  }
  | loc = pos kSkip { fun(a) { assertVoid(a, Skip, loc) } }
  | loc = pos kIf cond = exp kThen s1 = scopeExpr tl = elsePart
    { fun(a) { If(cond(Val), s1(a), tl(a)) } }
  | loc = pos kWhile cond = exp kDo body = scopeExpr kOd
    { fun(a) { assertVoid(a, While(cond(Val), body(Void)), loc) } }
  | loc = pos kDo body = scopeExpr kWhile cond = exp kOd {
    fun(a) {
      assertVoid(
        a,
        distributeScope(
          body(Void),
          fun(body) { DoWhile(body, cond(Val)) }
        ),
        loc
      )
    }
  }
  | loc = pos kFor pre = scopeExpr s[","] cond = exp s[","] incr = exp kDo body = exp kOd {
    fun(a) {
      assertVoid(
        a,
        distributeScope(
          pre(Void),
          fun(pre) { Seq(pre, While(cond(Val), Seq(body(Void), incr(Void)))) }
        )
      )
    }
  }
  | loc = pos what = inbr[s("("), scopeExpr, s(")")] { what }
);

var elsePart = memo $ eta syntax(
  loc = pos kElif cond = scopeExpr kThen s2 = scopeExpr tl = elsePart
    { fun(a) { If(cond(Val), s2(a), tl(a)) } }
  | loc = pos kElse s2 = scopeExpr kFi { fun(a) { s2(a) } }
  | loc = pos kFi { fun(a) { assertVoid(a, Skip, loc) } }
);
var basic = memo $ eta(
  expr(
    {
      [
        Right,
        {
          [
            s(":="),
            fun(l, loc, r) {
              fun(a) { assertValue(a, Assn(l(Ref), r(Val)), loc) }
            }
          ]
        }
      ],
      [Left, map(binop, { "!!" })],
      [Left, map(binop, { "&&" })],
      [Nona, map(binop, { "==", "!=", "<", ">", "<=", ">=" })],
      [Left, map(binop, { "+", "-" })],
      [Left, map(binop, { "*", "/", "%" })]
    },
    postfix
  )
);

var postfix = memo $ eta syntax(
  loc = pos e = primary ps = (i = inbr[s("["), exp, s("]")] { Index(i(Val)) })* {
    fun(a) {
      foldl(
        fun(e, p) {
          case p of
            Index(i) ->
              fun(a) {
                case a of
                  Ref -> ElemRef(e(Val), i)
                  | _ -> assertValue(a, Elem(e(Val), i), loc)
                esac
              }
          esac
        },
        e,
        ps
      ) (a)
    }
  }
);

var scopeExpr = memo $ eta syntax(
  ds = definition* e = exp? {
    fun (a) {
      fun (e) { case ds of { } -> e | _ -> expandScope(ds, e) esac }
      (
        case e of
          Some (e) -> e(a)
          | _ -> Skip
        esac
      )
    }
  }
);

var definition = memo $ eta syntax(
  kVar ds = list[syntax (lident (s["="] e = basic { e(Val) })?)] s[";"] { Var(ds) }
  | kVal ds = list[syntax (lident (s["="] e = basic { e(Val) }))] s[";"] { Val(ds) }
  | kFun name = lident args = inbr[s("("), list0(lident), s(")")] body = inbr[s("{"), scopeExpr, s("}")]
    { Fun(name, args, body(Weak)) }
);

var exp = memo $ eta syntax(
  basic
  | s1 = basic s[";"] s2 = exp { fun(a) { Seq(s1(Void), s2(a)) } }
);

-- Public top-level parser
public parse = syntax(s = scopeExpr { s(Void) });
