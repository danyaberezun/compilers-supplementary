-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- Primary expression
var primary = memo $ eta (decimal @ fun (x) {Const (stringInt (x))} |
                          lident  @ fun (x) {Var (x)} |
                          inbr (s ("("), exp, s (")"))),
    exp = memo $ expr({
        [Left, {
          [s ("!!"), fun (l, op, r) {Binop("!!", l, r)}]
              }
        ],
        [Left, {
          [s ("&&"), fun (l, op, r) {Binop("&&", l, r)}]
              }
        ],
        [Nona, {
          [s ("=="), fun (l, op, r) {Binop("==", l, r)}],
          [s ("!="), fun (l, op, r) {Binop("!=", l, r)}],
          [s (">="), fun (l, op, r) {Binop(">=", l, r)}],
          [s ("<="), fun (l, op, r) {Binop("<=", l, r)}],
          [s ("<"), fun (l, op, r) {Binop("<", l, r)}],
          [s (">"), fun (l, op, r) {Binop(">", l, r)}]
              }
        ],
        [Left, {
          [s ("+"), fun (l, op, r) {Binop("+", l, r)}],
          [s ("-"), fun (l, op, r) {Binop("-", l, r)}]
              }
        ],
        [Left, {
          [s ("*"), fun (l, op, r) {Binop("*", l, r)}],
          [s ("/"), fun (l, op, r) {Binop("/", l, r)}],
          [s ("%"), fun (l, op, r) {Binop("%", l, r)}]
                }
        ]
        },
        primary);

var endIfStmt = memo $ eta syntax (
  kFi {Skip} |
  kElse s=stmt kFi {s} |
  kElif e=exp kThen s=stmt end=endIfStmt {If(e, s, end)}
);

var singleStmt = memo $ eta syntax (
  kRead v=inbr[s("("), lident, s(")")] {Read(v)} |
  kWrite e=inbr[s("("), exp, s(")")] {Write(e)} |
  kSkip {Skip} |
  kIf e=exp kThen st=stmt end=endIfStmt {If(e, st, end)} |
  kWhile e=exp kDo st=stmt kOd {While(e, st)} |
  kDo st=stmt kWhile e=exp kOd {DoWhile(st, e)} |
  kFor s1=stmt s[","] e=exp s[","] s2=stmt kDo s3=stmt kOd {Seq(s1, While(e, Seq(s3, s2)))} |
  w=lident s[":="] y=exp {Assn(w, y)}
);

var stmt = memo $ eta syntax (
  w=singleStmt s[";"] y=stmt {Seq(w, y)} |
  w=singleStmt {w}
);
-- Public top-level parser
public parse = stmt;
