-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun operatorBuilder (leftSide, operation, rightSide) {
  Binop (operation, leftSide, rightSide)
}

-- Primary expression
var basicElement = memo $ eta (decimal @ fun (x) {Const (stringInt (x))} |
                          lident  @ fun (x) {Var (x)} |
                          inbr (s ("("), expression, s (")"))),
    expression = memo $ expr ({
      [Left, {[s ("!!"), operatorBuilder]}],
      [Left, {[s ("&&"), operatorBuilder]}],
      [Nona, {[s ("=="), operatorBuilder], [s ("!="), operatorBuilder],
              [s ("<="), operatorBuilder], [s ("<"), operatorBuilder],
              [s (">="), operatorBuilder], [s (">"),  operatorBuilder]}],
      [Left, {[s ("+"),  operatorBuilder], [s ("-"), operatorBuilder]}],
      [Left, {[s ("*"),  operatorBuilder], [s ("/"), operatorBuilder], [s ("%"), operatorBuilder]}]
    }, basicElement);

var statement = memo $ eta syntax (
      kRead x=inbr[s ("("), lident, s (")")] {Read (x)} |
      kWrite x=inbr[s ("("), expression, s (")")] {Write (x)} |
      kSkip {Skip} |
      kIf e=expression kThen s1=stmt s2=elifStmt {If (e, s1, s2)} |
      kWhile e=expression kDo s=stmt kOd {While (e, s)} |
      kDo s=stmt kWhile e=expression kOd {DoWhile (s, e)} |
      kFor s1=stmt s [","] e=expression s [","] s2=stmt kDo s3=stmt kOd {Seq (s1, While (e, Seq (s3, s2)))} |
      x=lident s [":="] e=expression {Assn (x, e)}
    ),
    elifStmt = memo $ eta syntax (
      kElif e=expression kThen s1=stmt s2=elifStmt {If (e, s1, s2)} |
      kElse s=stmt kFi {s} |
      kFi {Skip}
    ),
    stmt = memo $ eta syntax (
      statement | 
      f=statement s[";"] rest=stmt {Seq (f, rest)}
    );

-- Public top-level parser
public parse = stmt;
