-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun inbkts (p) {
  inbr (s("("), p, s(")"))
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  inbkts[exp]),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- TODO: reimplement as expressions
var singleStmt = memo $ eta syntax (
      kRead x=inbkts[lident] { Read (x) }
    | kWrite e=inbkts[exp] { Write (e) }
    | kSkip { Skip }
    | kIf cond=exp kThen pbr=stmt 
        brs=(kElif condi=exp kThen bri=stmt { Br (condi, bri) })* 
        nbr=(-kElse nbr=stmt)? kFi
         {
          let cont = foldl (
            fun (cont, Br (cond, br)) { fun (x) { cont (If (cond, br, x)) } },
            fun (x) { If (cond, pbr, x) },
            brs
          ) in 
            case nbr of 
              Some (nbr) -> cont (nbr)
            | _          -> cont (Skip)
            esac
      }
    | kWhile cond=exp kDo body=stmt kOd { While (cond, body) }
    | kDo body=stmt kWhile cond=exp kOd { DoWhile (cond, body) }
    | kFor ini=stmt comma cond=exp comma upd=stmt kDo body=stmt kOd { 
        Seq (ini, While (cond, Seq (body, upd)))
      }
    ),
    stmt = memo $ eta syntax (s1=singleStmt semicolon s2=stmt { Seq (s1, s2) } | singleStmt);
   

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
