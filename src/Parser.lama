-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  loc=pos x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |

                                  -- skip expression
                                  loc=pos kSkip                                             {fun(a) {assertVoid (a, Skip, loc)}} |

                                  -- read expression
                                  loc=pos kRead   into = inbr[s("("), exp, s(")")]       {fun(a) {assertVoid (a, Read(into (Ref)), loc)}} |

                                  -- write expression
                                  loc=pos kWrite   what = inbr[s("("), exp, s(")")]    { fun(a) {assertVoid(a,Write(what (Val)),loc)}} |

                                  -- if expression
                                  loc=pos kIf   cond = exp   kThen   s1 = exp   tl = ifElse
                                               { fun(a) { If(cond(Val), s1(a), tl(a))  }  }                                     |
                                  
                                  -- while expression
                                  loc=pos kWhile   cond = exp   kDo   body = exp   kOd
                                               { fun(a) { assertVoid(a,While (cond(Val), body(Void)),loc) }  }  |
                                  
                                  -- dowhile expression
                                  loc=pos kDo   body = exp   kWhile   cond = exp   kOd
                                              { fun(a) { assertVoid(a,DoWhile(body(Void), cond(Val)),loc) } } |
                                  -- for expression
                                  loc=pos kFor   pre = exp   s[","]   cond = exp   
                                            s[","]   incr = exp   kDo   body = exp   kOd
                                                                                      { fun (a) { assertVoid(a,Seq(pre(Void), While(cond(Val), Seq(body(Void), incr(Void))))) } } |
                                  -- brackets expression
                                  loc=pos what=inbr[s("("), exp, s(")")]    { what }
                                  ),
    ifElse = memo $ eta syntax(
                                    loc=pos kFi { fun(a){assertVoid(a, Skip, loc)} } |
                                    loc=pos kElse   s2 = exp   kFi   { fun(a){ s2(a) } } |
                                    loc=pos kElif   cond = exp   kThen   s2 = exp   tl = ifElse { fun(a){If(cond(Val), s2(a), tl(a))} }
                                  ),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});


-- parse = syntax (s=exp {s (Void)});


-- var parsed = case parseString(parse |> bypass(end), 
--         "write ((read (x); x) + (read (y); y))"
--     ) of
--     Succ(x) -> x |
--     x -> x
-- esac;
-- printf("%s\n", parsed.string)