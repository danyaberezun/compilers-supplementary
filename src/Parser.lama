-- Parser 

import Matcher;
import Ostap;
import Lexer;
import List;
import Fun;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

var primary = memo $ eta syntax ( x=decimal {Const (stringInt (x))}
                                | x=lident  {Var (x)}
                                | inbr[s("("), exp, s(")")] );

fun bw (l, op, r) {  -- binop wrapper
  Binop (op, l, r)
}

var exp  = memo $ expr ({
                          [Left, {[s ("!!"), bw]}],
                          [Left, {[s ("&&"), bw]}],
                          [Left, {[s ("<"), bw], [s (">"), bw], [s ("<="), bw], [s (">="), bw], [s ("=="), bw], [s ("!="), bw]}],
                          [Left, {[s ("+"), bw], [s ("-"), bw]}],
                          [Left, {[s ("*"), bw], [s ("/"), bw], [s ("%"), bw]}]
                        }, primary);

var stmt = memo $ eta syntax ( x=lident s[":="] e=exp         {Assn (x, e)}
                             | kSkip                          {Skip}
                             | kRead  s["("] x=lident s[")"]  {Read (x)}
                             | kWrite s["("] e=exp    s[")"]  {Write (e)} );

var stmts = memo $ expr ({[Left, {[s (";"), fun (l, _, r) {Seq (l, r)}]}]}, stmt);

-- Public top-level parser
public parse = stmts;

(*
fun debugThing (thing) {
  case thing of
    Const (i)        -> sprintf ("%d", i)
  | Var   (x)        -> sprintf ("$%s", x)
  | Binop (op, l, r) -> sprintf ("(%s)%s(%s)", debugThing (l), op, debugThing (r))
  | Assn  (x, e)     -> sprintf ("%s:=%s", x, debugThing (e))
  | Seq   (s, t)     -> sprintf ("%s;\n%s", debugThing (s), debugThing (t))
  | Skip             -> sprintf ("`")
  | Read  (x)        -> sprintf (">>>%s", x)
  | Write (x)        -> sprintf ("%s>>>", x)
  esac
}

fun debugParseRes (parseRes) {
  case parseRes of
    Succ (v) -> v.debugThing
  | Fail (e, f, g) -> sprintf ("X %s|%s|%s", e, f, g)
  esac
}

failure ("@ %s\n", (eta syntax (parse -end)).parseString("read (x);\nread (y);\nz := x * y * 3;\nwrite (z)").debugParseRes)
*)
