-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;
import STM;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- AST environment
fun createEnv (id) {
  fun freshName () {
    [createEnv (id+1), sprintf ("_tmp%d", id)]
  }

  [freshName]
}

fun emptyEnv () {
  createEnv (0)
}

fun freshName (env) {
  env [0] ()
}

-- Attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, vl, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> vl => fun (vl) {Ignore (vl)}
  | _    -> vl
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, vl, loc) {
  case atr of
    Void -> vl
  | Val  -> error ("value expected", loc)
  | Weak -> vl => fun (vl) {Seq (vl, Const (0))}
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, l (Val) =>> fun (l) {
                                            r (Val) =>  fun (r) {
                                              Binop (op, l, r)
                                            }}, loc)
                          }
                        }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

fun caseBranches (item) {
  listBy (item, s("|"))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandVarDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Set (ident, value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  fun expandValDefs (defs, expr) {
    foldr (fun ([defs, expr], [ident, value]) {     
             [ident : defs, Seq (Ignore (Set (ident, value)), expr)]
           },
           [{}, expr],
           defs)
  }
  
  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Val (ds) ->
                 case expandValDefs (ds, expr) of
                   [ds, expr] -> [Val (ds) : defs, expr]
                 esac
             | Var (ds) ->             
                 case expandVarDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> exprConstructor(sexpr) => fun (expr) { Scope (defs, expr) }
  | _                   -> exprConstructor (expr)
  esac
}

-- Helper AST function: reifies pattern bindings into scope
-- definitions and assignments; takes an returns lists of
-- pattern-branch pairs
fun reifyPatternBindings (matched, brs) {
  map (
    fun ([pat, br]) {      
      fun collectBindings (path, p) {
        fun collectList (path, ps) {
          case
            foldl (fun ([acc, i], p) {
                     [collectBindings (i : path, p) : acc, i+1]                      
                   },
                   [{}, 0],
                   ps) of
            [acc, _] -> flatten (acc)
          esac            
        }
        
        case p of
          Sexp  (_, ps)   -> collectList (path, ps)
        | Array (ps)      -> collectList (path, ps)
        | Named (name, p) -> [name, path] : collectBindings (path, p)
        |  _              -> {}
        esac
      }

      case collectBindings ({}, pat) of
        {}       -> [pat, br]
      | bindings ->
           [pat,
             expandScope (
               {Val (map (
                      fun ([name, path]) {
                        [name, foldr (fun (p, i) {Elem (p, Const (i))}, Var (matched), path)]
                      },
                      bindings))},
               br
             )]
      esac
    },
    brs
  )
}

var
  primary = memo $ eta syntax (
    -- array constant
    loc=pos x=inbr[s("["), list0(syntax (e=exp {e(Val)})), s("]")] {
      fun (a) {assertValue (a, chainST (x) => fun (x) {Array (x)}, loc)}
    } |
                                      
    -- string constant
    loc=pos x=strlit {
      fun (a) {assertValue (a, returnST $ String (x), loc)}
    } |
                                      
    -- character literal
    loc=pos x=chrlit {
      fun (a) {assertValue (a, returnST $ Const (x), loc)}
    } |
                                      
    -- decimal constant
    loc=pos x=decimal {
      fun (a) {assertValue (a, returnST $ Const (stringInt (x)), loc)}
    } |
                                      
    -- identifier
    loc=pos x=lident args=inbr[s("("), list0(syntax(e=exp {e(Val)})), s(")")]? {
      fun (a) {
        case args of
          None -> returnST $ case a of
                               Ref  -> Ref (x)
                             | Void -> Ignore (Var (x))
                             | _    -> Var (x)
                             esac
        | Some (args) -> assertValue (a, chainST (args) => fun (args) {Call (x, args)}, loc)
        esac}
    } |

    -- S-expression
    loc=pos x=uident args=inbr[s("("), list0(syntax(e=exp {e (Val)})), s(")")]? {
      fun (a) {
        assertValue (a, case args of
                          None        -> returnST $ Sexp (x, {})
                        | Some (args) -> chainST (args) => fun (args) {Sexp (x, args)}
                        esac, loc)}
    } |

    loc=pos kWhile e=exp kDo body=scopeExpr kOd    {fun (a) {
                                                        assertVoid(a, e(Val) =>> fun(e) {
                                                          body(Void) => fun(body) {
                                                            While(e, body)
                                                          }
                                                        }, loc)
                                                      }} |

    loc=pos kFor s1=scopeExpr s[","] e=exp s[","] s2=exp kDo s3=scopeExpr kOd {fun(a) {
                                                                                  s1(Void) =>> fun (s1) {
                                                                                    fun addIni(ini) {
                                                                                      assertVoid(a, 
                                                                                        e(Val) =>> fun (e) {
                                                                                          s3(Void) =>> fun (s3) {
                                                                                            s2(Void) => fun (s2) {
                                                                                              Seq (ini, While (e, Seq(s3, s2)))
                                                                                            }
                                                                                          }
                                                                                        }, 
                                                                                      loc)
                                                                                    }
                                                                                    distributeScope(s1, addIni)
                                                                                  }
                                                                              }} |

    loc=pos kDo body=scopeExpr kWhile e=exp kOd    {fun (a) { 
                                                              body(Void) =>> fun (body) {
                                                                  fun addBody(body) {
                                                                    assertVoid(a, 
                                                                      e(Val) => fun (e) {
                                                                          DoWhile(body, e)
                                                                      },
                                                                      loc
                                                                    )
                                                                  }
                                                                  distributeScope(body, addBody)
                                                              }
                                                  }} |

    kIf e=exp kThen tru=scopeExpr kElse fls=scopeExpr kFi    {fun (a) {
                                                      e(Val) =>> fun(e) {
                                                        tru(a) =>> fun(tru) {
                                                          fls(a) => fun(fls) {
                                                            If (e, tru, fls)
                                                          }
                                                        }
                                                      }
                                                  }} |

    kIf e=exp kThen tru=scopeExpr fls=elifB      {fun (a) {
                                                      e(Val) =>> fun(e) {
                                                        tru(a) =>> fun(tru) {
                                                          fls(a) => fun(fls) {
                                                            If (e, tru, fls)
                                                          }
                                                        }
                                                      }
                                                  }} |

    loc=pos kSkip                          {fun (a) {
                                              assertVoid(a, returnST $ Skip, loc)
                                          }} |

    x=inbr[s ("("), scopeExpr, s (")")] {x} |

    kCase e=exp kOf brs=caseBranches[syntax ( p=pat s["->"] e=exp { fun (a) {
                                      e(a) => fun (e) {
                                        [p, e]
                                      }}})] kEsac { fun(a) {
                                        
                                        e(Val) =>> fun (e) {
                                          freshName =>> fun (name) {
                                            chainST(map(fun (br) { br(a) }, brs)) => fun (brs) {
                                              case reifyPatternBindings(name, brs) of brs -> (
                                                var defValue = case a of  -- Dirty hack: in case of error defValue makes stack size equal for branches and failed expr => SM invariant is preserved
                                                  Ref -> Ref (name)
                                                | Void -> Skip
                                                | _ -> Var (name)
                                                esac;
                                                Scope ({Var({name})}, Seq(Ignore(Set(name, e)), Case(name, brs, defValue)))
                                              )
                                              esac
                                            }
                                          }
                                        }
                                    }}),

  pat = memo $ eta syntax (
    x=decimal { Const (stringInt(x)) } |
    s["_"] { Wildcard } |
    x=inbr[s("["), list0(syntax (e=pat {e})), s("]")] { Array(x) } |
    x=uident args=inbr[s("("), list0(syntax(p=pat {p})), s(")")]?  {case args of
                          None        -> Sexp (x, {})
                        | Some (args) -> Sexp (x, args)
                        esac} |
    x=lident s["@"] p=pat { Named(x, p) } |
    x=lident { Named(x, Wildcard) }
  ),


  elifB = memo $ eta syntax (kFi {fun (a) {
                                    assertVoid(a, returnST $ Skip, loc)
                                  }} |
  kElif e=exp kThen tru=scopeExpr kElse fls=scopeExpr kFi {fun (a) {
                                              e(Val) =>> fun(e) {
                                                tru(a) =>> fun(tru) {
                                                  fls(a) => fun(fls) {
                                                    If (e, tru, fls)
                                                  }
                                                }
                                              }
                                                }} |
  kElif e=exp kThen tru=scopeExpr fls=elifB {fun (a) {
                                              e(Val) =>> fun(e) {
                                                tru(a) =>> fun(tru) {
                                                  fls(a) => fun(fls) {
                                                    If (e, tru, fls)
                                                  }
                                                }
                                              }
                                      }}),

  basic = memo $ eta (
    expr ({[Right, {[s (":="), fun (l, loc, r) {
                                 fun (a) {assertValue (a, l (Ref) =>> fun (l) {
                                                          r (Val) =>  fun (r) {
                                                          Assn (l, r)
                                                          }},
                                                        loc)
                                 }
                               }]}],
           [Left , map (binop, {"!!"})],
           [Left , map (binop, {"&&"})],
           [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
           [Left , map (binop, {"+", "-"})],
           [Left , map (binop, {"*", "/", "%"})]
          },
          postfix)
  ),
  
  postfix = memo $ eta syntax (
    loc=pos e=primary ps=(i=inbr[s("["), exp, s("]")] {Index (i (Val))})* {
      fun (a) {
        foldl (fun (e, p) {                                                                            
                 case p of                                                                                   
                   Index (i) ->
                    fun (a) {
                      case a of
                        Ref -> e (Val) =>> fun (e) {
                               i       =>  fun (i) {
                                 ElemRef (e, i)
                               }} 
                      | _   -> assertValue (a, e (Val) =>> fun (e) {
                                               i       =>  fun (i) {
                                               Elem (e, i)
                                               }},
                                            loc)
                      esac
                    }
                 esac                                                                            
               }, e, ps) (a)
      }}
  ),
  
  scopeExpr  = memo $ eta syntax (ds=definition* e=exp? {
    fun (a) {
      fun (e) {
        case ds of
          {} -> e
        | _  -> chainST (ds) =>> fun (ds) {
                e            =>  fun (e)  {
                  expandScope (ds, e)
                }}
        esac
      }
      (case e of 
        Some (e) -> e (a)
      | _        -> returnST $ Skip
      esac)
    }}                                                                  
  ),
  
  definition = memo $ eta syntax (
    kVar ds=list[syntax (x=lident e=(-s["="] basic)? {case e of
                                                        None -> returnST $ [x, None]
                                                      | Some (e) ->
                                                          e (Val) => fun (e) {[x, Some (e)]}
                                                      esac})] s[";"] {
      chainST (ds) => fun (ds) {Var (ds)}
    } |
    kVal ds=list[syntax (x=lident s["="] e=basic {e (Val) => fun (e) {[x, e]}})] s[";"] {
      chainST (ds) => fun (ds) {Val (ds)}
    } |  
    kFun name=lident args=inbr[s("("), list0 (lident), s(")")] body=inbr[s("{"), scopeExpr, s("}")] {
      body (Weak) => fun (body) {Fun (name, args, body)}
    }
  ),
  
  exp = memo $ eta syntax (
    basic
  | s1=basic s[";"] s2=exp {
     fun (a) {
       s1 (Void) =>> fun (s1) {
       s2 (a)    =>  fun (s2) {
         Seq (s1, s2)
       }}
     }
   }
  );

-- Public top-level parser
public parse = syntax (s=scopeExpr {s (Void) (emptyEnv ()) [1]});
