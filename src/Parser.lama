-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | Val -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val -> error ("value expected", loc)
  | Ref -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [
    syntax (pos -s[op]), 
    fun (l, loc, r) {
      fun (a) {
        assertValue (a, Binop (op, l (Val), r (Val)), loc)
      }
    }
  ]
}

fun mkOps () {
  {[Right, {
      [s (":="),
        fun (l, loc, r) {
          fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
        }
      ]
    }],
    [Left , map (binop, {"!!"})],
    [Left , map (binop, {"&&"})],
    [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
    [Left , map (binop, {"+", "-"})],
    [Left , map (binop, {"*", "/", "%"})]
    }
}

var primary = 
      memo $ eta syntax (
          loc=pos x=decimal { 
            fun (a) { 
              assertValue (a, Const (stringInt (x)), loc) 
            } 
          }
        | x=lident {
            fun (a) {
              case a of
                Ref -> Ref (x)
              | Void -> Ignore (Var (x))
              | Val -> Var (x)
              esac
            }
          } 
        | loc=pos kRead i=inbr[s ("("), lident, s (")")] {
            fun (a) { 
              assertVoid (a, Read (i), loc) 
            } 
          }
        | loc=pos kWrite e=inbr[s ("("), exp, s (")")] { 
            fun (a) {
              assertVoid (a, Write (e (Val)), loc)
            }
          }
        | inbr [s ("("), exp, s (")")]
        | loc=pos kWhile e=exp kDo s=exp kOd {
            fun (a) {
              assertVoid (a, While (e (Val), s (Void)), loc)
            }
          }
        | kIf cond=exp kThen bt=exp kFi { 
            fun (a) { 
              If (cond (Val), bt (a), Skip)
            } 
          }
        | kIf cond=exp kThen bt=exp bf=ifEnd kFi {
            fun (a) {
              If (cond (Val), bt (a), bf (a))
            }
          }
        | loc=pos kDo b=exp kWhile cond=exp kOd { 
            fun (a) {
              assertVoid (a, DoWhile (b (Void), cond (Val)), loc)
            }
          }
        | loc=pos kSkip { fun (a) { assertVoid (a, Skip, loc) } }
        | loc=pos kFor e1=exp s [","] e2=exp s [","] e3=exp
              kDo b=exp kOd {
            fun (a) { assertVoid (
              a, Seq (e1 (Void), While (e2 (Val), Seq (b (Void), e3 (Void)))), loc
            ) }
          }
        -- | $(failure ("the rest of primary parsing in not implemented\n"))
      ),
    ifEnd =
      memo $ eta syntax (
          kElse b=exp { fun (a) { b (a) } }
        | kElif cond=exp kThen bt=exp bf=ifEnd {
            fun (a) {
              If (cond (Val), bt (a), bf (a))
            }
          }
        | kElif cond=exp kThen b=exp { 
            fun (a) { 
              If (cond (Val), b (a), Skip) 
            } 
          }
      ),
    basic = memo $ eta (expr (mkOps (), primary)),
    exp = 
      memo $ eta syntax (
          basic 
        | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}}
      );

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
