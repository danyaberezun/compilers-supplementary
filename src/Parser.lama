-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var stmtElse = memo $ eta syntax (
  kFi {fun (a) {assertVoid (a, Skip, loc)}} |
  kElif e=exp kThen s=exp o=stmtElse {fun (a) {If (e (Val), s (a), o (a))}} |
  kElse s=exp kFi {s} 
);

var primary  = memo $ eta syntax (
                                  -- decimal constant
                                  loc=pos x=decimal                                          {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                                   {fun (a) {
                                                                                               case a of
                                                                                                 Ref  -> Ref (x)
                                                                                               | Void -> Ignore (Var (x))
                                                                                               | _    -> Var (x)
                                                                                               esac
                                                                                             }} |
                                  inbr[s("("), exp, s(")")] |
                                  loc=pos kRead x=inbr[s("("), lident, s(")")]               {fun (a) {assertVoid (a, Read (x), loc)}} |
                                  loc=pos kWrite e=inbr[s("("), exp, s(")")]                 {fun (a) {assertVoid (a, Write (e (Val)), loc)}} |
                                  loc=pos kSkip                                              {fun (a) {assertVoid (a, Skip, loc)}} |
                                  loc=pos kIf e=exp kThen i=exp el=stmtElse                  {fun (a) {If (e (Val), i (a), el (a))}} |
                                  loc=pos kWhile e=exp kDo s=exp kOd                         {fun (a) {assertVoid (a, While (e (Val), s (Void)), loc)}} |
                                  loc=pos kDo e=exp kWhile s=exp kOd                         {fun (a) {assertVoid (a, DoWhile (e (Void), s (Val)), loc)}} |
                                  loc=pos kFor i=exp s[","] e=exp s[","] s=exp kDo f=exp kOd {fun (a) {assertVoid (a, Seq (i (Void), While (e (Val), Seq (f (Void), s (Void)))), loc)}}),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary)),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
