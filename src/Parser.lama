-- Parser 
import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun value (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun void (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | Weak -> Seq (v, Const (0))
  | _    -> error ("reference expected", loc)
  esac
}

fun ref (atr, v, loc) {
  case atr of
      Ref  -> Ref (v)
    | Void -> Ignore (Var (v))
    | _    -> Var (v)
    esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binopSyntax (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            value (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

fun binop(assoc, ops) {
  [assoc, map (binopSyntax, ops)]
}

fun binops(aops) {
  map (fun ([assoc, ops]) { binop(assoc, ops) }, aops)
}

var assignOp =  [Right, 
          {[
          s (":="), 
          fun (l, loc, r) {
                        fun (a) {
                          value (a, Assn (l (Ref), r (Val)), loc)
                        }
                    }
                    ]}
              ];

var elseBody = memo $ eta syntax (
        loc=pos kElse elseSt=scopeExpr kFi { 
          fun(a) {
            elseSt (a)
          }
        }
      | loc=pos kElif ifSt=ifBody { 
          fun (a) {
            ifSt (a)
          }
        }
      | loc=pos kFi { 
          fun (a) {
            void (a, Skip, loc)
          }
       }
    ),
    ifBody = memo $ eta syntax (
      loc=pos cond=exp kThen thenB=scopeExpr elseB=elseBody { 
        fun (a) {
          If (cond (Val), thenB (a), elseB (a))
        }
      }
    );

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }

  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Var (ds)      ->
                 case expandDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}

fun par(p) {
  inbr(s ("("), p, s (")"))
}

fun brak(p) {
  inbr(s ("["), p, s ("]"))
}

 
var primary = memo $ eta syntax (
                  par[scopeExpr]
                | loc=pos kSkip { -- Skip
                  fun (a) {
                      void (a, Skip, loc)
                  }
                  }
                | loc=pos kIf body=ifBody { -- If
                  fun (a) {
                    body (a)
                  }
                  }
                | loc=pos kWhile cond=exp kDo x=scopeExpr kOd { -- While (cond, x)
                  fun (a) {
                    void (a, While(cond (Val), x (Void)), loc)
                  }
                  }
                | loc=pos kFor x=scopeExpr s[","] cond=exp s[","] z=exp kDo y=scopeExpr kOd { -- x;While (cond, y;z)
                  fun (a) {
                    void (
                      a,
                      distributeScope(x (Void), fun (x) { Seq (x, While(cond (Val), Seq (y (Void), z (Void)))) }),
                      loc
                    )
                  }
                 }
                | loc=pos kDo x=scopeExpr kWhile cond=exp kOd { -- DoWhile(x, cond)
                  fun (a) {
                    void (
                      a,
                      distributeScope(x (Void), fun (x) { DoWhile(x, cond (Val)) }),
                      loc
                    )
                  }
                  }
                | loc=pos x=lident args=par[list0(syntax(e=exp {e(Val)}))]? { -- Val (x) / Ref (x) / Val (Call (x, args))
                  fun (a) {
                    case args of
                      None -> ref (a, x, loc)
                    | Some (args) -> value (a, Call (x, args), loc)
                    esac
                  }
                  }
                | loc=pos x=brak[list0(syntax (e=exp {e(Val)}))] {
                  fun (a) {
                    value (a, Array (x), loc)
                  }
                  }
                | loc=pos x=strlit {
                  fun (a) {
                    value (a, String (x), loc)
                  }
                  }
                | loc=pos x=chrlit {
                  fun (a) {
                    value (a, Const (x), loc)
                  }
                  } 
                | loc=pos x=decimal {
                  fun (a) {
                    value (a, Const (stringInt (x)), loc)
                  }
                  } 
                | loc=pos x=uident args=par[list0(syntax(e=exp {e(Val)}))]? {
                  fun (a) {
                    args := case args of
                      None -> {}
                    | Some (args) -> args
                    esac;
                    value (a, Sexp (x, args), loc)
                  }
                  } 
                ),
      basic = memo $ eta (expr (assignOp : binops({
                                  [Left, {"!!"}],
                                  [Left, {"&&"}],
                                  [Nona, {"==", "!=", "<", ">", "<=", ">="}],
                                  [Left, {"+", "-"}],
                                  [Left, {"*", "/", "%"}]
                                 }),
                                 postfix)),
      postfix = memo $ eta syntax (
        loc=pos e=primary ps=(i=brak[exp] {Index (i (Val))})* {
          fun (a) {
            foldl (fun (e, p) {
              case p of                                                                                   
                  Index (i) -> fun (a) {
                    case a of
                      Ref -> ElemRef (e (Val), i)
                    | _   -> value (a, Elem (e (Val), i), loc)
                    esac
                  }
              esac                                                                            
            }, e, ps) (a)
          }
        }
      ),
      scopeExpr = memo $ eta syntax (
        ds=definition* e=exp? {
          fun (a) {
            fun (e) {
            case ds of
              {} -> e
            | _  -> expandScope (ds, e) 
            esac
            }(case e of 
               Some (e) -> e (a)
             | _        -> Skip
             esac)
          }
          }
      ),
      definition = memo $ eta syntax (kVar ds=list[syntax (lident (s["="] e=basic {e (Val)})?)] s[";"] {Var (ds)} |
                                      kVal ds=list[syntax (lident (s["="] e=basic {e (Val)}))]  s[";"] {Val (ds)} |
                                      kFun name=lident
                                           args=inbr[s("("), list0 (lident), s(")")]
                                           body=inbr[s("{"), scopeExpr, s("}")] {
                                           Fun (name, args, body (Weak))
                                      }
                                     ),                                    
      exp        = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=scopeExpr {s (Void)});
