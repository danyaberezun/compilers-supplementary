-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- Primary expression
var primary = memo $ eta (decimal @ fun (x) {Const (stringInt (x))} |
                          lident  @ fun (x) {Var (x)} |
                          inbr (s ("("), exp, s (")"))),
    exp = memo $ eta expr({
    [Left, {
      [s("!!"), fun (l, op, r) { Binop(op, Binop("!=", l, Const(0)), Binop("!=", r, Const(0))) }]
    }],
    [Left, {
      [s("&&"), fun (l, op, r) { Binop(op, Binop("!=", l, Const(0)), Binop("!=", r, Const(0))) }]
    }],
    [Nona, {
      [s("=="), fun (l, op, r) { Binop(op, l, r) }],
      [s("!="), fun (l, op, r) { Binop(op, l, r) }],
      [s("<="), fun (l, op, r) { Binop(op, l, r) }],
      [s("<"), fun (l, op, r) { Binop(op, l, r) }],
      [s(">="), fun (l, op, r) { Binop(op, l, r) }],
      [s(">"), fun (l, op, r) { Binop(op, l, r) }]
    }],
    [Left, {
      [s("+"), fun (l, op, r) { Binop(op, l, r) }],
      [s("-"), fun (l, op, r) { Binop(op, l, r) }]
    }],
    [Left, {
      [s("*"), fun (l, op, r) { Binop(op, l, r) }],
      [s("/"), fun (l, op, r) { Binop(op, l, r) }],
      [s("%"), fun (l, op, r) { Binop(op, l, r) }]
    }]
  }, primary);

var elifStmt = memo $ eta syntax (
    e=exp kThen s1=stmt kElif s2=elifStmt { If(e, s1, s2) }
  | e=exp kThen s1=stmt kElse s2=stmt kFi { If(e, s1, s2) }
);

var nonSeqStmt = memo $ eta syntax (
    kSkip { Skip }
  | kRead x=inbr[s("("), lident, s(")")] { Read(x) }
  | kWrite x=inbr[s("("), exp, s(")")] { Write(x) }
  | x=lident s[":="] v=exp { Assn(x, v) }
  | kIf e=exp kThen s1=stmt kElse s2=stmt kFi { If(e, s1, s2) }
  | kIf e=exp kThen s=stmt kFi { If(e, s, Skip) }
  | kIf e=exp kThen s1=stmt kElif s2=elifStmt { If(e, s1, s2) }
  | kWhile e=exp kDo s=stmt kOd { While(e, s) }
  | kDo s=stmt kWhile e=exp kOd { DoWhile(s, e) }
  | kFor s1=stmt s[","] e=exp s[","] s2=stmt kDo s3=stmt kOd { Seq(s1, While(e, Seq(s3, s2))) }
);

var stmt = memo $ eta syntax (
    s1=nonSeqStmt s[";"] s2=stmt { Seq(s1, s2) }
  | nonSeqStmt
);


-- Public top-level parser
public parse = stmt;
             
