-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;
import STM;

-- Signals an error; takes an error message and location info
public fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- AST environment
fun createEnv (id) {
  fun freshName () {
    [createEnv (id+1), sprintf ("_tmp%d", id)]
  }

  [freshName]
}

fun emptyEnv () {
  createEnv (0)
}

fun freshName (env) {
  env [0] ()
}

-- Attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")
--   Weak --- works like Val, but allows to provide a "default" value for void constructs

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, vl, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> vl => fun (vl) {Ignore (vl)}
  | _    -> vl
  esac
}

-- Checks if a void epxression can be used in the context described by
-- the attribute "atr".
fun assertVoid (atr, vl, loc) {
  case atr of
    Void -> vl
  | Val  -> error ("value expected", loc)
  | Weak -> vl => fun (vl) {Seq (vl, Const (0))}
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

-- A helper function to be used with super-combinator "expr"
fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, l (Val) =>> fun (l) {
                                            r (Val) =>  fun (r) {
                                              Binop (op, l, r)
                                            }}, loc)
                          }
                        }
  ]
}

-- Helper parser: parses a (possible empty) list of items separated by ","
fun list0 (item) {
  list0By (item, s(","))
}

-- Helper parser: parses a non-empty list of items separated by ","
fun list (item) {
  listBy (item, s(","))
}

-- Helper AST function: expands a "raw" scope expression, reifying
-- initializers into assignments
fun expandScope (defs, expr) {
  fun expandVarDefs (defs, expr) {
    foldr (fun ([defs, expr], def) {     
             case def of
                [ident, None]         -> [ident : defs, expr]
             |  [ident, Some (value)] -> [ident : defs, Seq (Ignore (Assn (Ref (ident), value)), expr)]
             esac
           },
           [{}, expr],
           defs)
  }
  
  case 
    foldr (fun ([defs, expr], def) {
             case def of
               f@Fun (_, _, _) -> [f : defs, expr]
             | Var (ds) ->             
                 case expandVarDefs (ds, expr) of
                   [ds, expr] -> [Var (ds) : defs, expr]
                 esac
             esac
           },
           [{}, expr],
           defs) of
    [defs, expr] -> Scope (defs, expr)
  esac  
}

-- Helper AST function: distributes a scope through an expression
fun distributeScope (expr, exprConstructor) {
  case expr of
    Scope (defs, sexpr) -> Scope (defs, exprConstructor (sexpr))
  | _                   -> exprConstructor (expr)
  esac
}

-- Helper AST function: reifies pattern bindings into scope
-- definitions and assignments; takes an returns lists of
-- pattern-branch pairs
fun reifyPatternBindings (matched, brs) {
  map (
    fun ([pat, br]) {      
      fun collectBindings (path, p) {
        fun collectList (path, ps) {
          case
            foldl (fun ([acc, i], p) {
                     [collectBindings (i : path, p) : acc, i+1]                      
                   },
                   [{}, 0],
                   ps) of
            [acc, _] -> flatten (acc)
          esac            
        }
        
        case p of
          Sexp  (_, ps)   -> collectList (path, ps)
        | Array (ps)      -> collectList (path, ps)
        | Named (name, p) -> [name, path] : collectBindings (path, p)
        |  _              -> {}
        esac
      }

      case collectBindings ({}, pat) of
        {}       -> [pat, br]
      | bindings ->
           [pat,
             expandScope (
               {Var (map (
                      fun ([name, path]) {
                        var value = foldr (fun (p, i) {Elem (p, Const (i))}, Var (matched), path);
                        [name, Some (value)]
                      },
                      bindings))},
               br
             )]
      esac
    },
    brs
  )
}

var
  primary = memo $ eta syntax (
    -- array constant
    loc=pos x=inbr[s("["), list0(syntax (e=exp {e(Val)})), s("]")] {
      fun (a) {assertValue (a, chainST (x) => fun (x) {Array (x)}, loc)}
    } |
                                      
    -- string constant
    loc=pos x=strlit {
      fun (a) {assertValue (a, returnST $ String (x), loc)}
    } |
                                      
    -- character literal
    loc=pos x=chrlit {
      fun (a) {assertValue (a, returnST $ Const (x), loc)}
    } |
                                      
    -- decimal constant
    loc=pos x=decimal {
      fun (a) {assertValue (a, returnST $ Const (stringInt (x)), loc)}
    } |
                                      
    -- identifier
    loc=pos x=lident args=inbr[s("("), list0(syntax(e=exp {e(Val)})), s(")")]? {
      fun (a) {
        case args of
          None -> returnST $ case a of
                               Ref  -> Ref (x)
                             | Void -> Ignore (Var (x))
                             | _    -> Var (x)
                             esac
        | Some (args) -> assertValue (a, chainST (args) => fun (args) {Call (x, args)}, loc)
        esac}
    } |

    -- S-expression
    loc=pos x=uident args=inbr[s("("), list0(syntax(e=exp {e (Val)})), s(")")]? {
      fun (a) {
        assertValue (a, case args of
                          None        -> returnST $ Sexp (x, {})
                        | Some (args) -> chainST (args) => fun (args) {Sexp (x, args)}
                        esac, loc)}
    } |
  
    loc=pos kSkip {
      fun (expectedType) {
        var res = returnST (Skip);
        assertVoid (expectedType, res, loc)
      }
    } |

    loc=pos kWhile condGetter=exp kDo bodyGetter=scopeExpr kOd {
      fun (expectedType) {
        var condST = condGetter (Val);
        var bodyST = bodyGetter (Void);
        var res = chainST ({condST, bodyST}) => fun ({cond, body}) {
          While (cond, body)
        };
        assertVoid (expectedType, res, loc)
      }
    } |

    loc=pos kDo bodyGetter=scopeExpr kWhile condGetter=exp kOd {
      fun (expectedType) {
        var bodyST = bodyGetter (Void);
        var condST = condGetter (Val);
        var res = chainST ({bodyST, condST}) => fun ({body, cond}) {
          distributeScope (body, fun (bodyExpr) {
            DoWhile (bodyExpr, cond)
          })
        };
        assertVoid (expectedType, res, loc)
      }
    } |

    loc=pos 
    kFor initGetter=scopeExpr s[","] condGetter=exp s[","] stepGetter=exp kDo
      bodyGetter=scopeExpr 
    kOd {
      fun (expectedType) {
        var initST = initGetter (Void);
        var condST = condGetter (Val);
        var stepST = stepGetter (Void);
        var bodyST = bodyGetter (Void);
        var res = chainST ({initST, condST, stepST, bodyST}) => fun ({init, cond, step, body}) {
          var loopBody = Seq (body, step);
          var loop = While (cond, loopBody);
          distributeScope (init, fun (initExpr) {
            Seq (initExpr, loop)
          })
        };
        assertVoid (expectedType, res, loc)
      }
    } |

    kIf condGetter=exp kThen thenGetter=scopeExpr elseGetter=elseParser {
      fun (expectedType) {
        var condST = condGetter (Val);
        var thenST = thenGetter (expectedType);
        var elseST = elseGetter (expectedType);
        chainST ({condST, thenST, elseST}) => fun ({cond, thenBody, elseBody}) {
          If (cond, thenBody, elseBody)
        }
      }
    } |

    kCase scrutineeGetter=exp kOf patternsExprsGetter=patternsExprsParser kEsac {
      fun (expectedType) {
        var scrutineeST = scrutineeGetter (Val);
        var patternsExprsST = patternsExprsGetter (expectedType);
        
        chainST ({scrutineeST, patternsExprsST, freshName}) => fun ({scrutinee, patternsExprs, varName}) {
          var curDefinition = Var ({ varName });
          var curAssn = Assn (Ref (varName), scrutinee);
          var curCase = Case (varName, reifyPatternBindings (varName, patternsExprs));
          var curExpr = Seq (Ignore (curAssn), curCase);
          Scope ({curDefinition}, curExpr)
        }
      }
    } |

    inbr[s("("), scopeExpr, s(")")]
  ),
  patternExprParser = memo $ eta syntax (
    pattern=patternParser s["->"] exprGetter=scopeExpr {
      fun (expectedType) {
        exprGetter (expectedType) => fun (expr) {
          [pattern, expr]
        }
      }
    }
  ),
  patternsExprsParser = memo $ eta syntax (
    patternExprGetter=patternExprParser {
      fun (expectedType) {
        patternExprGetter (expectedType) => fun (patternExpr) {
          {patternExpr}
        }
      }
    } |
    patternExprGetter=patternExprParser s["|"] tailGetter=patternsExprsParser {
      fun (expectedType) {
        var patternExprST = patternExprGetter (expectedType);
        var tailST = tailGetter (expectedType);
        chainST ({patternExprST, tailST}) => fun ({patternExpr, tail}) {
          patternExpr : tail
        }
      }
    }
  ),
  elseParser = memo $ eta syntax (
    kElif condGetter=exp kThen thenGetter=scopeExpr elseGetter=elseParser {
      fun (expectedType) {
        var condST = condGetter (Val);
        var thenST = thenGetter (expectedType);
        var elseST = elseGetter (expectedType);
        chainST ({condST, thenST, elseST}) => fun ({cond, thenBody, elseBody}) {
          If (cond, thenBody, elseBody)
        }
      }
    } |
    -kElse scopeExpr -kFi |
    loc=pos kFi {
      fun (expectedType) {
        var res = returnST (Skip);
        assertVoid (expectedType, res, loc)
      } 
    }
  ),
  patternParser = memo $ eta syntax (
    s["_"] {
      Wildcard
    } |
    x=decimal {
      var value = stringInt (x);
      Const (value)
    } |
    s["["] patterns=list0[patternParser] s["]"] {
      Array (patterns)
    } |
    sexpName=uident s["("] patterns=list0[patternParser] s[")"] {
      Sexp (sexpName, patterns)
    } |
    sexpName=uident {
      Sexp (sexpName, {})
    } |
    varName=lident s["@"] pattern=patternParser {
      Named (varName, Some (pattern))
    } |
    varName=lident {
      Named (varName, None)
    }
  ),
  basic = memo $ eta (
    expr ({[Right, {[s (":="), fun (l, loc, r) {
                                 fun (a) {assertValue (a, l (Ref) =>> fun (l) {
                                                          r (Val) =>  fun (r) {
                                                          Assn (l, r)
                                                          }},
                                                        loc)
                                 }
                               }]}],
           [Left , map (binop, {"!!"})],
           [Left , map (binop, {"&&"})],
           [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
           [Left , map (binop, {"+", "-"})],
           [Left , map (binop, {"*", "/", "%"})]
          },
          postfix)
  ),
  
  postfix = memo $ eta syntax (
    loc=pos e=primary ps=(i=inbr[s("["), exp, s("]")] {Index (i (Val))})* {
      fun (a) {
        foldl (fun (e, p) {                                                                            
                 case p of                                                                                   
                   Index (i) ->
                    fun (a) {
                      case a of
                        Ref -> e (Val) =>> fun (e) {
                               i       =>  fun (i) {
                                 ElemRef (e, i)
                               }} 
                      | _   -> assertValue (a, e (Val) =>> fun (e) {
                                               i       =>  fun (i) {
                                               Elem (e, i)
                                               }},
                                            loc)
                      esac
                    }
                 esac                                                                            
               }, e, ps) (a)
      }}
  ),
  
  scopeExpr  = memo $ eta syntax (ds=definition* e=exp? {
    fun (a) {
      fun (e) {
        case ds of
          {} -> e
        | _  -> chainST (ds) =>> fun (ds) {
                e            =>  fun (e)  {
                  expandScope (ds, e)
                }}
        esac
      }
      (case e of 
        Some (e) -> e (a)
      | _        -> returnST $ Skip
      esac)
    }}                                                                  
  ),
  
  definition = memo $ eta syntax (
    kVar ds=list[syntax (x=lident e=(-s["="] basic)? {case e of
                                                        None -> returnST $ [x, None]
                                                      | Some (e) ->
                                                          e (Val) => fun (e) {[x, Some (e)]}
                                                      esac})] s[";"] {
      chainST (ds) => fun (ds) {Var (ds)}
    } |
    kFun name=lident args=inbr[s("("), list0 (lident), s(")")] body=inbr[s("{"), scopeExpr, s("}")] {
      body (Weak) => fun (body) {Fun (name, args, body)}
    }
  ),
  
  exp = memo $ eta syntax (
    basic
  | s1=basic s[";"] s2=exp {
     fun (a) {
       s1 (Void) =>> fun (s1) {
       s2 (a)    =>  fun (s2) {
         Seq (s1, s2)
       }}
     }
   }
  );

-- Public top-level parser
public parse = syntax (s=scopeExpr {s (Void) (emptyEnv ()) [1]});
