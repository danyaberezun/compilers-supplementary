-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  var result = case expr of
    Assn (e1, e2) -> 
      case evalList(c, {e1, e2}) of    
        [[s, w], {x, v}] -> [[s <- [x, v], w], v]
      esac
  | Seq (e1, e2) ->
      case eval(c, e1) of  
        [c, _] -> eval(c, e2)
      esac
  | Skip -> 
      [c, Void]
  | Read (x) ->
      case readWorld(w) of
		    [v, nw] -> [[s <- [x, v], nw], Void]
		  esac
  | Write (v) ->
      case eval(c, v) of  
        [[s, w], v_] -> [[s, writeWorld(v_, w)], Void]
      esac 
  | If (e, s1, s2) -> 
      case eval(c, e) of
        [c, 0] -> eval(c, s2)
      | [c, _] -> eval(c, s1)
      esac
  | While (e, s) ->
      case eval(c, e) of
        [c, 0] -> [c, Void]
      | [c, _] -> eval(c, Seq (s, While (e, s)))
      esac 
  | DoWhile (s, e) -> 
      eval(c, Seq (s, While (e, s)))
  | Var (x) -> 
      [c, s(x)]
  | Ref (x) ->
      [c, x]
  | Const (n) ->
      [c, n] 
  | Binop (o, e1, e2) ->  
      case eval(c, e1) of 
        [c, e1_] ->
          case eval(c, e2) of
            [c, e2_] -> [c, evalOp(o, e1_, e2_)]
          esac
      esac
  | Ignore (e) -> 
      case eval(c, e) of 
        [c, _] -> [c, Void]
      esac
  esac;
  result
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}