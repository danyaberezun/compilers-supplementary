-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
      Var (lable) -> 
        [c, s (lable)]
    | Const (n) ->
        [c, n]
    | Binop (op, l, r) ->
        case evalList (c, {l, r}) of
          [c, {l, r}] -> [c, evalOp (op, l, r)]
        esac
    | Assn (x, e) ->
        case evalList (c, {x, e}) of
          [c, {x, e}] -> [[s <- [x, e], w], e]
        esac
    | Seq (fst_stmt, snd_stmt) ->
        case evalList (c, {fst_stmt, snd_stmt}) of
          [c, {fst_stmt, snd_stmt}] -> [c, snd_stmt]
        esac
    | Skip -> 
        [c, None]
    | Read (lable) ->
        case readWorld (w) of
          [data, w] -> [[s <- [lable, data], w], None]
        esac
    | Write (expr) ->
        case eval (c, expr) of
          [c, data] -> [[s, writeWorld (data, w)], None]
        esac
    | If (expr, is_stmt, else_stmt) ->
        case eval (c, expr) of [c, data] ->
          if data then eval (c, is_stmt) else eval (c, else_stmt) fi
        esac
    | While (expr, stmt) ->
        case eval (c, expr) of [c, data] ->
          if data then eval (c, Seq (stmt, While (expr, stmt))) else [c, None] fi
        esac
    | DoWhile (stmt, expr) ->
        eval (c, Seq (stmt, While (expr, stmt)))
    | Ref (expr) -> 
        [c, expr]
    | Ignore (expr) ->
        case eval (c, expr) of
          [c, _] -> [c, None]
        esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
