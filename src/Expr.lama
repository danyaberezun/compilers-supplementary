-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of 
      Assn (varName, expr) ->
        case evalList (c, {varName, expr}) of 
          [[s, w], {varName, expr}] -> 
            [[s <- [varName, expr], w], expr]
        esac
    | Seq (s1, s2) ->
        case evalList (c, {s1, s2}) of
          [c, {s1, s2}] -> 
            [c, s2]
        esac
    | Skip ->
        [c, {}]
    | Read (varName) ->
        case readWorld (w) of
          [value, newWorld] -> 
            [[s <- [varName, value], newWorld], {}]
        esac
    | Write (expr) ->
        case eval (c, expr) of
          [[s, w], value] -> 
            [[s, writeWorld (value, w)], {}]
        esac
    | If (e, s1, s2) ->
        case eval (c, e) of
          [c, cond] ->
            if cond then eval (c, s1)
            else eval (c, s2)
            fi
        esac                
    | While (e, s) ->
        case eval (c, e) of
          [c, cond] ->
            if cond then eval (c, Seq (s, While (e, s)))
            else [c, {}]
            fi
        esac
    | DoWhile (s, e) ->
        eval (c, Seq (s, While (e, s)))
    | Var (varName) ->
        [c, s (varName)]
    | Ref (e) ->
        [c, e]
    | Const (n) ->
        [c, n]
    | Binop (op, lhs, rhs) ->
        case evalList (c, {lhs, rhs}) of
          [c, {lhs, rhs}] -> 
            [c, evalOp (op, lhs, rhs)] 
        esac
    | Ignore (e) ->
        case eval (c, e) of
          [c, _] -> 
            [c, {}]       
        esac           
  esac  
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
