-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun getNames (st, names, vals) {
  foldl(fun(s, [name, v]) {addName(s, name, v)}, st, zip(names, vals))
}

fun getDefs (st, defs) {
  foldl(fun(s, def) {
    case def of 
      Fun(name, arg, body) -> addFunction(s, name, arg, body)
    | Var(names) -> addNames(s, names)
    esac    
    },
    st, defs)
}


(* Assignment *)
fun eval (c@[s, w], expr) {
  case expr of
    Skip -> [c, None]
  | Const (n) -> [c, n]
  | Ref (x) -> [c, x]
  | Ignore(e) -> case eval(c, e) of 
      [c, _] -> [c, None]  
    esac
  | Var (x) -> [c, lookup(s, x)]
  | Read(x) -> 
    case readWorld(w) of 
      [res, w] -> [[s <- [x, res], w], None]
    esac
  | Write(e) -> 
    case eval(c, e) of 
      [c@[s, w], res] -> [[s, writeWorld(res, w)], None]    
    esac
  | If(cond, l, r) -> 
    case eval(c, cond) of 
      [c, cond] -> eval(c, if cond then l else r fi)
    esac 
  | While(cond, body) -> 
    case eval(c, cond) of 
      [c, cond] -> if cond then eval(c, Seq(body, expr)) else [c, None] fi 
    esac
  | DoWhile(body, cond) -> 
    case eval(c, body) of 
      [c, _] -> eval(c, While(cond, body))
    esac
  | Assn(l, r) -> 
    case evalList(c, {l, r}) of 
      [c@[s, w], {x, r}] -> [[s <- [x, r], w], r]
    esac
  | Seq(l, r) -> 
    case evalList(c, {l, r}) of 
      [c, {_, rRes}] -> [c, rRes]
    esac

  | Binop(op, l, r) -> 
    case evalList(c, {l, r}) of 
      [c, {l, r}] -> 
        [c, evalOp(op, l, r)]
    esac

  | Scope(names, body) -> case eval([getDefs(enterScope(s), names), w], body) of
      [c@[s, w], res] -> [[leaveScope(s), w], res]
    esac

  | Call(function, args) -> case evalList(c, args) of 
      [c@[arg_s, w], res] -> case lookup(arg_s, function) of
        Fun(arg, body) -> case getNames(enterFunction(arg_s), arg, res) of
            cur_s -> case eval([cur_s, w], body) of
                [c2@[s, w], res] -> [[leaveFunction(arg_s, getGlobal(s)), w], res] 
              esac
          esac
      esac
    esac
  esac
}

-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |  
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}