-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of 
    Var (s) -> st (s)
  | Const (n) -> n
  | Binop (op, e1, e2) -> ( 
    var val1, val2;
    val1 := evalExpr (st, e1);
    val2 := evalExpr (st, e2);
    case op of
      "+" -> val1 + val2
    | "-" -> val1 - val2
    | "*" -> val1 * val2
    | "/" -> val1 / val2
    | "%" -> val1 % val2
    | "<" -> val1 < val2
    | ">" -> val1 > val2
    | "<=" -> val1 <= val2
    | ">=" -> val1 >= val2
    | "==" -> val1 == val2
    | "!=" -> val1 != val2
    | "&&" -> val1 && val2
    | "!!" -> val1 !! val2
    esac
  )
  esac
}
