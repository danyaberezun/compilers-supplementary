-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)
fun addDefs(s, defs) {
  foldl(fun (s, def) {
    case def of
      Var(name) -> s.addNames(name) | Fun(name, args, body) -> s.addFunction(name, args, body)
    esac
  }, s, defs)
}

fun addArgs(s, args, values) {
  foldl(fun (s, [arg, value]) { s.addName(arg, value) }, s, zip(args, values))
}

fun eval(c@[s, w], expr) {
  case expr of
    Assn(l, r) -> 
      case evalList(c, {l, r}) of
          [[s, w], {Ref(x), Val(e)}] -> [[s <- [x, Val(e)], w], Val(e)]
      esac
  | Seq(e1, e2) ->
      case eval(c, e1) of
          [c, _] -> eval(c, e2)
      esac         
  | Skip -> [c, Void] 
  | Read(x) ->
      case readWorld(w) of
          [z, w] -> [[s <- [x, Val(z)], w], Void]
      esac 
  | Write(e) ->
      case eval(c, e) of
          [[s, w], Val(v)] -> [[s, writeWorld(v, w)], Void]
      esac
  | If(cond, st1, st2) ->
      case eval(c, cond) of
          [c, Val(0)] -> eval(c, st2) | [c, Val(_)] -> eval(c, st1)
      esac
  | While(cond, st) ->
      case eval(c, cond) of [c, Val(0)] -> [c, Void] | [c, Val(_)] ->
          case eval(c, st) of
              [c, _] -> eval(c, While(cond, st))
          esac
      esac
  | DoWhile(st, cond) -> eval(c, Seq(st, While(cond, st)))
  | Var(v) -> [c, s.lookup(v)]
  | Ref(x) -> [c, Ref(x)]
  | Const(x) -> [c, Val(x)]
  | Binop(op, l, r) -> 
      case evalList(c, {l, r}) of 
          [c, {Val(l), Val(r)}] -> [c, Val(evalOp(op, l, r))]
      esac
  | Ignore(e) -> 
      case eval(c, e) of 
          [c, _] -> [c, Void]
      esac
  | Scope(d, e) ->
      case eval([s.enterScope.addDefs(d), w], e) of
          [[s, w], v] -> [[s.leaveScope, w], v]
      esac
  | Call(n, e) ->
      case evalList(c, e) of [[s1, w], values] ->
          case s1.lookup(n) of Fun(fargs, body) ->
              case eval([s1.enterFunction.addArgs(fargs, values), w], body) of
                [[s2, w], v] -> [[s1.leaveFunction(s2.getGlobal), w], v]
              esac
          esac
      esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}