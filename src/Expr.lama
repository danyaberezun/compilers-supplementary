-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)
fun eval (c@[s, w], expr) {
  case expr of
    Var (v)   -> [c, s(v)]
  | Const (x) -> [c, x]
  | Ref (x) -> [c, REF (x)]
  | Binop (op, l, r) -> case evalList(c, {l, r}) of
      [c, {l, r}] -> [c, evalOp(op, l, r)]
    esac
  | Skip -> [c, VOID]
  | Assn (l, r) -> case evalList(c, {l, r}) of
      [[s, w], {REF (x), v}] -> [[s <- [x, v], w], v]
    esac
  | Read (x) -> case readWorld (w) of
      [v, w] -> [[s <- [x, v], w], VOID]
    esac
  | Write (e) -> case eval(c, e) of
      [[s, w], r] -> [[s, writeWorld (r, w)], VOID]
    esac
  | Seq (e1, e2) -> case eval (c, e1) of
      [c, v] -> eval (c, e2)
    esac
  | Ignore (e) -> case eval (c, e) of
      [c, v] -> [c, VOID]
    esac
  | If (pred, tb, fb) -> case eval (c, pred) of
      [c, cond] -> if cond then eval (c, tb) else eval (c, fb) fi
    esac
  | While (pred, body) -> case eval (c, pred) of
      [c, cond] -> if cond then 
        case eval (c, body) of
          [c, res] -> eval (c, While (pred, body))
        esac
      else [c, VOID] fi
    esac
  | DoWhile (pred, body) -> case eval (c, body) of
      [c, res] -> eval (c, While (pred, body))
    esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}