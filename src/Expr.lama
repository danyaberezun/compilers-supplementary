-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalBinop(op, exp1, exp2) {
  case op of 
    "+" -> exp1 + exp2
  | "-" -> exp1 - exp2
  | "*" -> exp1 * exp2
  | "/" -> exp1 / exp2
  | "%" -> exp1 % exp2
  | "<" -> exp1 < exp2
  | ">" -> exp1 > exp2
  | "<=" -> exp1 <= exp2
  | ">=" -> exp1 >= exp2
  | "==" -> exp1 ==  exp2
  | "!=" -> exp1 != exp2
  | "&&" -> exp1 && exp2
  | "!!" -> exp1 !! exp2
  | _ -> failure ("Operator %s is not implemented\n", op)
  esac
}

public fun evalExpr (st, exp) {
  case exp of 
    Var(s) -> st(s)
  | Const(i) -> i
  | Binop(op, exp1, exp2) -> evalBinop(op, evalExpr(st, exp1), evalExpr(st, exp2))
  esac
}
