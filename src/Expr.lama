-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Sexp    (string, expr list)  |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  fun defineSymbols (st, definitions) {
    case definitions of 
      head : tail -> case head of
                      Fun (fn, argList, expr) -> defineSymbols (st.addFunction (fn, argList, expr), tail)
                    | Var (nameList)          -> defineSymbols (st.addNames (nameList), tail)
                    esac
    | _           -> st
    esac
  }

  fun setArgs (st, args) {
    case args of 
      [argId, argVal] : tail -> setArgs (st.addName (argId, argVal), tail)
    | _                      -> st
    esac
  }

  case expr of 
    Assn    (lhs, rhs)           -> case evalList(c, {lhs, rhs}) of
                                      [[s, w], {lvalue, rvalue}] -> case lvalue of 
                                                                      ElemRef (arr, idx) -> [[s, w], arr[idx] := rvalue] 
                                                                    | _                  -> [[s <- [lvalue, rvalue], w], rvalue]
                                                                    esac
                                    esac
  | Set     (name, expr)         -> eval (c, Assn (Ref (name), expr))
  | Seq     (expr1, expr2)       -> case evalList (c, {expr1, expr2}) of
                                      [c, {_, tail}] -> [c, tail]
                                    esac
  | Skip                         -> [c, Void]
  | If      (cond, expr1, expr2) -> case eval (c, cond) of
                                      [c, 0] -> eval (c, expr2)
                                    | [c, _] -> eval (c, expr1)
                                    esac
  | While   (cond, expr)         -> case eval (c, cond) of
                                      [c, 0] -> [c, Void]
                                    | [c, _] -> eval (c, Seq (expr, While (cond, expr)))
                                    esac
  | DoWhile (expr, cond)         -> eval (c, Seq (expr, While (cond, expr)))
  | Var     (name)               -> [c, s.lookup (name)]
  | Ref     (name)               -> [c, name]
  | Const   (value)              -> [c, value]
  | Binop   (kind, lhs, rhs)     -> case evalList (c, {lhs, rhs}) of
                                      [c, {lhs, rhs}] -> [c, evalOp (kind, lhs, rhs)]
                                    esac
  | Scope   (defList, expr)      -> case eval ([defineSymbols (s.enterScope, defList), w], expr) of
                                      [[s, w], tail] -> [[s.leaveScope, w], tail]
                                    esac
  | Call    (fn, passList)       -> case s.lookup (fn) of
                                      Fun (_, External)   -> eval (c, Builtin (fn, passList))
                                    | Fun (argList, expr) -> case evalList (c, passList) of
                                                               [[sOld, w], argValues] -> 
                                                                 case setArgs (sOld.enterFunction, zip (argList, argValues)) of
                                                                   s -> case eval ([s, w], expr) of
                                                                          [[s, w], tail] -> 
                                                                            [[sOld.leaveFunction (s.getGlobal), w], tail]
                                                                        esac
                                                                 esac
                                                             esac 
                                    esac
  | Ignore  (expr)               -> case eval (c, expr) of
                                      [c, _] -> [c, Void]
                                    esac
  | Array   (exprs)              -> case evalList (c, exprs) of
                                      [c, values] -> [c, listArray (values)]
                                    esac  
  | Builtin (name, args)         -> case evalList (c, args) of
                                      [[s, w], argValues] -> case evalBuiltin (name, argValues, w) of
                                                               [res, w] -> [[s, w], res]
                                                             esac
                                    esac
  | Elem    (arrExpr, idxExpr)   -> case evalList (c, {arrExpr, idxExpr}) of
                                      [c, {arr, idx}] -> case arr of
                                                           Sexp (name, arr) -> [c, arr[idx]]
                                                         | _                -> [c, arr[idx]]
                                                         esac
                                    esac
  | ElemRef (arrExpr, idxExpr)   -> case evalList (c, {arrExpr, idxExpr}) of
                                      [c, {arr, idx}] -> [c, ElemRef (arr, idx)]
                                    esac  
  | Sexp    (name, args)         -> case evalList (c, args) of
                                      [c, values] -> [c, Sexp (name, listArray (values))]
                                    esac
  | String  (cstr)               -> [c, cstr]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
