-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn(a, b) -> case eval(c, a) of
      [c1@[s1, w1], Ref(x)] -> case eval(c1, b) of
        [[s2, w2], v] -> [[s2 <- [x, v], w2], v]
      esac
    esac
  | Var(x) -> [c, s(x)]
  | Ref(x) -> [c, Ref(x)]
  | Const(v) -> [c, v]
  | Seq(a, b) -> case eval(c, a) of
      [c1, _] -> eval(c1, b)
    esac
  | Skip -> [c, 0]
  | Read(x) -> case readWorld(w) of
      [v, w1] -> [[s <- [x, v], w1], 0]
    esac
  | Write(a) -> case eval(c, a) of
      [c1@[s1, w1], v] -> [[s1, writeWorld(v, w1)], 0]
    esac
  | If(cond, t, f) -> case eval(c, cond) of
      [c1, 0] -> eval(c1, f)
    | [c1, _] -> eval(c1, t)
    esac
  | While(cond, body) -> case eval(c, cond) of
      [c1, 0] -> [c1, 0]
    | [c1, _] -> case eval(c1, body) of
        [c2, _] -> eval(c2, While(cond, body))
      esac
    esac
  | DoWhile(body, cond) -> case eval(c, body) of
      [c1, _] -> case eval(c1, cond) of
        [c2, 0] -> [c2, 0]
      | [c2, _] -> eval(c2, DoWhile(body, cond))
      esac
    esac
  | Binop(op, a, b) -> case evalList(c, {a, b}) of
      [c1, {x, y}] -> [c1, evalOp(op, x, y)]
    esac
  | Ignore(a) -> [eval(c, a)[0], 0]
  | _ -> failure("could not evaluate expression")
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
