-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
    case expr of
      Var(x)           -> [c, s(x)]
    | Ignore(e)        -> [eval(c, e)[0], 0]
    | Const(n)         -> [c, n]
    | Ref(x)           -> [c, Ref(x)]
    | Skip             -> [c, 0]
    | Assn(l, r)       -> case evalList(c, {l, r}) of [[s, w], {Ref(lv), rv}] -> [[s <- [lv, rv], w], rv]
                          esac
    | Seq(e1, e2)      -> case eval(c, e1) of [c1, v1] -> eval(c1, e2)
                          esac
    | Read(varname)    -> case readWorld(c[1]) of [res, w] -> [[(c[0] <- [varname, res]), w], 0]
                          esac
    | Write(expr1)     -> case eval(c, expr1) of [c1, e] -> [[c1[0], writeWorld(e, c[1])], 0]
                          esac
    | Binop(op, l, r)  -> case evalList(c, {l, r}) of [c1, {lv, rv}] -> [c1, evalOp(op, lv, rv)]
                          esac
    | While (con, b) -> case eval(c, con) of
                                  [c, 0] -> [c, 0]
                                | [c, _] -> let [c, _] = eval(c, b) in eval(c, While (con, b))
                                esac
    | DoWhile(s, cond) -> eval(c, Seq(s, While(cond, s)))
    | If(con, t, f) -> let [c, v] = eval(c, con) in if v then eval(c, t) else eval(c, f) fi
    | _ -> [c, 0]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
