-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn(ref, e) ->
      case evalList(c, {ref, e}) of
        [c@[s, w], {ref, e}] -> [[s <- [ref, e], w], e]
      esac
    | Seq(e1, e2) ->
      case eval(c, e1) of
        [c, e1] -> eval(c, e2)
      esac
    | Skip -> [c, Bottom]
    | Read(name) ->
      case readWorld(w) of
        [v, w] -> [[s <- [name, v], w], Bottom]
      esac
    | Write(e) ->
      case eval(c, e) of
         [c@[s, w], e] -> [[s, writeWorld(e, w)], Bottom]
      esac
    | If(cond, e1, e2) ->
      case eval(c, cond) of
        [c@[s, w], cond] -> if cond then eval(c, e1) else eval(c, e2) fi
      esac
    | While(cond, e) ->
      case eval(c, cond) of
        [c, 0] -> [c, Bottom]
        | [c, _] -> eval(eval(c, e)[0], expr)
      esac
    | DoWhile(e, cond) ->
      case eval(eval(c, e)[0], cond) of
        [c, 0] -> [c, Bottom]
        | [c, _] -> eval(c, expr)
      esac
    | Var(name) -> [c, s(name)]
    | Ref(name) -> [c, name]
    | Const(v) -> [c, v]
    | Binop(op, e1, e2) ->
      case evalList(c, {e1, e2}) of
        [c, {e1, e2}] -> [c, evalOp(op, e1, e2)]
      esac
    | Ignore(e) -> [eval(c, e)[0], Bottom]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
