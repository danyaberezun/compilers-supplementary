-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun getOp(op) {
  case op of
      "+"  -> infix +
    | "-"  -> infix -
    | "*"  -> infix *
    | "/"  -> infix /
    | "%"  -> infix %
    | "==" -> infix ==
    | ">=" -> infix >=
    | "<=" -> infix <=
    | "!!" -> infix !!
    | "!=" -> infix !=
    | "&&" -> infix &&
    | "<"  -> infix <
    | ">"  -> infix >
  esac

}

public fun evalExpr(st, expr) {
  case expr of
      Var (x)                -> st(x)
    | Const (x)              -> x
    | Binop (op, lexp, rexp) -> getOp(op)(evalExpr(st, lexp), evalExpr(st, rexp))
  esac
}
