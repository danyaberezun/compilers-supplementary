-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> p("error"); p(name); error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)

fun evalDef(c@[s, w], def) {
  case def of
    Var (vars) -> p("Var def"); p(def); [addNames (s, vars), w]
  | Fun (f, args, e) -> [addFunction (s, f, args, e), w]
  esac
}

fun evalDefs(c, defs) {
  p("evaling defs"); p(defs); p("\n");
  case defs of
    {} -> c
  | d:defs -> evalDefs(evalDef(c, d), defs)
  esac
}

fun p(some) {
  system (sprintf("echo '%s' >&2", string(some)))
}

fun getValue(Var (v)) { v }

fun eval (c@[s, w], expr) {
  case expr of 
    Var (x) -> [c, getValue (lookup (s, x))]
  | Assn (e1, e2) ->
      case evalList (c, {e1, e2}) of 
        [[s, w], {Ref (x), v}] -> [[s <- [x, Var(v)], w], v]
      esac
  | Seq (e1, e2) -> 
    case eval (c, e1) of
      [c, _] -> eval(c, e2)
    esac
  | Skip -> [c, {}]
  | Read (x) ->
    case readWorld (w) of
      [v, w] -> [[s <- [x, Var (v)], w], {}]
    esac
  | Write (e) ->
    case eval (c, e) of
      [[s, w], v] -> [[s, writeWorld (v, w)], {}]
    esac
  | If (e, e1, e2) ->
    case eval (c, e) of
      [c, v] -> if v then eval(c, e1) else eval(c, e2) fi
    esac
  | While (cond, e) ->
    case eval (c, cond) of
      [c, v] -> 
        if v then 
          case eval (c, e) of
            [c, _] -> eval (c, While (cond, e))
          esac
        else [c, {}] 
        fi
    esac
  | DoWhile (e, cond) -> eval (c, Seq(e, While (cond, e)))
  | Ref (x) -> [c, Ref (x)]
  | Const (n) -> [c, n]
  | Binop (op, e1, e2) -> 
    case evalList (c, {e1, e2}) of
      [c, {v1, v2}] -> [c, evalOp (op, v1, v2)]
    esac
  | Scope (defs, e) -> 
    case eval (evalDefs ([s.enterScope, w], defs), e) of
      [[s, w], v] -> [[s.leaveScope, w], v]
    esac
  | Call (f, exprs) -> checkFun(s, f); 
    case evalList(c, exprs) of 
      [[s, w], vals] -> 
        case lookup (s, f) of 
          Fun (names, e) -> 
            var s1 = foldl ( 
              fun (s, [name, v]) { 
                s.addName (name, Var(v)) 
              }, s.enterFunction, zip (names, vals)
            );
            case eval ([s1, w], e) of
              [[s2, w], v] -> [[s.leaveFunction (s2.getGlobal), w], v]
            esac
        esac
    esac
  | Ignore (e) -> case eval(c, e) of [c, _] -> [c, {}] esac
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  p("eval expr is called");
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}