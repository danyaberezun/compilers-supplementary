-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+", infix +],
  ["-", infix -],
  ["*", infix *],
  ["/", infix /],
  ["%", infix %],
  ["==", infix ==],
  ["!=", infix !=],
  ["<", infix <],
  ["<=", infix <=],
  [">", infix >],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp(op, l, r) {
  case assoc(ops, op) of
    Some (f) -> f(l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar(state, name) {
  case state.lookup(name) of
    Var (_) -> skip
    | _ -> error(sprintf("the name ""%s"" does not designate a variable", name), getLoc(name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun(state, name) {
  case state.lookup(name) of
    Fun (_, _) -> skip
    | _ -> error(sprintf("the name ""%s"" does not designate a function", name), getLoc(name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames(state, names) {
  foldl(fun (s, name) { s.addName(name, Var(0)) }, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction(state, name, args, body) {
  state.addName(name, Fun(args, body))
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList(c, exprs) {
  case foldl(fun ([c, vals], e) {
    case eval(c, e) of
      [c, v] -> [c, v:vals]
    esac
  },
    [c, { }],
    exprs) of
    [c, vals] -> [c, reverse(vals)]
  esac
}

(* Assignment *)
fun eval(c@[s, w], expr) {
  case expr of
    Var (v) -> [c, Val(s.lookup(v))]
    | Const (v) -> [c, Val(v)]
    | Ref (x) -> [c, Ref(x)]
    | Seq (e1, e2) ->
      case eval(c, e1) of
        [cn, _] -> eval(cn, e2)
      esac
    | Ignore (e) ->
      case eval(c, e) of
        [cn, _] -> [cn, Void]
      esac
    | Skip -> [c, Void]
    | Read (e) ->
      case eval(c, e) of
        [[sn, wn], Ref (x)] ->
          case readWorld(wn) of
            [v, wnn] -> [[sn <- [x, v], wnn], Void]
          esac
      esac
    | Write (e) ->
      case eval(c, e) of
        [[sn, wn], Val (v)] -> [[sn, writeWorld(v, wn)], Void]
      esac
    | Assn (ex, ev) ->
      case eval(c, ex) of
        [cn, Ref (x)] ->
          case eval(cn, ev) of
            [[sn, wn], Val (v)] -> [[sn <- [x, v], wn], Val(v)]
          esac
      esac
    | Binop (op, el, er) ->
      case eval(c, el) of
        [cn, Val (vl)] ->
          case eval(cn, er) of
            [cnn, Val (vr)] -> [cnn, Val(evalOp(op, vl, vr))]
          esac
      esac
    | While (e, s) ->
      while case eval(c, e) of [cn, Val (v)] -> c := cn; v esac do
        c := eval(c, s).fst
      od;
      [c, Void]
    | DoWhile (s, e) ->
      do
        c := eval(c, s).fst
      while case eval(c, e) of [cn, Val (v)] -> c := cn; v esac od;
      [c, Void]
    | If (e, s1, s2) ->
      if case eval(c, e) of [cn, Val (v)] -> c := cn; v esac then
        eval(c, s1)
        else
          eval(c, s2)
      fi
    | Scope (v, sts) ->
      fun addSymbols(s, v) {
        case v of
          { } -> s
          | vf:vrest ->
            case vf of
              Var (vr) -> addNames(addSymbols(s, vrest), vr)
              | Fun (name, args, body) -> addFunction(addSymbols(s, vrest), name, args, body)
            esac
        esac
      }
      var sn = addSymbols(enterScope(s), v);
      case eval([sn, w], sts) of [[s, w], v] ->
        [[leaveScope(s), w], v]
      esac
    | Call (f, argsExpr) ->
      case lookup(s, f) of Fun (args, body) ->
        case evalList(c, argsExpr) of [[st, w], argsExpr] ->
          fun addArgs(s, args) {
            case args of
              { } -> s
              | [a, Val (v)] : argsRest ->
                addArgs(s, argsRest).addName(a, v)
            esac
          }
          case eval([enterFunction(s).addArgs(zip(args, argsExpr)), w], body) of [[s, w], v] ->
            [[leaveFunction(st, getGlobal(s)), w], v]
          esac
        esac
      esac
    | _ -> failure("evalExpr not implemented: %s\n", expr.string)
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr(input, expr) {
  case eval([emptyState(), createWorld(input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
