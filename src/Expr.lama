-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c@[s, w], exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Var(x)                      -> [c, s(x)]
  | Const(n)                    -> [c, n]
  | Ref(x)                      -> [c, x]
  | Ignore(exp)                 -> case eval(c, exp) of
                                    [c, _] -> [c, Void]
                                    esac
  | Skip                        -> [c, Void]
  | Binop(op, exp1, exp2)       -> case evalList(c, {exp1, exp2}) of
                                    [c, val1:val2:_] -> [c, evalOp(op, val1, val2)]
                                    esac
  | Assn(lexp, rexp)            -> case evalList(c, {lexp, rexp}) of
                                    [[s, w], lval:rval:_ ] -> [[s <- [lval, rval], w], rval]
                                    esac
  | Seq(exp1, exp2)             -> case eval(c, exp1) of
                                    [c, _] -> eval(c, exp2)
                                    esac
  | Read(x)                     -> case readWorld(w) of 
                                    [n, wr] -> [[s <- [x, n], wr], Void]
                                    esac
  | Write(exp)                  -> case eval(c, exp) of
                                    [c, v] -> [[s, writeWorld(v, w)], Void]
                                    esac
  | If(exp, expTrue, expFalse)  -> case eval(c, exp) of
                                    [c, v] -> 
                                      if v != 0
                                      then eval(c, expTrue)
                                      else eval(c, expFalse) fi
                                    esac
  | While(exp, expTrue)         -> case eval(c, exp) of
                                    [c, v] ->
                                      if v != 0
                                      then 
                                        case eval(c, expTrue) of
                                          [c, _] -> eval(c, expr)
                                          esac
                                      else [c, Void] fi
                                    esac
  | DoWhile(expTrue, exp)       -> case eval(c, expTrue) of
                                    [c, _] ->
                                      case eval(c, exp) of
                                        [c, v] ->
                                          if v != 0
                                          then eval(c, expr)
                                          else [c, Void] fi
                                        esac
                                    esac
  | _                           -> failure ("evalExpr not implemented\n")
   esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
