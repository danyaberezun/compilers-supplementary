-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Scope   (def list, expr)     |
--        Call    (string, expr list)  |
--        Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string list)                |
--       Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {addName (s, name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  addName (state, name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun addNamesOrFunction (s, expr) {
  case expr of
    Var (name) -> addNames (s, name)
    | Fun (name, args, body) -> addFunction (s, name, args, body)
  esac
}

fun initNames (st, names, values) {
  foldl (
    fun (st, [name, value]) { addName (st, name, value) }, 
    st, zip (names, values)
  )
}

fun eval (c@[st, w], expr) {
  case expr of
    Assn (r, e) -> let [c@[st, w], {Ref(x), v}] = evalList (c, {r, e}) in [[st <- [x, v], w], v]
  | Seq (e1, e2) -> let [c, {v1, v2}] = evalList (c, {e1, e2}) in [c, v2]
  | Skip -> [c, Void]
  | Read (x) -> let [v, w] = readWorld (w) in [[st <- [x, v], w], Void]
  | Write (e) -> let [[st, w], v] = eval (c, e) in [[st, writeWorld (v, w)], Void]
  | If (e, e1, e2) -> let [c, v] = eval (c, e) in 
      if v != 0 then eval (c, e1) else eval (c, e2) fi
  | DoWhile (cond, body) -> eval (c, Seq (cond, While (body, cond)))
  | While (cond, body) -> let [c, res] = eval (c, cond) in 
      if res != 0 then eval (c, Seq (body, While (cond, body))) else [c, Void] fi
  | Var (x) -> [c, lookup (st, x)]
  | Ref (x) -> [c, Ref (x)] 
  | Const (n) -> [c, n]
  | Binop (op, l, r) -> let [c, {l, r}] = evalList (c, {l, r}) in [c, evalOp (op, l, r)]
  | Ignore (e) -> let [c, v] = eval (c, e) in [c, Void]
  | Scope (defs, expr) ->
      let st = foldl (addNamesOrFunction, enterScope (st), defs) in
        let [[st, w], result] = eval ([st, w], expr) in
          [[leaveScope (st), w], result] 
  | Call (name, exprs) ->
      let f@Fun (params, body) = lookup(st, name) in 
        let [c, v_args] = evalList (c, exprs) in
          let st2 = initNames(enterFunction (st), params, v_args) in
            let [[st2, w], result] = eval ([st2, w], body) in
              [[leaveFunction (st, getGlobal (st2)), w], result]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> getOutput (snd (c))
  esac
}