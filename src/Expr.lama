-- Expression evaluator

import List;
import State;


-- Evaluate the binary operation
public fun evalBinop(op) {
  case op of
    "+"  -> infix +
  | "-"  -> infix -
  | "*"  -> infix *
  | "/"  -> infix /
  | "%"  -> infix %
  | "&&" -> infix &&
  | "==" -> infix ==
  | "!=" -> infix !=
  | "<=" -> infix <=
  | "<"  -> infix <
  | ">=" -> infix >=
  | ">"  -> infix >
  | "!!" -> infix !!  
  esac
}

-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
    Const (n) -> n
  | Var   (x) -> st (x)
  | Binop (op, x, y) -> evalBinop (op) (evalExpr (st, x), evalExpr (st, y))
  esac
}
