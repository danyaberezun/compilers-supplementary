-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalExpr (st, expr) {
  case expr of
    Var (v) -> st (v)
  | Const (c) -> c
  | Binop (s, l, r) -> (
    var le = st.evalExpr (l);
    var re = st.evalExpr (r);
    case s of
      "+" -> le + re
    | "-" -> le - re
    | "*" -> le * re
    | "/" -> le / re
    | "%" -> le % re
    | "<" -> le < re
    | ">" -> le > re
    | "<=" -> le <= re
    | ">=" -> le >= re
    | "==" -> le == re
    | "!=" -> le != re
    | "&&" -> le && re
    | "!!" -> le !! re
    esac
  )

  esac
}
