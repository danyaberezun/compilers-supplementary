-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalOp (op, left, right) {
  case op of 
      "+" -> left + right
    | "-" -> left - right
    | "*" -> left * right 
    | "/" -> left / right
    | "%" -> left % right
    | "<" -> left < right
    | ">" -> left > right
    | "<=" -> left <= right
    | ">=" -> left >= right
    | "==" -> left == right
    | "!=" -> left != right
    | "&&" -> left && right
    | "!!" -> left !! right
    | o -> failure ("incorrect op\n")
  esac
}

public fun evalExpr (st, expr) {
  case expr of 
      Var (s) -> st (s) 
    | Const (x) -> x
    | Binop (op, left, right) -> evalOp(op, (evalExpr(st, left)), (evalExpr(st, right)))
    | e -> failure ("incorrect expr\n")
  esac 
}