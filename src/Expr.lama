-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn    (v, x)        -> 
    case eval (c, v) of
      [c, v] ->
        case eval (c, x) of
          [[s, w], x] -> [[s <- [v, x], w], x]
        esac
    esac
  | Seq     (e1, e2)        -> 
    case eval (c, e1) of
      [c, _] -> eval (c, e2)
    esac
  | Skip                    -> [c, None]
  | Read    (v)             ->
    case readWorld (w) of
      [x, w] -> [[s <- [v, x], w], None]
    esac
  | Write   (e)             ->
    case eval (c, e) of
      [[s, w], e] -> [[s, writeWorld (e, w)], None]
    esac
  | If      (cond, t, f)    ->
    case eval (c, cond) of
      [c, 1] -> eval (c, t)
    | [c, 0] -> eval (c, f)
    esac
  | While   (cond, body)    -> 
    case eval (c, cond) of
      [c, 1] ->
        case eval (c, body) of
          [c, _] -> eval (c, expr)
        esac
    | [c, 0] -> [c, None]
    esac
  | DoWhile (body, cond)    -> 
    case eval (c, body) of
      [c, _] ->
        case eval (c, cond) of
          [c, 1] -> eval (c, expr)
        | [c, 0] -> [c, None]
        esac
    esac
  | Var     (v)             -> [c, s (v)]
  | Ref     (v)             -> [c, v]
  | Const   (cnst)          -> [c, cnst]
  | Binop   (op, e1, e2)    -> 
    case eval (c, e1) of 
      [c, e1] -> 
        case eval (c, e2) of 
          [c, e2] -> [c, evalOp (op, e1, e2)]
        esac
    esac
  | Ignore  (e)             -> 
    case eval (c, e) of
      [c, _] -> [c, None]
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
