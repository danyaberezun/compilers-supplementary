-- Expression evaluator

import List;
import Array;
import State;
import World;
import Lexer;
import Parser;
import Fun;
import Builtins;

var debug = false;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Set     (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        if      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)               |
--        String  (string)             |
--        Array   (expr list)          |
--        Elem    (expr, expr)         |
--        ElemRef (expr, expr)         |
--        Builtin (string, expr list)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun lookupVal (state, name) {
  case state.lookup (name) of
    x@Val (_) -> x
  | _         -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun lookupFun (state, name) {
  case state.lookup (name) of
    x@Fun (_, _) -> x
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Val (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun unwrapArg (arg) {
  case arg of
    Val (v) -> v
  | _ -> failure ("Cannot unwrap %s\n", arg.string)
  esac
}

fun unwrapArgs (args) { map(unwrapArg, args) }

fun eval (c@[s, w], expr) {
    if debug then printf("Evaluating %s\n", expr.string) fi;
    case expr of
      -- Var (v) -> printf ("Reading var %s -> %s\n", v, s.lookup (v).string); [c, Val (s.lookup (v))]
      Var (v) -> [c, Val (s.lookup (v))]
    | Const (v) -> [c, Val (v)]
    | Ref (x) -> [c, Ref (x)]
    | Seq (e1, e2) ->
      case eval (c, e1) of
        [cn, _] -> eval (cn, e2)
      esac
    | Ignore (e) ->
      case eval (c, e) of
        [cn, _] -> [cn, Void]
      esac
    | Skip -> [c, Void]
    | Assn (ex, ev) ->
      case eval (c, ex) of
        [cn, Ref (x)] ->
	  case eval (cn, ev) of
	    [[sn, wn], Val (v)] ->
	      if debug then printf("Assigning var %s to %s\n", x.string, v.string) fi;
	      [[sn <- [x, v], wn], Val (v)]
	  esac
      | [cn, ElemRef (x, i)] ->
	  case eval (cn, ev) of
	    [[sn, wn], Val (v)] ->
	      if debug then printf("Assigning elem %s of %s to %s\n", i.string, x.string, v.string) fi;
	      x[i] := v;
	      [[sn, wn], Val (v)]
	  esac
      esac
    | Binop (op, el, er) ->
      case eval (c, el) of
        [cn, Val (vl)] ->
	  case eval (cn, er) of
	    [cnn, Val (vr)] -> [cnn, Val (evalOp (op, vl, vr))]
	  esac
      esac
    | While (e,  s) ->
      while case eval (c, e) of [cn, Val (v)] -> c := cn; v esac do
      	    c := eval (c, s).fst
      od;
      -- printf ("Whil ended\n");
      [c, Void]
    | DoWhile (s, e) ->
      do
        c := eval (c, s).fst
      while case eval (c, e) of [cn, Val (v)] -> c := cn; v esac od;
      [c, Void]
    | If (e, s1, s2) ->
      if case eval (c, e) of [cn, Val (v)] -> c := cn; v esac then
         eval (c, s1)
      else
         eval (c, s2)
      fi
    | Scope (v, sts) ->
      fun addSymbols (s, v) {
      	  case v of
	    {} -> s
	  | vf:vrest ->
	    case vf of
	      Var (vr) -> addNames (addSymbols(s, vrest), vr)
	    | Fun (name, args, body) -> addFunction (addSymbols(s, vrest), name, args, body)
	    esac
	  esac
      }
      var sn = addSymbols (enterScope (s), v);
      case eval([sn, w], sts) of [[s, w], v] ->
        [[leaveScope (s), w], v]
      esac
    | Call (f, argExprs) ->
    case lookup (s, f) of Fun (args, body) ->
    	 case evalList (c, argExprs) of [[st, w], argVals] ->
	      fun addArgs (s, args) {
	      	  case args of
		    -- {} -> printf ("\n"); s
		    {} -> s
		  | [a, Val (v)] : argsRest ->
		    -- printf (" %s = %s ", a.string, v.string);
		    addArgs (s, argsRest).addName (a, v)
		  esac
	      }
	      if debug then printf ("Calling function %s with arg vals: %s\n", f, argVals.string) fi;

	      case body of
	        External -> case evalBuiltin (f, unwrapArgs (argVals), w) of [v, w] -> [[s, w], Val (v)] esac
	      | _ -> case eval ([enterFunction (s).addArgs (zip (args, argVals)), w], body) of [[s, w], v] ->
	      	     -- printf ("Returning %s\n", v.string);
	      	     [[leaveFunction (st, getGlobal (s)), w], v]
	        esac
	      esac

	 esac
    esac
    | Set (ex, ev) -> eval(c, Assn (Ref (ex), ev))
    | String (x) -> [c, Val (x)]
    | Elem (ex, ei) ->
      case eval (c, ex) of [c, Val (x)] ->
        case eval (c, ei) of [c, Val (i)] ->
	  [c, Val (case x of
            Sexp (_, x) -> x
	  | x -> x
          esac[i])]
	esac
      esac
    | ElemRef (ex, ei) ->
      case eval (c, ex) of [c, Val (x)] ->
        case eval (c, ei) of [c, Val (i)] ->
	  [c, ElemRef (x, i)]
	esac
      esac
    | Array (exs) ->
      case evalList (c, exs) of [c, xs] ->
        [c, Val (listArray (unwrapArgs (xs)))]
      esac
    | Sexp (name, exs) -> 
      case evalList (c, exs) of [c, xs] ->
        [c, Val (Sexp (name, listArray (unwrapArgs (xs))))]
      esac
    | _ -> failure ("evalExpr not implemented: %s\n", expr.string)
    esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState ().enterScope.addName ("read",   Fun ({}, External))
                                      .addName ("write",  Fun ({"a"}, External))
                                      .addName ("length", Fun ({"a"}, External)), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
