-- X86 codegeneration interface
-- We use stack machine programs as intermediate representation
import SM;
import Collection;
import List;
import Buffer;
import Fun;

-- Assembler language interface
-- The registers: 
var regs = ["%ebx", "%ecx", "%esi", "%edi", "%eax", "%edx", "%ebp", "%esp"];

-- We can not freely operate with all register; only with 4 by now
var nRegs = regs.length - 5;

-- For convenience we define the following synonyms for the registers: 
var ebx = R (0),
    ecx = R (1),
    esi = R (2),
    edi = R (3),
    eax = R (4),
    edx = R (5),
    ebp = R (6),
    esp = R (7);

-- We need to know the word size to calculate offsets correctly
var wordSize = 4;

-- We need to distinguish the following operand types: 
--    R (int)    -- hard register                    
--    S (int)    -- a position on the hardware stack 
--    M (string) -- a named memory location          
--    L (int)    -- an immediate operand             

-- Some x86 instruction (we do not need all of them):
--   Mov   (opnd, opnd)         -- copies a value from the first to the second operand  
--   Binop (string, opnd, opnd) -- makes a binary operation; note, the first operand   
--                                 designates x86 operator, not the source language one
--   IDiv  (opnd)               -- x86 integer division, see instruction set reference 
--   Cltd                       -- see instruction set reference           
--   Set   (string, string)     -- sets a value from flags; the first operand is the   
--                                 suffix, which determines the value being set, the
--                                 the second --- (sub)register name 
--   Push  (opnd)               -- pushes the operand on the hardware stack
--   Pop   (opnd)               -- pops from the hardware stack to the operand
--   Call  (string)             -- calls a function by its name 
--   Ret                        -- returns from a function
--   Meta  (string)             -- metainformation (declarations, etc.)
                                                               
-- Machine instruction printer
fun insnString (insn) {
  
  fun binopString (op) {
    case op of
      "+"   -> "addl"
    | "-"   -> "subl"
    | "*"   -> "imull"
    | "&&"  -> "andl"
    | "!!"  -> "orl" 
    | "^"   -> "xorl"
    | "cmp" -> "cmpl"
    esac
  }

  fun opndString (opnd) {
    case opnd of
      R (i) -> regs [i]
    | S (i) -> sprintf ("-%d(%%ebp)", (i+1) * wordSize)
    | M (x) -> x
    | L (i) -> sprintf ("$%d", i)
    esac
  }
  
  case insn of
    Cltd               -> "\tcltd\n"
  | Set   (suf, s)     -> sprintf ("\tset%s\t%s\n", suf, s)
  | IDiv  (s1)         -> sprintf ("\tidivl\t%s\n", opndString (s1))
  | Binop (op, s1, s2) -> sprintf ("\t%s\t%s,\t%s\n", binopString (op), opndString (s1), opndString (s2))
  | Mov   (s1, s2)     -> sprintf ("\tmovl\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Push  (s)          -> sprintf ("\tpushl\t%s\n", opndString (s))
  | Pop   (s)          -> sprintf ("\tpopl\t%s\n", opndString (s))
  | Ret                -> "\tret\n"
  | Call  (p)          -> sprintf ("\tcall\t%s\n", p)
  | Meta  (m)          -> m
  esac
}

-- Environment for symbolic interpreter
-- An environment holds
--    a symbolic stack
--    a maximal stack depth reached so far
--    a set of global variable names

fun makeEnv (stack, stackSlots, globals) {
  -- Returns an internal name for a global variable
  fun globalName (name) {
    "global_" ++ name
  }

  -- Returns a string representation of the environment
  fun envString () {
    sprintf ("Stack     : %s\nStackSlots: %d\nGlobals   : %s\n", stack.string, stackSlots, elements (globals).string)
  }
  
  -- Allocates a new position on the symbolic stack;
  -- returns a pair: a location for allocated item and
  -- an updated environment
  fun allocate () {
    case 
      case stack of
        {}        -> [ebx, 0]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> if n < nRegs then [R (n+1), stackSlots] else [S (0), 1] fi
      | _         -> [S (0), 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals)]esac
  }

  -- Pushes an item on the symbolic state; returns an updated envirtonment
  fun push (y) {
    makeEnv (y : stack, stackSlots, globals)
  }

  -- Pops one item from the symbolic stack; returns a pair: a popped
  -- item and an updated environment
  fun pop () {
    case stack of
      x : stack -> [x, makeEnv (stack, stackSlots, globals)]
    esac
  }

  -- Pops two items from the symbolic stack; returns a triple:
  -- popped items and an undated environment
  fun pop2 () {
    case stack of
      x : y : stack -> [x, y, makeEnv (stack, stackSlots, globals)]
    esac
  }

  -- Adds a global variable; returns an updated environment
  fun addGlobal (name) {
    makeEnv (stack, stackSlots, addSet (globals, globalName (name)))
  }

  -- References a global variable
  fun loc (name) {
    M (globalName (name))
  }
  
  -- Gets a list of global variables from the environment
  fun getGlobals () {
    globals.elements
  }

  -- Gets a maximal stack size from the environment
  fun getStackSize () {
    stackSlots
  }

  [envString, allocate, push, pop, pop2, addGlobal, loc, getGlobals, getStackSize]
}

-- Exported accessors
fun envString (env) {
  env [0] ()
}

fun allocate (env) {
  env [1] ()
}

fun push (env, x) {
  env [2] (x)
}

fun pop (env) {
  env [3] ()
}

fun pop2 (env) {
  env [4] ()
}

fun addGlobal (env, name) {
  env [5] (name)
}

fun loc (env, name) {
  env [6] (name)
}

fun getGlobals (env) {
  env [7] ()
}

fun getStackSize (env) {
  env [8] ()
}

-- Creates an initial environment
fun initEnv () {
  makeEnv (0, 0, emptySet (compare))
}

-- Codegeneration helper functions
-- Generates code section
fun codeSection (text) {
  singletonBuffer (Meta ("\t.text\n")) <+> text
}

-- Generates data section
fun dataSection (text) {
  singletonBuffer (Meta ("\t.data\n")) <+> text
}

-- Generates data definition
fun dataDef (name) {
  Meta (sprintf ("%s:\t.int\t0\n", name))
}

-- Generates function prologue
fun prologue (size) {
  singletonBuffer (Push (ebp)) <+
  Mov   (esp, ebp) <+
  Binop ("-", L (wordSize*size), esp)
}

-- Generates function epilogue
fun epilogue () {
  singletonBuffer (Mov (ebp, esp)) <+
  Pop   (ebp) <+
  Binop ("^", eax, eax) <+
  Ret
}

-- Checks if an operand resides on a stack
fun stackOpnd (opnd) {
  case opnd of
    S (_) -> true
  | _     -> false
  esac      
}

-- Checks if an operand resides in memory
fun memOpnd (opnd) {
  case opnd of
    S (_) -> true
  | M (_) -> true
  | _     -> false
  esac
}

-- Generates a move between locations, using
-- intermediate register if needed
fun move (from, to) {
  if memOpnd (from) && memOpnd (to)
  then singletonBuffer (Mov (from, eax)) <+ Mov (eax, to)
  else singletonBuffer (Mov (from, to))
  fi
}

-- Gets a suffix for Set instruction from
-- source language comparison operator
fun suffix (op) {
  case op of
    "<"  -> "l"
  | "<=" -> "le"
  | "==" -> "e"
  | "!=" -> "ne"
  | ">=" -> "ge"
  | ">"  -> "g"
  esac
}

fun binopCompile (op, e1, e2) {
   case op of
     "+" -> move (e1, eax)
       <+> singletonBuffer (Binop (op, eax, e2)) |
     "-" -> move (e1, eax)
       <+> singletonBuffer (Binop (op, e2, eax)) <+> move (eax, e2) |
     "*" -> move (e1, eax)
       <+> singletonBuffer (Binop (op, eax, e2)) |
     "/" -> move (e1, eax) <+ Cltd <+ IDiv(e2) <+> move (eax, e2) |
     "%" -> move (e1, eax) <+ Cltd <+ IDiv(e2) <+> move (edx, e2) |
     "<" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     ">" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     "<=" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     ">=" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     "==" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     "!=" -> singletonBuffer (Binop ("cmp", e2, e1))
       <+ Set (suffix (op), "%al") <+> move (eax, e2) |
     "&&" -> move (e1, eax)
       <+> singletonBuffer (Binop (op, eax, e2)) |
     "!!" -> move (e1, eax)
       <+> singletonBuffer (Binop (op, eax, e2))
   esac
 }

-- Compiles stack machine code into a list of x86 instructions. Takes an environment
-- and stack machine code, returns an updated environment and x86 code.
public fun compileRead (env, code) {
  case env.allocate of
    [s, env] -> [env, code <+ Call ("Lread") <+ Mov (eax, s)]
  esac
}

public fun compileWrite (env, code) {
  case env.pop of
    [s, env] -> [env, code <+ Push (s) <+ Call ("Lwrite") <+ Pop (eax)]
  esac
}

public fun compileConst (env, code, n) {
  case env.allocate of
    [s, env] -> [env, code <+> move (L (n), s)]
  esac
}

public fun compileLD (env, code, x) {
  case env.addGlobal (x).allocate of
    [s, env] -> [env, code <+> move (env.loc (x), s)]
  esac
}

public fun compileST (env, code, x) {
  case env.addGlobal (x).pop of
    [s, env] -> [env, code <+> move (s, env.loc (x))]
  esac
}

public fun compileBinop (env, code, op) {
  case env.pop2 of
    [e2, e1, env] -> [env.push(e2), code <+> binopCompile(op, e1, e2)]
  esac
}

fun compile (env, code) {
  foldl (
    fun ([env, scode], i) {
      var code = scode <+ Meta ("# " ++ showSMInsn (i) ++ "\n");
      case i of
        Read       -> compileRead (env, code) |           
        Write      -> compileWrite (env, code) |
        Const (n)  -> compileConst (env, code, n) |
        LD (x)     -> compileLD (env, code, x) |
        ST (x)     -> compileST (env, code, x) |
        Binop (op) -> compileBinop (env, code, op) |
        _          -> failure ("codegeneration for instruction %s is not yet implemented\n", i.string)
      esac
    }, [env, emptyBuffer ()], code)
}

-- A top-level codegeneration function. Takes a stack machine program
-- and returns x86 listing as a string
public fun compileX86 (code) {
  case compile (initEnv (), code) of
    [env, code] ->
       map (insnString, 
            getBuffer $
              singletonBuffer (Meta ("\t.global\tmain\n")) <+>
              dataSection (listBuffer $ map (dataDef, getGlobals (env))) <+>
              codeSection (
                singletonBuffer (Meta ("main:\n")) <+>
                prologue (getStackSize (env)) <+>
                code <+>
                epilogue ()
              )
           ).stringcat
  esac
}
