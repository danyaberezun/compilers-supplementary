-- X86 codegeneration interface
-- We use stack machine programs as intermediate representation
import Fun;
import SM;
import Collection;
import List;
import Manifest;
import Buffer;

-- Assembler language interface
-- The registers: 
var regs = ["%ebx", "%ecx", "%esi", "%edi", "%eax", "%edx", "%ebp", "%esp"];

-- We can not freely operate with all register; only with 4 by now
var nRegs = regs.length - 5;

-- For convenience we define the following synonyms for the registers: 
var ebx = R (0),
    ecx = R (1),
    esi = R (2),
    edi = R (3),
    eax = R (4),
    edx = R (5),
    ebp = R (6),
    esp = R (7);

-- We need to know the word size to calculate offsets correctly
var wordSize = 4;

-- We need to distinguish the following operand types: 
--    R (int)       -- hard register                    
--    S (int)       -- a position on the hardware stack 
--    M (string)    -- a named memory location          
--    L (int)       -- an immediate operand
--    I (int, opnd) -- an indirect operand with offset

-- Some x86 instruction (we do not need all of them):
--   Mov   (opnd, opnd)         -- copies a value from the first to the second operand  
--   Lea   (opnd, opnd)         -- loads an address of the first operand into the second
--   Binop (string, opnd, opnd) -- makes a binary operation; note, the first operand   
--                                 designates x86 operator, not the source language one
--   IDiv  (opnd)               -- x86 integer division, see instruction set reference 
--   Cltd                       -- see instruction set reference           
--   Set   (string, string)     -- sets a value from flags; the first operand is the   
--                                 suffix, which determines the value being set, the
--                                 the second --- (sub)register name
--   Jmp   (string)             -- unconditional jump to a label
--   CJmp  (string, string)     -- conditional jump to a label
--   Label (string)             -- a label
--   Push  (opnd)               -- pushes the operand on the hardware stack
--   Pop   (opnd)               -- pops from the hardware stack to the operand
--   Call  (string)             -- calls a function by its name 
--   Ret                        -- returns from a function
--   Meta  (string)             -- metainformation (declarations, etc.)
                                                               
-- Machine instruction printer
fun insnString (insn) {
  
  fun binopString (op) {
    case op of
      "+"   -> "addl"
    | "-"   -> "subl"
    | "*"   -> "imull"
    | "&&"  -> "andl"
    | "!!"  -> "orl" 
    | "^"   -> "xorl"
    | "cmp" -> "cmpl"
    esac
  }

  fun opndString (opnd) {
    case opnd of
      R (i)    -> regs [i]
    | S (i)    ->  if i >= 0
                   then sprintf ("-%d(%%ebp)", (i+1) * wordSize)
                   else sprintf ("%d(%%ebp)", 8 + (-1-i) * wordSize)
                   fi

    | M (x)    -> x
    | L (i)    -> sprintf ("$%d", i)
    | I (0, x) -> sprintf ("(%s)", opndString (x))
    | I (n, x) -> sprintf ("%d(%s)", n, opndString (x))
    esac
  }

  case insn of
    Cltd               -> "\tcltd\n"
  | Set   (suf, s)     -> sprintf ("\tset%s\t%s\n", suf, s)
  | IDiv  (s1)         -> sprintf ("\tidivl\t%s\n", opndString (s1))
  | Binop (op, s1, s2) -> sprintf ("\t%s\t%s,\t%s\n", binopString (op), opndString (s1), opndString (s2))
  | Lea   (s1, s2)     -> sprintf ("\tleal\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Mov   (s1, s2)     -> sprintf ("\tmovl\t%s,\t%s\n", opndString (s1), opndString (s2))
  | Push  (s)          -> sprintf ("\tpushl\t%s\n", opndString (s))
  | Pop   (s)          -> sprintf ("\tpopl\t%s\n", opndString (s))
  | Ret                -> "\tret\n"
  | Call  (p)          -> sprintf ("\tcall\t%s\n", p)
  | Label (l)          -> sprintf ("%s:\n", l)
  | Jmp   (l)          -> sprintf ("\tjmp\t%s\n", l)
  | CJmp  (c, l)       -> sprintf ("\tj%s\t%s\n", c, l)
  | Meta  (m)          -> m
  esac
}

-- Environment for symbolic interpreter
-- An environment holds
--    a symbolic stack
--    a maximal stack depth reached so far
--    a set of global variable names
--    a barrier condition (set up right after an unconditional jump is interpreter)
--    a stack map which maps labels to symbolic stacks

fun makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals) {
  -- Returns an internal name for a global variable
  fun globalName (name) {
    "global_" ++ name
  }

  -- Returns a string representation of the environment
  fun envString () {
    sprintf ("Stack     : %s\nStackSlots: %d\nGlobals   : %s\n", stack.string, stackSlots, elements (globals).string)
  }
  
  -- Allocates a new position on the symbolic stack;
  -- returns a pair: a location for allocated item and
  -- an updated environment
  fun allocate () {
    case 
      case stack of
        {}        -> [ebx, 0]
      | S (n) : _ -> [S (n+1), n+2]
      | R (n) : _ -> if n < nRegs then [R (n+1), stackSlots] else [S (nLocals), nLocals + 1] fi
      | _         -> [S (nLocals), nLocals + 1]
      esac
    of [x, n] -> [x, makeEnv (x : stack, if n > stackSlots then n else stackSlots fi, globals, barrier, stackMap, fLabel, nLocals)]esac
  }

  -- Pushes an item on the symbolic state; returns an updated envirtonment
  fun push (y) {
    makeEnv (y : stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)
  }

  -- Pops one item from the symbolic stack; returns a pair: a popped
  -- item and an updated environment
  fun pop () {
    case stack of
      x : stack -> [x, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Pops two items from the symbolic stack; returns a triple:
  -- popped items and an undated environment
  fun pop2 () {
    case stack of
      x : y : stack -> [x, y, makeEnv (stack, stackSlots, globals, barrier, stackMap, fLabel, nLocals)]
    esac
  }

  -- Peeks the top of the symbolic stack
  fun peek () {
    stack.fst
  }
  
  -- Adds a global variable; returns an updated environment
  fun addGlobal (name) {
    makeEnv (stack, stackSlots, addSet (globals, globalName (name)), barrier, stackMap, fLabel, nLocals)
  }

  -- References an allocation
  fun loc (alloc) {
    case alloc of
      Glb (x) -> M (globalName (x))
    | Arg (i) -> S (-1-i)
    | Loc (i) -> S (i)
    esac
  }
  
  -- Gets a list of global variables from the environment
  fun getGlobals () {
    globals.elements
  }

  -- Gets a maximal stack size from the environment
  fun getStackSize () {
    stackSlots
  }

  -- Gets barrier condition
  fun isBarrier () {
    barrier
  }

  -- Sets barrier condition
  fun setBarrier () {
    makeEnv (stack, stackSlots, globals, true, stackMap, fLabel, nLocals)
  }

  -- Associates the current symbolic state with a label
  fun setStack (l) {
    makeEnv (stack, stackSlots, globals, false, addMap (stackMap, l, stack), fLabel, nLocals)
  }

  -- Retrieves a stack for a label
  fun retrieveStack (l) {
    case findMap (stackMap, l) of
      None         -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    | Some (stack) -> makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nLocals)
    esac
  }

  -- Sets the label of current function
  fun enterFunction (fLabel, nl) {
    makeEnv (stack, stackSlots, globals, false, stackMap, fLabel, nl)
  }

  -- Gets the label of current function
  fun currentFunction () {
    fLabel
  }

  -- Gets the list of live registers past given symbolic stack depth
  fun liveRegisters (depth) {
    fun inner (d, acc, st) {
      case st of
        {}             -> acc
      | (r@R (_)) : tl -> inner (d+1, if d >= depth then r : acc else acc fi, tl)
      | _         : tl -> inner (d+1, acc, tl)
      esac
    }

    inner (0, {}, stack)
  }

   -- Gets a local static size for the current function
  fun staticSize () {
    nLocals + stackSlots
  }

  [envString,
   allocate,
   push,
   pop,
   pop2,
   addGlobal,
   loc,
   getGlobals,
   getStackSize,
   peek,
   isBarrier,
   setBarrier,
   setStack,
   retrieveStack,
   enterFunction,
   currentFunction,
   liveRegisters,
   staticSize]
}

-- Exported accessors
fun envString (env) {
  env [0] ()
}

fun allocate (env) {
  env [1] ()
}

fun push (env, x) {
  env [2] (x)
}

fun pop (env) {
  env [3] ()
}

fun pop2 (env) {
  env [4] ()
}

fun addGlobal (env, name) {
  env [5] (name)
}

fun loc (env, name) {
  env [6] (name)
}

fun getGlobals (env) {
  env [7] ()
}

fun getStackSize (env) {
  env [8] ()
}

fun peek (env) {
  env [9] ()
}

fun isBarrier (env) {
  env [10] ()
}

fun setBarrier (env) {
  env [11] ()
}

fun setStack (env, l) {
  env [12] (l)
}

fun retrieveStack (env, l) {
  env [13] (l)
}

fun enterFunction (env, fLabel, nL) {
  env [14] (fLabel, nL)
}

fun currentFunction (env) {
  env [15] ()
}

fun liveRegisters (env, depth) {
  env [16] (depth)
}

fun staticSize (env) {
  env [17] ()
}

-- Creates an initial environment
fun initEnv () {
  makeEnv (0, emptySet (compare), emptySet (compare), false, emptyMap (compare), "", 0)
}

-- Codegeneration helper functions
-- Generates code section
fun codeSection (text) {
  singletonBuffer (Meta ("\t.text\n")) <+> text
}

-- Generates data section
fun dataSection (text) {
  singletonBuffer (Meta ("\t.data\n")) <+> text
}

-- Generates data definition
fun dataDef (name) {
  Meta (sprintf ("%s:\t.int\t0\n", name))
}

-- Generates function prologue
fun prologue (fLabel) {
  singletonBuffer (Push  (ebp)) <+
  Mov   (esp, ebp) <+
  Binop ("-", M (sprintf ("$%s_SIZE", fLabel)), esp)  
}

-- Generates function epilogue
fun epilogue (env) {
  var metaDef = Meta (sprintf ("\t.set\t%s_SIZE,\t%d\n", env.currentFunction, env.staticSize * wordSize));

  if compare (env.currentFunction, "main") == 0
  then [env, singletonBuffer (Mov (ebp, esp)) <+ Pop (ebp) <+ Binop ("^", eax, eax) <+ Ret <+ metaDef]
  else case env.pop of
         [y, env] -> [env, singletonBuffer (Mov (y, eax)) <+ Mov (ebp, esp) <+ Pop (ebp) <+ Ret <+ metaDef]
       esac
  fi
}

-- Checks if an operand resides on a stack
fun stackOpnd (opnd) {
  case opnd of
    S (_) -> true
  | _     -> false
  esac      
}

-- Checks if an operand resides in memory
fun memOpnd (opnd) {
  case opnd of
    S (_) -> true
  | M (_) -> true
  | I (_, _) -> true
  | _     -> false
  esac
}

-- Generates a move between locations, using
-- intermediate register if needed
fun move (from, to) {
  if memOpnd (from) && memOpnd (to)
  then singletonBuffer (Mov (from, eax)) <+ Mov (eax, to)
  else singletonBuffer (Mov (from, to))
  fi
}

-- Gets a suffix for Set instruction from
-- source language comparison operator
fun suffix (op) {
  case op of
    "<"  -> "l"
  | "<=" -> "le"
  | "==" -> "e"
  | "!=" -> "ne"
  | ">=" -> "ge"
  | ">"  -> "g"
  esac
}

fun compileArgsPush (env, argsCount) {
  fun go (curEnv, curArgsCount, result) {
    case curArgsCount of
      0 -> [curEnv, result]
    | _ ->
        var popRes = pop (curEnv);
        var argMem = popRes.fst;
        var newEnv = popRes.snd;

        var newResult = result <+ Push (argMem);
        go (newEnv, curArgsCount - 1, newResult)
    esac
  }
  go (env, argsCount, emptyBuffer ())
}

fun processRegisters (regsList, action) {
  fun go (curRegsList, result) {
    case curRegsList of
      {} -> result
    | headReg : tailRegs ->
        var newResult = result <+ action (headReg);
        go (tailRegs, newResult, action)
    esac
  }
  go (regsList, emptyBuffer (), action)
}

fun compileSaveLiveRegisters (liveRegs) {
  processRegisters (liveRegs, fun (regName) { Push (regName) })
}

fun compileLoadLiveRegisters (liveRegs) {
  processRegisters (reverse (liveRegs), fun (regName) { Pop (regName) })
}

fun compileCall (env, functionLabel, argsCount) {
  var liveRegs = liveRegisters (env, argsCount);
  var saveRegsCode = compileSaveLiveRegisters (liveRegs);
  var loadRegistersCode = compileLoadLiveRegisters (liveRegs);

  var pushArgsRes = compileArgsPush (env, argsCount);
  var newEnv = pushArgsRes.fst;
  var pushArgsCode = pushArgsRes.snd;

  var allocRes = allocate (newEnv);
  var ansMem = allocRes.fst;
  var finalEnv = allocRes.snd;

  var resCode = saveRegsCode <+> 
                pushArgsCode <+
                Call (functionLabel) <+
                Binop ("+", L (wordSize * argsCount), esp) <+> 
                loadRegistersCode <+
                Mov (eax, ansMem);
  [finalEnv, resCode]
}

fun compileSimpleBinOp (op, x, y) {
  case y of
    R (_) -> singletonBuffer (Binop (op, x, y))
  | _ -> singletonBuffer (Mov (y, eax)) <+ 
         Binop (op, x, eax) <+ 
         Mov (eax, y)
  esac
}

fun compileDiv (op, x, y) {
  var resultReg = case op of
    "/" -> eax
  | "%" -> edx
  esac;
  singletonBuffer (Mov (y, eax)) <+
  Cltd <+ 
  IDiv (x) <+ 
  Mov (resultReg, y)
}

fun compileBoolConversion (x) {
  singletonBuffer (Binop ("^", edx, edx)) <+
  Binop ("cmp", L (0), x) <+
  Set ("ne", "%dl") <+
  Mov (edx, x)
}

fun compileBoolBinOp (op, x, y) {
  compileBoolConversion (x) <+>
  compileBoolConversion (y) <+>
  compileSimpleBinOp (op, x, y)
}

fun compileComparison (op, x, y) {
  singletonBuffer (Binop ("^", edx, edx)) <+
  Mov (x, eax) <+
  Binop ("cmp", eax, y) <+
  Set (suffix (op), "%dl") <+
  Mov (edx, y)
}

fun compileSingleInstruction (env, instr) {
  case instr of
    READ -> compileCall (env, "Lread", 0)
  | WRITE -> compileCall (env, "Lwrite", 1)
  (* Assignment *)

   -- Some guidelines for generating function calls:
   --
   -- 1. generate instructions to save live registers on the X86 stack (use
   --    env.liveRegisters (number of arguments);
   -- 2. generate instructions to move actual parameters from the symbolic
   --    stack to the hardware one;
   -- 3. generate the call itself;
   -- 4. discard the actual parameters from the stack;
   -- 5. restore saved live registers.
   --
   -- Some guidelines for generating functions:
   --
   -- 1. generate proper prologue for BEGIN instruction (use "prologue" helper); use
   --    env.enterFunction to create a proper environment;
   -- 2. generate epilogue for END instruction.
  | LD (varName) ->
      var allocateRes = allocate (env);
      var mem = allocateRes.fst;
      var newEnv = allocateRes.snd;

      var varLoc = loc (newEnv, varName);
      var newCode = move (varLoc, mem);
      [newEnv, newCode]
  | ST (varName) ->
      var mem = peek (env);
      var varLoc = loc (env, varName);
      var newCode = move (mem, varLoc);
      [env, newCode]
  | CONST (n) ->
      var allocateRes = allocate (env);
      var mem = allocateRes.fst;
      var newEnv = allocateRes.snd;

      var newCode = singletonBuffer (Mov (L (n), mem));
      [newEnv, newCode]
  | DROP ->
      var newEnv = pop (env).snd;
      [newEnv, emptyBuffer ()]
  | DUP ->
      var top = peek (env);
      var allocateRes = allocate (env);
      var mem = allocateRes.fst;
      var newEnv = allocateRes.snd;

      var newCode = move (top, mem);
      [newEnv, newCode]
  | LDA (varName) ->
      var allocateRes = allocate (env);
      var mem = allocateRes.fst;
      var newEnv = allocateRes.snd;

      var varAddr = loc (newEnv, varName);
      var newCode = singletonBuffer (Lea (varAddr, eax)) <+
                    Mov (eax, mem);
      [newEnv, newCode]
  | STI ->
      var popRes = pop2 (env);
      var value = popRes[0];
      var varAddr = popRes[1];
      var newEnv = popRes[2];

      var newCode = singletonBuffer (Mov (varAddr, edx)) <+>
                    move (value, I (0, edx));
      var finalEnv = push (newEnv, value);
      [finalEnv, newCode]
  | LABEL (label) ->
      var newEnv = if isBarrier (env) then
        retrieveStack (env, label)
      else
        env
      fi;
      var newCode = singletonBuffer (Label (label));
      [newEnv, newCode]
  | JMP (label) ->
      var newEnv = setStack (env, label).setBarrier;
      var newCode = singletonBuffer (Jmp (label));
      [newEnv, newCode]
  | CJMP (tp, label) ->
      var x86Tp = case tp of
        Z -> "e"
      | NZ -> "ne"
      esac;
      var popRes = pop (env);
      var mem = popRes.fst;
      var newEnv = popRes.snd;

      var finalEnv = setStack (newEnv, label);
      var newCode = singletonBuffer (Binop ("cmp", L (0), mem)) <+
                    CJmp (x86Tp, label);
      [finalEnv, newCode]
  | BINOP (op) ->
      var popRes = pop (env);
      var x = popRes.fst;
      var newEnv = popRes.snd;
      var y = peek (newEnv);

      var opCodeFun = case op of
        "+" -> compileSimpleBinOp
      | "-" -> compileSimpleBinOp
      | "*" -> compileSimpleBinOp
      | "&&" -> compileBoolBinOp
      | "!!" -> compileBoolBinOp
      | "/" -> compileDiv
      | "%" -> compileDiv
      | _ -> compileComparison
      esac;
      var newCode = opCodeFun (op, x, y);
      [newEnv, newCode]
  | GLOBAL (varName) ->
      var newEnv = addGlobal (env, varName);
      [newEnv, emptyBuffer ()]
  | BEGIN (functionName, _, localsCount) ->
      var newEnv = enterFunction (env, functionName, localsCount);
      var newCode = prologue (functionName);
      [newEnv, newCode]
  | END -> epilogue (env)
  | CALL (functionLabel, argsCount) -> compileCall (env, functionLabel, argsCount)
  (* End *)
  esac
}

fun compileFold ([env, scode], instr) {
  var code = scode <+ Meta ("# " ++ showSMInsn (instr) ++ "\n");
  var compileRes = compileSingleInstruction (env, instr);
  var newEnv = compileRes.fst;
  var newCode = compileRes.snd;
  var resCode = code <+> newCode;
  [newEnv, resCode]
}

-- Compiles stack machine code into a list of x86 instructions. Takes an environment
-- and stack machine code, returns an updated environment and x86 code.
fun compile (env, code) {
  foldl (compileFold, [env, emptyBuffer ()], code)
}

-- A top-level codegeneration function. Takes a driver's environment and a stack machine program,
-- compiles the program into machine code, and compiles the machine code into an executable
public fun compileX86 (args, code) {
  case compile (initEnv (), code) of
    [env, code] ->
       var asmFile = args.getBaseName ++ ".s",
             runtime = case getEnv ("LAMA_RUNTIME") of
                         #val -> "../runtime/"
                       | path     -> path
                       esac ++ "/runtime.o";

       fwrite (asmFile,
               map (insnString, 
                    getBuffer $
                      singletonBuffer (Meta ("\t.global\tmain\n")) <+>
                      dataSection (listBuffer $ map (dataDef, getGlobals (env))) <+>
                      codeSection (code)
                   ).stringcat);
                    
       system ({"gcc -g -m32 -o ", args.getBaseName, " ", runtime, " ", asmFile}.stringcat)
  esac
}
